//----------------------------------------------------------------------
// スクリプト : Event_Making v1.0
//----------------------------------------------------------------------

//----------------------------------
// 演劇の小道具
//----------------------------------

payon_in01.gat,99,72,4	script	おばあさん	103,{
	if(countitem(1049)<4) {
		mes "[おばあさん]";
		mes "毎日フェイヨンの裏の山に登り、";
		mes "茸狩りだけを一生懸命やって……。";
		mes "今月もいつのまに過ぎたか";
		mes "わからんのう。";
		next;
		mes "[おばあさん]";
		mes "……そういえば、来月は名節じゃな。";
		mes "今年の名節には、何がなんでも家族に";
		mes "新しい上着を作ってあげたい";
		mes "もんじゃ。それには上着の材料となる";
		mes "^3355FF「乙女の着物」^000000が四着ぐらい";
		mes "必要なんじゃが……。";
		next;
		mes "[おばあさん]";
		mes "ばばが青年さんに一つ頼みたい。";
		mes "もし、あちこち旅をする途中に";
		mes "^0000FF「乙女の着物」^000000というものを";
		mes "見つけたら、必ずもう一度ここに来て";
		mes "くだされ。ばばは大したお礼が";
		mes "できんが、恩恵は忘れないからね……。";
		close;
	}
	mes "[フェイヨンおばあさん]";
	mes "ホッホッ……今度の名節にうちの子達にあげる上着つくりに要る……";
	mes "青年の持っている^3355FF「乙女の裾」^000000をすこしだけ…四着だけ，分けてくれないかね？";
	mes "お願い，青年さんよ ..";
	next;
	if(select("渡す","断る")==2) {
		mes "[フェイヨンおばあさん]";
		mes "まいったね……このばばの力で^3355FF「乙女の裾」^000000をどうやって……";
		mes "目の前が真っ暗ね，真っ暗……";
		close;
	}
	if(checkweight(2293,1)==0) {
		mes "[フェイヨンおばあさん]";
		mes "重量オーバーです。";
		close;
	}
	delitem 1049,4;
	mes "[フェイヨンおばあさん]";
	mes "本当にありがとう〜青年さん。もう上着がつくれる〜";
	mes "ほっほっ……あっ、そうだ。これは私が若い頃，人気あった時代に演劇をやってたけどね……";
	mes "結婚することになって，劇団を出る当時に持ってきた劇団のマスコートみたいなモンだよ……";
	next;
	getitem 2293,1;
	mes "[フェイヨンおばあさん]";
	mes "これでも良かったら…どうぞ";
	mes "元気で〜";
	close;
}

//----------------------------------
// 溶接マスク
//----------------------------------

geffen_in.gat,144,166,4	script	鍛冶屋さん	63,{
	mes "[鍛冶屋さん]";
	mes "この俺様が、ベテラン鍛冶屋だぜ！";
	mes "真っ赤に溶けてしまうような";
	mes "熱気の中で過ごすようになって";
	mes "30年ぐらい経つぜ！";
	next;
	mes "[鍛冶屋さん]";
	mes "長い鍛冶屋生活の間に";
	mes "一ついいものを発明したんだぜ！";
	mes "詳しく聞きたいか？";
	next;
	if(select("教えてください","別に必要ないです")==2) {
		mes "[鍛冶屋さん]";
		mes "力抜けること言うね……";
		mes "もう言うことはない、行ってくれ！";
		close;
	}
	mes "[鍛冶屋さん]";
	mes "想像もできない程の高熱で鋼鉄を溶かし";
	mes "様々な金属のアイテムを作り出す";
	mes "俺たち鍛冶屋は";
	mes "いつも精錬中にすごい熱気と";
	mes "真っ赤に溶けた鉄を相手にするから";
	mes "肌、特に顔がもたないぜ！";
	next;
	mes "[鍛冶屋さん]";
	mes "しかし、俺様が発明した";
	mes "このすげぇアイテムさえあれば";
	mes "そんな心配とはバイバイだぜ！";
	mes "その名も……^3355FF「溶接マスク」^000000!!";
	next;
	mes "[鍛冶屋さん]";
	mes "赤々と燃える炎から、君の白いツルツル";
	mes "スベスベな肌を守ってくれる";
	mes "超必須アイテムだぜっ！";
	mes "どう？^2244FF「鋼鉄 50個」^000000と";
	mes "たったの^4422FF「2000 Zeny」^000000で";
	mes "作ってあげるぜ！";
	next;
	if(select("いいですね、一つお願いします","意味のないもののようですね")==2) {
		mes "[鍛冶屋さん]";
		mes "エエッ！こんな";
		mes "素晴らしいアイテムを断るとは……";
		mes "物を見る目がないぜ！";
		mes "あんまり自分の肌を";
		mes "過信するもんじゃないぜ！";
		close;
	}
	if(countitem(999)<50 || Zeny<2000) {
		mes "[鍛冶屋さん]";
		mes "こら、鋼鉄や費用が足りないぜ！";
		mes "俺にどうしろっていうんだ？";
		mes "君、「不完全燃焼」って";
		mes "どんな意味か分かるか？";
		mes "君みたいな人間にピッタリだぜ！";
		mes "今すぐ鋼鉄と金を手に入れて来い！";
		close;
	}
	mes "[鍛冶屋さん]";
	mes "気に入ったよ、君！";
	mes "今すぐ作ってあげるぜ！！";
	delitem 999,50;
	set Zeny,Zeny-2000;
	next;
	mes "[鍛冶屋さん]";
	mes "こいつさえあれば";
	mes "君は高熱の魅力を";
	mes "感じることができるぜ！";
	mes "お客さん♪";
	getitem 2292,1;
	close;
}

//----------------------------------
// 耳当て
//----------------------------------

payon_in01.gat,18,10,4	script	不思議な乙女	76,{
	mes "[不思議な乙女]";
	mes "……。";
	mes "我が家では代々、寒い冬にも";
	mes "耐えられるように考案された保温用の";
	mes "^3355FF「耳当て」^000000を作って売っております。";
	mes "しかし、ここは暖かい地域である為、";
	mes "鋭い程の寒さが分からない人ばかり。";
	next;
	mes "[不思議な乙女]";
	mes "それ故、収入が少なくて、";
	mes "生活するのでさえ厳しいんです……。";
	mes "もし、あなたが寒いところへ";
	mes "旅立つのなら、";
	mes "私が作る^3355FF「耳当て」^000000は";
	mes "いかがでしょうか？";
	next;
	mes "[不思議な乙女]";
	mes "……「耳当て」は、私の家の生活名品";
	mes "でございます。^FF3355「呪われたルビー」^000000と";
	mes "^3311AA「ヘッドフォン」^000000を一つずつと";
	mes "^3355FF「やわらかな毛 200個」^000000、そして、";
	mes "^DDDD00「5000 Zeny」^000000で";
	mes "作らせて頂きます。";
	next;
	if(select("では、お願いします","結構です")==2) {
		mes "[不思議な乙女]";
		mes "……";
		mes "縁があれば、また会えるでしょう……。";
		mes "……。";
		close;
	}
	if(countitem(724)<1 || countitem(949)<200 || countitem(5001)<1 || Zeny<5000) {
		mes "[不思議な乙女]";
		mes "……お客さん、";
		mes "お持ちのアイテムだけでは、";
		mes "材料が足りませんね……。";
		mes "出来るだけ安い費用と負担のない";
		mes "条件で作らせて頂きたいですが……";
		mes "私達も厳しい状況でして……。";
		close;
	}
	mes "[不思議な乙女]";
	mes "……";
	mes "ありがとうございます。これ…作っておいた余分の耳当てを差し上げます。";
	delitem 724,1;
	delitem 949,200;
	delitem 5001,1;
	set Zeny,Zeny-5000;
	getitem 2283,1;
	next;
	mes "[不思議な乙女]";
	mes "……";
	mes "我が家の物品をご購入頂き、ありがとうございます。性能については絶対後悔なさることはございません…";
	close;
}

//----------------------------------
// 鹿の角
//----------------------------------

alberta_in.gat,122,53,4	script	青年	47,{
	mes "[チェロキ]";
	mes "僕は動物の角コレクターです！";
	mes "動物の角は本当にすばらしいですね。";
	mes "とっても上品です……。適当に加工して";
	mes "直接頭にかぶってもいいですし。";
	mes "また、家の中に飾っておく等、";
	mes "インテリアとしても価値が高いです。";
	next;
	mes "[チェロキ]";
	mes "ちょっと別の話になるけど、";
	mes "^FF3355「悪魔の角」^000000と";
	mes "いう物は非常に手に入れるのが";
	mes "難しいです。動物の角とは";
	mes "かけ離れた収集品といっても、";
	mes "すごく欲しいものなんです。";
	next;
	mes "[チェロキ]";
	mes "もしかして^FF3355「悪魔の角」^000000を";
	mes "持ってますか？";
	mes "もし20本ぐらい持ってましたら……";
	mes "僕が今までコレクターをしながら、";
	mes "一生懸命集めた^3355FF「鹿の角」^000000と";
	mes "交換してあげます。";
	next;
	if(select("交換しましょう","別に欲しくありません")==2) {
		mes "[チェロキ]";
		mes "そうですか……。いつか 準備が";
		mes "できましたら、またお訪ねください。";
		mes "あなたの旅に幸福が満ちるように……。";
		close;
	}
	if(countitem(923)<20) {
		mes "[チェロキ]";
		mes "まだ^FF3355「悪魔の角 20本」^000000の";
		mes "準備ができていないようですね。";
		close;
	}
	if(checkweight(2284,1)==0) {
		mes "[チェロキ]";
		mes "重量オーバーです。";
		close;
	}
	mes "[チェロキ]";
	mes "ウワー本物の^FF3355「悪魔の角」^000000ですね!!";
	mes "ありがとうございます！僕が大切にしてた鹿の角を差し上げます！";
	delitem 923,20;
	getitem 2284,1;
	next;
	mes "[チェロキ]";
	mes "これで僕はもう一度、本当のコレクターとしての一歩を踏み出すようになりました。ありがとうございます！";
	mes "いつも幸福が満ちるように…";
	close;
}

//----------------------------------
// 停止表示板
//----------------------------------

morocc_in.gat,142,100,4	script	ステアー	89,{
	mes "[ステアー]";
	mes "いらっしゃいませ。";
	mes "我がマックケラーン";
	mes "ステアー家自慢の";
	mes "^3355FF「停止表示板」^000000商店へのご訪問を";
	mes "歓迎いたします。";
	next;
	mes "[ステアー]";
	mes "ご存じかどうか知りませんが、";
	mes "我が家は、馬車が走っていた大昔から";
	mes "250年間交通表示板を専門として";
	mes "製作してきました。最近はお飾り";
	mes "としての機能を持つ^3355FF「停止表示板」^000000を";
	mes "請負作製しております。";
	next;
	if(select("^3355FF「停止表示板」^000000を注文","対話終了")==2) {
		mes "[ステアー]";
		mes "我がマックケラーン ステアー家を";
		mes "お訪ね頂き、まことに";
		mes "ありがとうございます。";
		mes "お宅のご家庭にいつも幸運が";
		mes "満ちますように……。";
		close;
	}
	mes "[ステアー]";
	mes "250年の伝統ある我が";
	mes "マックケラーン ステアー家の";
	mes "目立つ上に耐久力を兼ね備えた";
	mes "交通表示板の技術力をそのまま";
	mes "受け継いだ^3355FF「停止表示板」^000000アクセサリー";
	mes "バージョンは、ご注文なさるお客様を";
	next;
	mes "[ステアー]";
	mes "満足させること間違いないでしょう。";
	mes "^3355FF「停止表示板」^000000を注文するためには、";
	mes "^3355FF「91100 Zeny」^000000と";
	mes "^3355FF「木屑 50個」^000000、そして";
	mes "^3355FF「黒色の染料 1個」^000000が必要です。";
	next;
	if(countitem(1019)<50 || countitem(983)<1 || Zeny<91100) {
		mes "[ステアー]";
		mes "ご注文に必要な物品が不足している";
		mes "のではないでしょうか？";
		mes "もう一度ご確認下さい。";
		close;
	}
	if(checkweight(2272,1)==0) {
		mes "[ステアー]";
		mes "重量オーバーです。";
		close;
	}
	mes "[ステアー]";
	mes "これ！250年伝統に輝くマックケラーン ステアー^3355FF「停止表示板」^000000です！";
	mes "ご利用頂きありがとうございます。又いらして下さい！";
	delitem 1019,50;
	delitem 983,1;
	set Zeny,Zeny-91100;
	getitem 2272,1;
	close;
}

//----------------------------------
// 医者のヘアバンド
//----------------------------------

aldeba_in.gat,152,166,4	script	ある商人	86,{
	mes "[ある商人]";
	mes "ははは！";
	mes "俺がまさに噂の謎の商人だぜ！";
	mes "このミッドガルド中を旅している！";
	mes "さあ……珍しい物品を売ってるから、";
	mes "一度見てみるのはどうかい？";
	next;
	mes "[ある商人]";
	mes "(1)^3355FF医者のヘアバンド^000000 : ";
	mes "バンダナ 1個 + 鉄 50個 + ";
	mes "損傷したダイヤモンド + 3500 Zeny";
	mes "(2)^3355FF羽毛帽子^000000 : ";
	mes "オールドスターローマンス 1個";
	mes "+ 鳥の羽毛 300個 + 500 Zeny";
	next;
	mes "[ある商人]";
	mes "(3)^3355FFオペラ仮面^000000 : ";
	mes "鋼鉄 20個 + 歌う草 1個 + 5000 Zeny";
	mes "(4)^3355FF笠^000000 : ";
	mes "木屑 120個 + 10000 Zeny";
	next;
	switch (select("医者のヘアバンド","羽毛帽子","オペラ仮面","笠")) {
	case 1:
		if(countitem(2275)<1 || countitem(998)<50 || countitem(733)<1 || Zeny<3500) {
			mes "[ある商人]";
			mes "こら、俺が言った必要品目をよ〜く";
			mes "確認したまえ。";
			mes "若者、頭が悪いようだな……。";
			mes "しっかりした方がいいぜ、うん？";
			close;
		}
		mes "[ある商人]";
		mes "うーむ！君…医師免許は";
		mes "持ってるのか？";
		mes "まあ、ブラックヒャックという";
		mes "奴ほどの実力があれば";
		mes "免許がなくても関係ないけど。";
		mes "ま、いいだろ。さあ持っていけ！";
		delitem 2275,1;
		delitem 998,50;
		delitem 733,1;
		set Zeny,Zeny-3500;
		getitem 2273,1;
		close;
	case 2:
		if(countitem(2247)<1 || countitem(916)<300 || Zeny<500) {
			mes "[ある商人]";
			mes "こら、俺が言った必要品目をよ〜く";
			mes "確認したまえ。";
			mes "若者、頭が悪いようだな……。";
			mes "しっかりした方がいいぜ、うん？";
			close;
		}
		mes "[ある商人]";
		mes "フフ。センスあるね、君。";
		mes "材料を求めるのがきつそうだったろうが、やり甲斐のあるほどのデザインではないか。さあ、持っていけ…";
		delitem 2247,1;
		delitem 916,300;
		set Zeny,Zeny-500;
		getitem 5018,1;
		close;
	case 3:
		if(countitem(999)<20 || countitem(707)<1 || Zeny<5000) {
			mes "[ある商人]";
			mes "こら、俺が言った必要品目をよ〜く";
			mes "確認したまえ。";
			mes "若者、頭が悪いようだな……。";
			mes "しっかりした方がいいぜ、うん？";
			close;
		}
		mes "[ある商人]";
		mes "これはちょっといい物だ。";
		mes "嫌な感じが少しあるが……それなりに";
		mes "ユニークな感じがいいと思うよ。";
		mes "君はどうかい？気に入ったようだな。";
		mes "……さ、持っていけ！";
		delitem 999,20;
		delitem 707,1;
		set Zeny,Zeny-5000;
		getitem 2281,1;
		close;
	case 4:
		if(countitem(1019)<120 || Zeny<10000) {
			mes "[ある商人]";
			mes "こら、俺が言った必要品目をよ〜く";
			mes "確認したまえ。";
			mes "若者、頭が悪いようだな……。";
			mes "しっかりした方がいいぜ、うん？";
			close;
		}
		mes "[ある商人]";
		mes "フェイヨンという村に行けば、笠を作ってくれる名人がいる…";
		mes "その方が俺だけを通じて世に出した非常にユニークなアイテムじゃ。";
		mes "さあ！持っていけ！";
		delitem 1019,120;
		set Zeny,Zeny-10000;
		getitem 2280,1;
		close;
	}
}

//----------------------------------
// 学生帽
//----------------------------------

//所々整合性が取れません。
//eAもASBも見ましたがどれも信用ならないので未調査とします。 by Rayce

alberta_in.gat,59,139,4	script	学生帽爺	120,{
	mes "[爺]";
	mes "やれやれ……";
	next;
	mes "[爺]";
	mes "人生というのは虚しいもんじゃの……";
	mes "気持ちは若い頃のままなのに……";
	mes "やっぱり年には勝てないもんじゃの……";
	mes "コホ!!コホ……!!";
	next;
	if(countitem(1030)<10) {
		if(select("対話する","やめる")==2) {
			mes "[爺]";
			mes "コホ!!コホ……!!";
			mes "と……虎……虎の……あ、足裏……。";
			mes "コホ、コホ……!!";
			close;
		}
		mes "[爺]";
		mes "よく聞くのじゃ……。";
		mes "何より健康が一番なのじゃ。";
		mes "若い時に体に無理をすると……";
		mes "年を取ってから苦労するもんじゃ。";
		next;
		mes "[爺]";
		mes "わしは若い頃の健康を取り戻すため、";
		mes "体に良いという物は全部食べてみたが、";
		mes "結局、何も効かず、財産だけ全部";
		mes "使ってしまったのじゃ。";
		next;
		mes "[爺]";
		mes "わしはもうあきらめた。";
		mes "じゃが、一つだけ未練があるとすれば、";
		mes "今まで、薬の中の薬である";
		mes "^3355FF「虎の足裏」^000000を飲めなかった事じゃ。";
		next;
		mes "[爺]";
		mes "聞いた事はあるかい?!虎の足裏……";
		mes "見るだけで力が湧いてくる！";
		mes "匂うだけで血が沸いてくる！";
		mes "もし飲んだら、おしまいの90歳爺も";
		mes "元気旺盛になる！";
		mes "名薬中の名薬なのじゃ！";
		next;
		mes "[爺]";
		mes "ああ……死ぬ前にあれを10個飲む事が";
		mes "できれば……。その他に願いなど";
		mes "ないのじゃ……。もし誰かが";
		mes "虎の足裏を10個持っていれば……";
		mes "わしの唯一の財産、追憶の^3355FF「学生帽」^000000を";
		mes "あげてもかまわないのじゃ。";
		close;
	}
	switch (select("虎の足裏を見せる","虎の足裏を学生帽に交換する","やめる")) {
	case 1:
		mes "[爺]";
		mes "おお！こ、これが噂の…";
		mes "虎の足裏！";
		next;
		mes "[爺]";
		mes "見るだけで力が湧いてくる！匂うだけで血が沸いてくる！";
		mes "もし飲んだら、おしまいの90歳爺も元気旺盛になる！";
		mes "あの足裏かい！！！！";
		mes "おおおおおっと！ぜひ…それをわしにくれ…早く…";
		close;
	case 2:
		if(countitem(1030)<10) {	//この条件ありえなく…？？
			mes "[爺]";
			mes "渇！この悪者！";
			mes "ない物で騙そうとすると天罰に当たるじゃ！";
			close;
		}
		if(checkweight(5016,1)==0) {
			mes "[爺]";
			mes "重量オーバーです。";
			close;
		}
		mes "[爺]";
		mes "あ、ありがたいじゃ!!";
		mes "これでわしも若さが戻せるかもしれない！";
		mes "早く飲まなくちゃだめじゃの〜!!";
		delitem 1030,10;
		getitem 5016,1;
		close;
	case 3:
		mes "[爺]";
		mes "お、おい！若者！ま、待ってくれ！";
		close;
	}
}

//----------------------------------
// 学者帽
//----------------------------------

prt_in.gat,38,108,4	script	学者帽先生	53,{
	if(countitem(710) && countitem(703) && countitem(704) && countitem(708)) {
		mes "[先生]";
		mes "あら、まあ…貴方が持っているその花は…";
		next;
		switch (select("花を見せる","花を渡す","やめる")) {
		case 1:
			mes "[先生]";
			mes "や、やはり…その花はわたくしが探している4種類の花ですね…";
			mes "わたくしにこの花をくださいませんか…？";
			next;
			mes "[先生]";
			mes "もし、くださるのなら…わ、わたくしの一番大切な物を貴方に…";
			close;
		case 2:
			if(countitem(710)<1 || countitem(703)<1 || countitem(704)<1 || countitem(708)<1) {	//ありえない条件…？
				mes "[先生]";
				mes "あ…よく見たらわたくしが探している花ではありませんネ…";
				mes "はあ…間違いでした…すみません…";
				close;
			}
			if(checkweight(5012,1)==0) {
				mes "[先生]";
				mes "重量オーバーです。";
				close;
			}
			mes "[先生]";
			mes "本当にありがとうございます.生まれてからこんな親切な好意は初めてです。";
			mes "約束とおりにわたくしの大切な物を差しあげましょう。";
			mes "わたくしが卒業した時の帽子です。わたくしの大切な追憶が残っている物です。";
			mes "ぜひ…受け取ってください…大切にしてくださいね。";
			delitem 710,1;
			delitem 703,1;
			delitem 704,1;
			delitem 708,1;
			getitem 5012,1;
			close;
		case 3:
			close;
		}
	}
	mes "[先生]";
	mes "花……という物は本当に美しいですね？";
	next;
	if(select("対話する","やめる")==2) {
		mes "[先生]";
		mes "いつかはこの世が花でいっぱい";
		mes "になる、美しい所になるように";
		mes "頑張ります！";
		close;
	}
	mes "[先生]";
	mes "幼い頃、わたくしは友達と遊ぶ事が";
	mes "できず、勉強ばかりしておりました。";
	mes "あの頃が懐かしいです……しかし悔いは";
	mes "ありません。若いうちに全ての";
	mes "高等教育を習う事ができたからです。";
	next;
	mes "[先生]";
	mes "しかし……友達が1人もいないのは";
	mes "本当に悲しい事でした。勉強の時……";
	mes "疲れている時……わたくしの話を聞いて";
	mes "くれる友達が欲しかったのです。";
	mes "そして、ある日わたくしは";
	mes "最高の友達と出会ったのです。";
	next;
	mes "[先生]";
	mes "誰も手を加えない花壇で美しく";
	mes "咲いた一本の花を見て……わたくしと";
	mes "その花は、同じ運命を持っていると";
	mes "直感したのです。";
	next;
	mes "[先生]";
	mes "他の人にはただの一本の花に";
	mes "過ぎないかも知れません……ですが、";
	mes "わたくしにはとても大切な力になって";
	mes "くれたのです。わたくしがこのように";
	mes "先生になれたのも……あの花のお陰、";
	mes "と言っても過言ではございません。";
	next;
	mes "[先生]";
	mes "そして……わたくしは……その友達に";
	mes "愛を頂いたお礼を言いたいと";
	mes "思っているのです。だから花について";
	mes "たくさんの研究をし、わたくしの町……";
	mes "いえ、もっともっと全世界を、";
	mes "花でいっぱいにしたいのです。";
	next;
	mes "[先生]";
	mes "その為には……たくさんの花が必要";
	mes "ですが……わたくしには難しいのです。";
	mes "わたくしは出かけた";
	mes "経験があまりにもないので……。";
	next;
	mes "[先生]";
	mes "^3355FF「幻想の花」^000000と^3355FF「ヒナレ」^000000と";
	mes "^3355FF「アロエ」^000000そして^3355FF「メント」^000000……";
	mes "これらの花を見つけてくださるのなら、";
	mes "わたくしの大切な物を";
	mes "差しあげましょう……。";
	close;
}

//----------------------------------
// 酸素マスク
//----------------------------------

payon_in02.gat,25,71,4	script	酸素マスク青年	86,{
	if(countitem(701)<5) {
		mes "[青年]";
		mes "うあああああ！";
		mes "もう、もうたくさんだ！";
		mes "この盗蟲め!!!!!!";
		next;
		if(select("対話する","やめる")==2) {
			mes "[青年]";
			mes "……何度追い払っても無駄だ……。";
			mes "もうあきらめよう……疲れたよ……。";
			mes "くううう……";
			close;
		}
		mes "[青年]";
		mes "そういえば僕の人生って波乱万丈";
		mes "でした。幼い頃、親が亡くなって、";
		mes "孤児になり、誰も助けてくれない";
		mes "厳しい生活が続きました。";
		next;
		mes "[青年]";
		mes "下っ端から……今までやらなかった";
		mes "仕事はないほど人の下で頑張り";
		mes "続けたのです。そして10年後……";
		mes "ついに僕はいつも夢見る";
		mes "MY−SWEET−HOMEが";
		mes "買えました！";
		next;
		mes "[青年]";
		mes "しかし、なんという不幸っていうか、";
		mes "この家は完全に盗蟲の巣でした！";
		mes "奴らに手段を選ばず、いろんな方法で";
		mes "追い払おうとしました。しかし、";
		mes "その時だけでした！本当に、この家を";
		next;
		mes "[青年]";
		mes "焼き払おうかと思った時さえ";
		mes "あります！";
		mes "ああ…この盗蟲を全て倒すためには、 ";
		mes "^3355FF「オラオラ」^000000という物が";
		mes "なければならないのでしょうか？";
		mes "遠い国に行かなければ";
		next;
		mes "[青年]";
		mes "手に入れないほど、珍しい物だと";
		mes "聞きましたが……。オラオラが５個";
		mes "だけあれば、この盗蟲との戦争から";
		mes "解放されるのに……。";
		close;
	}
	mes "[青年]";
	mes "うああああああ！どうしても駄目だ！";
	mes "うう…奴らが可愛く見える！だ、駄目だ！";
	next;
	switch (select("オラオラを見せる","オラオラをあげる","やめる")) {
	case 1:
		mes "[青年]";
		mes "おおおおおおっと!!!";
		mes "こ、これがオラオラ…？！";
		mes "単刀直入で言います！そのオラオラと、僕の一番の宝物と交換しましょう！";
		mes "ど、どうですか？交換しますか？";
		close;
	case 2:
		if(countitem(701)<5)
			close;
		if(checkweight(5004,1)==0) {
			mes "[青年]";
			mes "重量オーバーです。";
			close;
		}
		delitem 701,5;
		mes "[青年]";
		mes "くはははは！やっと手に入れたぞ！オラオラ！（叫ぶ）";
		mes "しっつこい盗蟲やろうめ！全部やっつけてやる！（燃える）";
		next;
		mes "[青年]";
		mes "はあ...はあ...";
		mes "ぼ、僕がちょっと興奮したようですね.う…約束どおりに僕の宝物を渡します。";
		next;
		getitem 5004,1;
		mes "[青年]";
		mes "これを顔にして息をしたら…いつでもどこでも爽快な息ができる物です。";
		mes "前に、漁師の仕事をした時、海から手に入れた物です。";
		mes "僕には大切な物だが、僕を助けてくれた礼として持ってください。";
		next;
		mes "[青年]";
		mes "くはははは！とりあえず、さっそくこのオラオラを使ってみたいんです！";
		close;
	case 3:
		mes "[青年]";
		mes "くうううう…盗蟲を追い払う事ができなければ…";
		mes "僕に残された道は盗蟲を愛する事しかない…くうううう…";
		close;
	}
}



//----------------------------------
// ヘッドフォン
//----------------------------------

geffen_in.gat,30,71,4	script	ゼンハイザ	83,{
	mes "[ゼンハイザ]";
	mes "うちの祖父の発明話を聞いて";
	mes "いただけませんか？";
	next;
	switch (select("対話する","その発明品に必要な材料を聞く","製作を頼む","やめる")) {
	case 1:
		mes "[ゼンハイザ]";
		mes "数年前に亡くなった私の祖父は、";
		mes "私にとっては特別な存在でした。";
		mes "いつも仕事で忙しい父より、";
		mes "私のことをいつも心配して";
		mes "くれたのです。";
		next;
		mes "[ゼンハイザ]";
		mes "それで祖父の書斎はいつも私の";
		mes "遊び場でした。私は祖父と一緒に";
		mes "本を読んだり、会話したりして、";
		mes "世の中のいろんな知識を";
		mes "学ぶことができました。";
		next;
		mes "[ゼンハイザ]";
		mes "祖父が亡くなってからも私は";
		mes "祖父の事を思い出したら、";
		mes "祖父の書斎で本を読んだり";
		mes "していました。";
		next;
		mes "[ゼンハイザ]";
		mes "ある日……";
		mes "いつものように祖父の書斎で";
		mes "本を読んでいる時、";
		mes "そこで珍しい設計図を";
		mes "発見したのです。";
		next;
		mes "[ゼンハイザ]";
		mes "設計図の内容は……";
		mes "耳をすませば、蓄音機の音が";
		mes "細い線を通して聞こえる";
		mes "素晴らしい道具でした。";
		next;
		mes "[ゼンハイザ]";
		mes "私は……亡くなった祖父が設計した、";
		mes "この発明品を完成させたいと";
		mes "思ったのです。しかし、他の";
		mes "錬金術師に設計図を見せましたが、";
		mes "みんな判らないという答えでした。";
		next;
		mes "[ゼンハイザ]";
		mes "だから、仕方なく自ら作ろうと";
		mes "思ったのです。数年間の勉強の末……";
		mes "やっとあの設計図を理解し、構造が";
		mes "わかってきたのです。しかし……それを";
		mes "作るための材料を集めるのは、";
		mes "本当に難しい事でした。";
		next;
		mes "[ゼンハイザ]";
		mes "その材料さえあれば今すぐにでも、";
		mes "祖父の遺産を作ることができます。";
		mes "もし……関心があれば、私にその";
		mes "材料を集めて持ってきてください。";
		next;
		mes "[ゼンハイザ]";
		mes "私は祖父の望みだった<それ>を";
		mes "完成させたいだけなのです。";
		mes "材料を持って来てくれれば、";
		mes "完成された物は貴方に差しあげます。";
		mes "その材料は貴方の物ですし……。";
		close;
	case 2:
		mes "[ゼンハイザ]";
		mes "^3355FF鋼鉄^000000 ^FF555540^000000個";
		mes "^3355FFオリデオコン^000000 ^FF55551^000000個";
		mes "^3355FFアルコール^000000 ^FF55551^000000個";
		mes "^3355FF石炭^000000 ^FF55551^000000個";
		mes "です。";
		close;
	case 3:
		if(countitem(999)<40 || countitem(984)<1 || countitem(970)<1 || countitem(1003)<1) {
			mes "[ゼンハイザ]";
			mes "ありがとう。……ですが、";
			mes "これではちょっと足りません。";
			mes "もうちょっとあればできそう";
			mes "ですが……。";
			close;
		}
		mes "[ゼンハイザ]";
		mes "（トン！トン！トン！）";
		next;
		mes "[ゼンハイザ]";
		mes "ありがとうございました！";
		mes "おかげさまで祖父の遺産が";
		mes "完成しました！";
		mes "これを祖父に見せたかったんです……。";
		delitem 999,40;
		delitem 984,1;
		delitem 970,1;
		delitem 1003,1;
		getitem 5001,1;
		next;
		mes "[ゼンハイザ]";
		mes "前に言ったように";
		mes "これは受け取ってください。";
		mes "私はこれを作れる事が証明された";
		mes "だけで十分うれしいです。";
		close;
	case 4:
		mes "[ゼンハイザ]";
		mes "さよなら。";
		close;
	}
}

//----------------------------------
// 望遠鏡
//----------------------------------

morocc_in.gat,76,163,4	script	錬金術師	64,{
	mes "[バリオジュナ]";
	mes "おほ！またお客さんか！";
	mes "君が何の話しをしようとするのか、";
	mes "わかってるぜべビィー";
	mes "この俺様に魔法の眼鏡を作って";
	mes "もらおうとしているだろう？";
	next;
	switch (select("魔法の眼鏡ってなに？","魔法の眼鏡を製作する","無視する")) {
	case 1:
		mes "[バリオジュナ]";
		mes "うむ！正式名称は^3355FF「望遠鏡」^000000という！!";
		mes "長い丸形の管の中に";
		mes "ガラスで作ったレンズを入れて、";
		mes "そのレンズが対象から反射する光を";
		mes "映し、遠く離れている事物が";
		mes "目の前にあるように見えるものだ！";
		next;
		mes "[バリオジュナ]";
		mes "どうだ？すごいだろう？";
		mes "うははははは！";
		mes "……。";
		mes "……？おい、信じてないのかよ。";
		mes "ったく、本当だってば！";
		next;
		mes "[バリオジュナ]";
		mes "望遠鏡を作るための材料を";
		mes "特別に教えてやろう。";
		mes "^3355FFスピングラス^000000 1個！^3355FF鋼鉄^000000 100個！";
		mes "そして^3355FF50000 Zeny^000000！";
		close;
	case 2:
		if(countitem(2243)<1 || countitem(999)<100 || Zeny<50000) {
			mes "[バリオジュナ]";
			mes "足りないじゃん〜!!";
			mes "このミッドガルド最高の";
			mes "光学錬金術師の俺様をなめてるのか？";
			mes "ひどすぎ〜〜〜!!";
			close;
		}
		mes "[バリオジュナ]";
		mes "OK、OK！";
		mes "俺様が作った望遠鏡も";
		mes "素敵な持ち主に出会って、幸せだろう！";
		next;
		mes "[バリオジュナ]";
		mes "ほいっ！望遠鏡だぜ！";
		delitem 2243,1;
		delitem 999,100;
		delitem 983,1;
		set Zeny,Zeny-50000;
		getitem 2296,1;
		next;
		mes "[バリオジュナ]";
		mes "望遠鏡を使って、";
		mes "いやらしいことはしないでね。";
		mes "ぷははははは！";
		close;
	case 3:
		mes "[バリオジュナ]";
		mes "おいおい！";
		mes "このミッドガルドの光学分野で";
		mes "一番有名で、純粋で、";
		mes "デリケートなこの俺様との";
		mes "対話を遠慮する気？";
		close;
	}
}
