//= Athena Script ==============================================================
// Ragnarok Online EP8.1 MakeHat Script	by refis
//==============================================================================

//============================================================
// リカラー リボンのヘアバンド
//------------------------------------------------------------
morocc_in.gat,137,102,5	script	研修生	82,{
	mes "[エルゴ]";
	mes "いらっしゃいませ！";
	mes "染色剤の制作をしに来たのであれば、";
	mes "先生の方に問い合わせてください。";
	next;
	if(select("あなたは？","特に用はありません") == 2) {
		mes "[エルゴ]";
		mes "……そうですか。";
		mes "ご用がないのでしたら";
		mes "お引取り願えませんか？";
		mes "わたしにも仕事がありますので……";
		close;
	}
	mes "[エルゴ]";
	mes "わたしは染色剤を使って服などを";
	mes "違う色に染める仕事をしています。";
	mes "例えばハンカチとか";
	mes "リボンのような物をね。";
	next;
	mes "[エルゴ]";
	mes "小さくてシンプルな物なら";
	mes "簡単に染められますよ？";
	mes "^0000FFリボンのヘアバンド^000000が";
	mes "いい例になりますね。";
	next;
	mes "[エルゴ]";
	mes "もし新しく色付けしてみたい";
	mes "リボンのヘアバンドを持っていましたら";
	mes "遠慮なくわたしに持ってきてください。";
	next;
	mes "[エルゴ]";
	mes "わたしはまだ研究生ですので";
	mes "染色代はいただきません。";
	mes "わたしにしてみれば";
	mes "良い練習になりますし、あなたも";
	mes "新しい色のヘアバンドが手に入る。";
	mes "悪い話ではないでしょう？";
	next;
	mes "[エルゴ]";
	mes "ただし、染色代は頂きませんが、";
	mes "^0000FF染料^000000が必要です。";
	mes "染料が無くては";
	mes "どうにもなりませんから。";
	next;
	mes "[エルゴ]";
	mes "絶対に失敗などしませんから";
	mes "ご安心下さい！";
	mes "わたし、染色の腕には";
	mes "自信があるんですよ。";
	mes "ですから、ご遠慮なく！";
	next;
	if(countitem(2250) < 1 && countitem(5441) < 1 && countitem(5440) < 1 &&
		countitem(5439) < 1 && countitem(5438) < 1) {
		mes "[エルゴ]";
		mes "あなたはリボンのヘアバンドを";
		mes "お持ちではないようですね。";
		next;
		mes "[エルゴ]";
		mes "もし、リボンのヘアバンドの";
		mes "染色をしたくなったら";
		mes "是非、お声をかけて下さいね！";
		close;
	}
	mes "[エルゴ]";
	mes "リボンのヘアバンドの";
	mes "染色をしますか？";
	next;
	if(select("染色する","やめる") == 2) {
		mes "[エルゴ]";
		mes "おやめになるのですか？";
		mes "まぁ、仕方ありませんね。";
		mes "また来てくださいね。";
		close;
	}
	mes "[エルゴ]";
	mes "ではどのヘアバンドを";
	mes "染色しますか？";
	mes "教えてください。";
	next;
	switch(select(
		"^ff6600リボンのヘアバンド^000000","ホワイトヘアバンド",
		"^0000ffブルーヘアバンド^000000","^FF0000レッドヘアバンド^000000",
		"^006633グリーンヘアバンド^000000","やめる"
	)) {
	case 1:
		set '@need,2250;
		break;
	case 2:
		set '@need,5441;
		break;
	case 3:
		set '@need,5440;
		break;
	case 4:
		set '@need,5439;
		break;
	case 5:
		set '@need,5438;
		break;
	case 6:
		mes "[エルゴ]";
		mes "おやめになるのですか？";
		mes "まぁ、仕方ありませんね。";
		mes "また来てくださいね。";
		close;
	}
	if(countitem('@need) < 1) {
		mes "[エルゴ]";
		mes getitemname('@need)+ "は";
		mes "お持ちではないようですよ？";
		close;
	}
	mes "[エルゴ]";
	mes "はい、かしこまりました。";
	mes "ではその" +getitemname('@need)+ "を";
	mes "何色に染めたいですか？";
	next;
	setarray '@color$,"^ff9933黄^000000","白","^0000ff青^000000","^FF0000赤^000000","^006633緑^000000";
	set '@color$[@menu-1],"";
	switch(select(printarray('@color$))) {
	case 1:
		set '@dye,976;
		set '@gain,2250;
		if(countitem('@dye) < 1) {
			mes "[エルゴ]";
			mes "黄色に染めるには、";
			mes "^ff9933黄色の染料1個^000000が必要です。";
			close;
		}
		mes "[エルゴ]";
		mes "^ff9933黄色^000000！";
		mes "幸せを運んでくれる色ですね！";
		break;
	case 2:
		set '@dye,982;
		set '@gain,5441;
		if(countitem('@dye) < 1) {
			mes "[エルゴ]";
			mes "白く染めるには、";
			mes "^0000FF白色の染料1個^000000が必要です。";
			close;
		}
		mes "[エルゴ]";
		mes "純白の白！";
		mes "素敵ですね！";
		break;
	case 3:
		set '@dye,978;
		set '@gain,5440;
		if(countitem('@dye) < 1) {
			mes "[エルゴ]";
			mes "青く染めるには、";
			mes "^0000FF青色の染料1個^000000が必要です。";
			close;
		}
		mes "[エルゴ]";
		mes "^0000ff青^000000！";
		mes "海を思い出しますね！";
		break;
	case 4:
		set '@dye,975;
		set '@gain,5439;
		if(countitem('@dye) < 1) {
			mes "[エルゴ]";
			mes "赤く染めるには、";
			mes "^FF0000赤色の染料1個^000000が必要です。";
			close;
		}
		mes "[エルゴ]";
		mes "^FF0000赤^000000！";
		mes "ほとばしる情熱を感じます！";
		break;
	case 5:
		set '@dye,979;
		set '@gain,5438;
		if(countitem('@dye) < 1) {
			mes "[エルゴ]";
			mes "緑に染めるには、";
			mes "^006633緑色の染料1個^000000が必要です。";
			close;
		}
		mes "[エルゴ]";
		mes "^006633緑^000000！";
		mes "緑はまさに命の源の色！";
		break;
	}
	mes "ではすぐに作業に入ります！";
	next;
	mes "‐エルゴは素早く染料を水に溶かし、";
	mes "　ヘアバンド全体に手際よく";
	mes "　色を付けていった‐";
	next;
	mes "[エルゴ]";
	mes "ふむ……";
	mes "ちょうどいい具合に染まりましたね。";
	mes "どうですか？";
	mes "すごいでしょう？";
	delitem '@need,1;
	delitem '@dye,1;
	getitem '@gain,1;
	close;
}

//============================================================
// フラワーヴェール
//------------------------------------------------------------
in_orcs01.gat,119,106,3	script	#オークレディ	1273,{}
in_orcs01.gat,119,105,3	script	オークレディ	111,{
	mes "‐^0000FFオークレディ^000000が何かに";
	mes "　夢中になっているようだ。";
	mes "　何かを作っているのだろうか‐";
	next;
	mes "‐オークレディがこちらに気がつき、";
	mes "　とても驚いた表情をみせた。";
	mes "　オークレディは手に持っていた";
	mes "　たくさんの食人植物の花を";
	mes "　落としてしまう‐";
	next;
	if(select("花を拾うのを手伝う","逃げる！") == 2) {
		mes "‐オークレディが慌てている内に";
		mes "　早く逃げよう！‐";
		close2;
		warp "gef_fild10.gat",223,203;
		end;
	}
	mes "[オークレディ]";
	mes "……";
	mes "アナタ、モンスター狩りが";
	mes "目当ての人間ではないの？";
	mes "不思議な人……";
	mes "ワタシ、^0000FFアイト・ナ・ジルービ^000000。";
	next;
	mes "‐驚くことにオークレディは";
	mes "　自ら自己紹介をしてきた。";
	mes "　どう答えるべきか……‐";
	next;
	if(select("何をしていたんですか？","無言で会釈をする") == 2) {
		mes "‐軽く会釈してみせた。";
		mes "　アイトも同じく会釈をしてくれた‐";
		close;
	}
	mes "[アイト]";
	mes "花かんむり、作る。";
	mes "食人植物の花をこのリボンに……";
	mes "これ、花嫁がかぶる。";
	next;
	mes "[アイト]";
	mes "^0000FFシルクリボン^000000と、";
	mes "^0000FF食人植物の花が1000個^000000";
	mes "で花かんむり、作る。";
	mes "……アナタも、欲しい？";
	next;
	if(select("欲しい！","いらない") == 2) {
		mes "[アイト]";
		mes "そう、わかった。";
		close;
	}
	if(countitem(10007) < 1 || countitem(1032) < 1000) {
		mes "[アイト]";
		mes "^0000FFシルクリボン^000000と、";
		mes "^0000FF食人植物の花が1000個^000000必要。";
		mes "材料集めてきたら、作る。";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "（^0000FFシルクリボン^000000と";
		mes "　^0000FF食人植物の花が1000個^000000か……）";
		close;
	}
	mes "[アイト]";
	mes "……";
	mes "アナタが持っている材料なら";
	mes "すぐに、作れる……";
	next;
	mes "[アイト]";
	mes "花かんむり、できた。";
	mes "……アナタ、本当に不思議。";
	mes "我らオーク族、人間怖い。";
	mes "でも、アナタは怖くない……";
	delitem 10007,1;
	delitem 1032,1000;
	getitem 5436,1;
	close;
}

//============================================================
// 肉球のヘアピン
//------------------------------------------------------------
gef_fild05.gat,80,149,3	script	ミュー	877,{
	mes "[ミュー]";
	mes "にゃ〜。";
	next;
	if(checkquest(7054)) {
		if(checkquest(7055) & 0x4) {
			mes "[ミュー]";
			mes "どうして^0000FFワイルドローズ^000000を";
			mes "いじめたのにゃ!?";
			mes "ミューはちゃんと知っているのにゃ！";
			next;
			mes "[ミュー]";
			mes "ワイルドローズにあんにゃ事を";
			mes "するにゃんて……許せにゃいのにゃ!!";
			next;
			mes "[ミュー]";
			mes "もう怒ったのにゃ！";
			mes "この話はにゃかった事に、";
			mes "してくれにゃ！";
			mes "……ニィヤァァァ……！";
			delquest 7054;
			delquest 7055;
			close;
		}
		if(checkquest(7054) & 0x4) {
			// 未調査
			mes "[ミュー]";
			mes "す……すごいのにゃ〜！";
			mes "本当に1000回退治してくれたのにゃ！";
			mes "さすがは冒険者さんにゃ！";
			mes "にゃにゃにゃにゃ〜!!";
			next;
			mes "[ミュー]";
			mes "これをあげるのにゃ！";
			mes "ミューのとっても大事にゃお宝にゃ！";
			mes "大事にするのにゃ！";
			if(checkweight(5446,1) == 0) {
				next;
				mes "[ミュー]";
				mes "にゃ？";
				mes "冒険者さんは荷物がいっぱいにゃ";
				mes "……残念にゃあ……";
				close;
			}
			delquest 7054;
			delquest 7055;
			getitem 5446,1;
			next;
			mes "[ミュー]";
			mes "冒険者さん、本当にありがとにゃ！";
			mes "ささ、早くそれを持ってここを";
			mes "去った方が良いのにゃ！";
			mes "冒険者さんがここにいると";
			mes "ちょっとだけ面倒にゃ事に";
			mes "にゃってしまうのにゃ！";
			next;
			mes "[ミュー]";
			mes "にゃにゃにゃ！";
			mes "にゃ〜！";
			close;
		}
		mes "[ミュー]";
		mes "まだにゃ！";
		mes "まだまだ足りにゃいにゃ！";
		mes "^0000FFコボルドアーチャーを^000000";
		mes "^0000FF1000回退治^000000してやるのにゃ〜！";
		mes "^FF0000ワイルドローズは^000000";
		mes "^FF0000いじめちゃダメ^000000にゃ〜！";
		close;
	}
	mes "[ミュー]";
	mes "にゃ？";
	mes "……冒険者さんにゃ？";
	mes "こんにゃ所に何の用にゃ？";
	mes "もしかして、ワイルドローズを";
	mes "いじめに来たのにゃ……!?";
	next;
	menu "にゃ？",-;
	mes "[ミュー]";
	mes "あ、気にしにゃくていいのにゃ〜。";
	mes "ここは、捨てられた";
	mes "彷徨う者たちの町にゃのにゃ。";
	mes "でも、最近は訪れる者も少にゃくて、";
	mes "静けさだけが残る町にゃのにゃ……";
	next;
	mes "[ミュー]";
	mes "ところで冒険者さん、";
	mes "^0000FFコボルドアーチャー^000000たちを";
	mes "懲らしめて来てくれにゃいかにゃ？";
	next;
	mes "[ミュー]";
	mes "コボルドアーチャーたちは";
	mes "ミューたちの領域に無断で";
	mes "出入りして、ミューたちの";
	mes "ご飯を狙っているのにゃ……";
	next;
	mes "[ミュー]";
	mes "もちろん、ミューたちは";
	mes "ちゃんとお話しようとしたんだけど、";
	mes "全然話を聞いてくれにゃいにゃあ……";
	next;
	mes "[ミュー]";
	mes "だから、コボルドアーチャーたちが";
	mes "ミューたちのご飯を狙わにゃいように";
	mes "ちょっとだけ懲らしめて来て";
	mes "欲しいのにゃ〜。";
	mes "ちゃんと懲らしめて来てくれたら、";
	mes "ミューのお宝をあげるのにゃ！";
	next;
	if(select("断る","詳しく話を聞く") == 1) {
		mes "[ミュー]";
		mes "無理にゃいにゃ……";
		mes "仕方にゃいにゃ……";
		mes "冒険者さんは無関係にゃ……";
		mes "……残念にゃあ……";
		close;
	}
	mes "[ミュー]";
	mes "さっき話した通り";
	mes "コボルドアーチャーを";
	mes "懲らしめて来て欲しいのにゃ！";
	mes "にゃ〜！";
	next;
	menu "懲らしめるって？",-;
	mes "[ミュー]";
	mes "^0000FFたくさんコボルドアーチャーを^000000";
	mes "^0000FF退治して欲しい^000000のにゃ！";
	next;
	mes "[ミュー]";
	mes "たくさん懲らしめてやれば、";
	mes "よほどのおバカさんでもにゃい限り";
	mes "二度とミューたちの前に";
	mes "姿を現す事はにゃいのにゃ！";
	mes "だから、^0000FF1000回^000000ぐらい退治するにゃ！";
	next;
	menu "……1000回!?",-;
	mes "[ミュー]";
	mes "1000回も退治すれば、きっと";
	mes "コボルドアーチャーも懲りるのにゃ。";
	next;
	if(select("そこまでしなくても……","わかりました") == 1) {
		mes "[ミュー]";
		mes "このぐらいしなきゃダメにゃ！";
		mes "コボルドアーチャーを野放しに";
		mes "しておいたら、いつの日か";
		mes "ミューたちのご飯は食べ尽くされて";
		mes "ミューたちは生きていけにゃく";
		mes "にゃってしまうのにゃ……!!";
		next;
		mes "[ミュー]";
		mes "ミューたちはとっても困ってるのに";
		mes "冒険者さんは無関心にゃ……";
		mes "……もう冒険者さんには";
		mes "お願いしにゃいのにゃ……";
		close;
	}
	mes "[ミュー]";
	mes "話が早くて助かるのにゃ〜。";
	next;
	mes "[ミュー]";
	mes "ただ、一つだけ条件があるのにゃ。";
	mes "^FF0000この仕事をしている間に、^000000";
	mes "^FF0000ワイルドローズはいじめにゃいで^000000";
	mes "^FF0000欲しい^000000のにゃ〜。";
	next;
	mes "[ミュー]";
	mes "わかったにゃ？";
	mes "^FF0000ワイルドローズはダメ^000000にゃのにゃ！";
	mes "気を付けるのにゃ！";
	mes "にゃにゃにゃ！";
	mes "にゃ〜！";
	setquest 7054;
	setquest 7055;
	close;
}

//============================================================
// お知らせ看板
//------------------------------------------------------------
alberta.gat,226,67,6	script	放浪の芸術家チハノ	881,{
	if(checkitemblank() == 0) {
		mes "‐所持アイテムの種類数が多い為";
		mes "　危険です。";
		mes "　所持アイテムを減らしてから、";
		mes "　再度試行してください‐";
		close;
	}
	if((MaxWeight - Weight) < 1000) {
		mes "‐所持アイテムの重量が重い為";
		mes "　危険です。";
		mes "　所持アイテムを減らしてから、";
		mes "　再度試行してください‐";
		close;
	}
	mes "[放浪の芸術家チハノ]";
	mes "……波風が気持ちいいなあ。";
	mes "広がる海、それに空！";
	mes "なんて気持ちいいんだ！";
	next;
	mes "[放浪の芸術家チハノ]";
	mes "……あ、これは失礼。";
	mes "私は旅をしながら絵を描いたり";
	mes "物を作っている者です。";
	mes "決して怪しい者ではありません。";
	emotion 1;
	next;
	mes "[放浪の芸術家チハノ]";
	mes "旅のお供のアフケーと一緒に";
	mes "彼方此方歩き回っているのですが";
	mes "先日妙な場所に";
	mes "迷い込んでしまいまして。";
	next;
	mes "[放浪の芸術家チハノ]";
	mes "しかし、ふと気付いたら";
	mes "アルベルタに居たんです。";
	mes "ひょっとしたら漂流中に";
	mes "夢でも見ていたのかも？";
	mes "とにかく今は非常に気持ちいいです！";
	next;
	mes "[放浪の芸術家チハノ]";
	mes "ここで出会ったのも何かの縁、";
	mes "芸術家の端くれとして";
	mes "あなたに何か作って差し上げようかと";
	mes "思うのですが……";
	mes "実は必要な材料を切らしてまして。";
	next;
	mes "[放浪の芸術家チハノ]";
	mes "私の自信作、^0000FFお知らせ看板^000000を";
	mes "作るには、";
	mes "^FF0000色々な染料^000000が必要なのです。";
	mes "染料そのものか、材料になるハーブが";
	mes "あれば助かるんですけどね……";
	mes "中和剤とかは足りているので。";
	next;
	mes "[放浪の芸術家チハノ]";
	mes "どうでしょう、";
	mes "必要な染料を用意していただくか、";
	mes "必要なハーブを用意していただければ";
	mes "^0000FFお知らせ看板^000000を作れますが。";
	mes "あ、あとやわらかな毛も多少必要です。";
	next;
	switch(select("^C71585染料コース^000000","^006400ハーブコース^000000","結構です")) {
	case 1:
		mes "[放浪の芸術家チハノ]";
		mes "染料を用意していただけると！";
		mes "助かります！";
		mes "必要な染料を言いますね。";
		next;
		mes "[放浪の芸術家チハノ]";
		mes "青色の染料　^0000FF1個^000000";
		mes "赤色の染料　^0000FF1個^000000";
		mes "黄色の染料　^0000FF1個^000000";
		mes "紅色の染料　^0000FF1個^000000";
		mes "緑色の染料　^0000FF1個^000000";
		mes "紫色の染料　^0000FF1個^000000";
		next;
		mes "[放浪の芸術家チハノ]";
		mes "以上6種類を1個ずつです。";
		mes "本当はやわらかい毛も必要なのですが";
		mes "染料を用意していただけるなら";
		mes "こちらでなんとかしましょう。";
		setarray '@need,978,975,976,980,979,981;
		setarray '@amount,1,1,1,1,1,1;
		break;
	case 2:
		mes "[放浪の芸術家チハノ]";
		mes "ハーブを用意していただけると！";
		mes "助かります！";
		mes "必要な材料を言いますね。";
		next;
		mes "[放浪の芸術家チハノ]";
		mes "赤ハーブ　^0000FF80個^000000";
		mes "黄ハーブ　^0000FF70個^000000";
		mes "青ハーブ　^0000FF35個^000000";
		mes "緑ハーブ　^0000FF20個^000000";
		mes "やわらかな毛　^0000FF100個^000000";
		next;
		mes "[放浪の芸術家チハノ]";
		mes "以上です。";
		mes "ちょっと多いですが、これで";
		mes "染料が作れます。";
		mes "やわらかな毛は看板のベースを";
		mes "作るのに必要なんです。";
		setarray '@need,507,508,510,511,949;
		setarray '@amount,80,70,35,20,100;
		break;
	case 3:
		mes "[放浪の芸術家チハノ]";
		mes "そうですか、残念です。";
		mes "結構可愛くて自信作なんですが。";
		close;
	}
	next;
	for(set '@i,0; '@i<getarraysize('@need); set '@i,'@i+1) {
		if(countitem('@need['@i]) < '@amount['@i]) {
			if(select("わかりました","やっぱりやめます") == 2) {
				mes "[放浪の芸術家チハノ]";
				mes "そうですか、残念です。";
				close;
			}
			mes "[放浪の芸術家チハノ]";
			mes "よろしくお願いしますね。";
			close;
		}
	}
	if(select("^0000FF材料を提供する^000000","やっぱりやめます") == 2) {
		mes "[放浪の芸術家チハノ]";
		mes "そうですか、残念です。";
		close;
	}
	mes "[放浪の芸術家チハノ]";
	mes "おお、ありがとうございます！";
	mes "それでは早速これを使って";
	mes "作成をしましょう。";
	next;
	mes "[放浪の芸術家チハノ]";
	mes "………………";
	next;
	for(set '@i,0; '@i<getarraysize('@need); set '@i,'@i+1)
		delitem '@need['@i],'@amount['@i];
	getitem 5474,1;
	emotion 21;
	mes "[放浪の芸術家チハノ]";
	mes "よし、出来ましたよ！";
	mes "なかなかいい出来です！";
	mes "大切に使ってくださいね。";
	next;
	mes "[放浪の芸術家チハノ]";
	mes "……さて、しばらくここで";
	mes "海の絵でも描こうかな。";
	mes "それでは、また！";
	close;
}

alberta.gat,226,66,6	script	愛猫アフケー	876,{
	mes "[アフケー]";
	mes "ニャー。";
	next;
	mes "[放浪の芸術家チハノ]";
	mes "その子は私の旅のお供、";
	mes "猫のアフケーちゃんですよ。";
	mes "可愛いでしょう。";
	next;
	mes "[アフケー]";
	mes "ニャー、ニャーニャニャー。";
	mes "ニャーゴ。";
	next;
	mes "[放浪の芸術家チハノ]";
	mes "んー、お腹が空いたのかな？";
	mes "ごめんねー、最近ろくな";
	mes "食べ物あげてないもんね……";
	next;
	mes "[アフケー]";
	mes "ニャ、ニャニャー。";
	mes "ニャウンニャー。";
	next;
	mes "[放浪の芸術家チハノ]";
	mes "『ご主人様、気にしないで』って？";
	mes "優しいね、お前は。";
	mes "今度新鮮なフェンを食べさせて";
	mes "あげるからね。";
	next;
	mes "[アフケー]";
	mes "ニャニャニャ、ニャニャー。";
	mes "ゴロニャーゴ。";
	close;
}
