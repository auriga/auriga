//==============================================================================
// Ragnarok Online Halloween2007 Script
//==============================================================================

//==========================================
// パンプキング
//------------------------------------------
-	script	パンプキング#Haloween	1130,{
	mes "[パンプキング]";
	mes "さぁ、今年もハロウィンの時期が";
	mes "やって参りました！";
	mes "ささ、どうぞ私の近くに";
	mes "集まってください！";
	mes "年に一度、この時期だけの";
	mes "特別なイベントですよ！";
	next;
	mes "[パンプキング]";
	mes "おぉっと、申し遅れました！";
	mes "私はパンプキング！";
	mes "ハロウィンらしく";
	mes "ジャックの仮装をしております、";
	mes "しがない芸人の一人でございます。";
	next;
	mes "[パンプキング]";
	mes "今まで退屈でしたよね？";
	mes "暇で暇で";
	mes "どうしようもない日々を";
	mes "すごしてきましたよね？";
	next;
	mes "[パンプキング]";
	mes "楽しい事が何か無いかと";
	mes "考えていた方々!!";
	mes "さぁさぁ、こちらへどうぞ！";
	next;
	mes "[パンプキング]";
	mes "この私が!!";
	mes "楽しい思い出を";
	mes "皆様にプレゼントしましょう！";
	next;
	mes "[パンプキング]";
	mes "それは……なんと!!";
	mes "私が皆さんに、";
	mes "とっても素敵な帽子を";
	mes "作って差し上げます！";
	next;
	mes "[パンプキング]";
	mes "手数料なんて野暮な事はいいません！";
	mes "材料さえ揃っていれば";
	mes "素敵な帽子が皆様の手に！";
	next;
	mes "[パンプキング]";
	mes "ぇ？　帽子なんていらない？";
	mes "大丈夫です！";
	mes "そういった方の為には、";
	mes "特別なイベントを";
	mes "用意してありますよ!!";
	next;
	mes "[パンプキング]";
	mes "興味がでてきましたか？";
	mes "さぁ、早くこっちへ来て、";
	mes "一緒に楽しみましょう！";
	next;
	switch(select("無視する","帽子？","イベント？")) {
	case 1:
		mes "[パンプキング]";
		mes "あぁっと、お客さん!!";
		mes "明日には私がいなくなっている";
		mes "かも知れないんですよ！";
		mes "さぁさぁ、早くこちらへ。";
		mes "後で後悔しても知りませんよ!?";
		close;
	case 2:
		mes "[パンプキング]";
		mes "帽子に興味があるのですね！";
		mes "簡単に説明しますと、";
		mes "海の向こう、遠い異国にいる";
		mes "とても有名なデザイナーさんが";
		mes "作り出した、素晴らしい一品です。";
		next;
		mes "[パンプキング]";
		mes "お金があるからって、";
		mes "簡単に買える物ではありません！";
		next;
		mes "[パンプキング]";
		mes "あまりにも素晴らしかったので、";
		mes "私はミッドガルド大陸の方々にも";
		mes "是非紹介したいと思い……";
		next;
		mes "[パンプキング]";
		mes "その有名なデザイナーさんに直接！";
		mes "帽子の作り方を";
		mes "伝授させて頂いたのです！";
		next;
		mes "[パンプキング]";
		mes "その帽子を";
		mes "あなたも手にしたいのですね？";
		mes "えぇ、わかりました。";
		mes "材料さえ持って来てくだされば、";
		mes "すぐにでも作って差し上げますよ！";
		next;
		mes "[パンプキング]";
		mes "材料を今から言いますから、";
		mes "よ〜く聞いてくださいね。";
		next;
		mes "[パンプキング]";
		mes "^0000FFカボチャの頭が10個^000000に、";
		mes "^0000FFキャップ[0]が1個^000000に、";
		mes "^0000FF透明な布を10個^000000!!";
		mes "わかりましたか？";
		next;
		mes "[パンプキング]";
		mes "さぁさぁ、材料が揃っている方は";
		mes "今すぐ手を上げてください！";
		mes "この場ですぐ!!";
		mes "帽子を作って差し上げますよ！";
		next;
		if(select("帽子を作ってください！","材料がまだないか……")==2) {
			mes "[パンプキング]";
			mes "期間限定です！";
			mes "帽子が欲しい方はお早めにー！";
			close;
		}
		mes "[パンプキング]";
		mes "はい、よろこんで！";
		mes "ではでは、材料を確認しますね。";
		mes "えーっと…………";
		next;
		if(countitem(1062) < 10 || countitem(2226) < 1 || countitem(1059) < 10) {
			mes "[パンプキング]";
			mes "おやおや……残念です。";
			mes "材料が足りていませんね？";
			mes "もう一度材料を言いますので、";
			mes "今度は是非、持ってきてくださいね！";
			next;
			mes "[パンプキング]";
			mes "^0000FFカボチャの頭が10個^000000に、";
			mes "^0000FFキャップ[0]が1個^000000に、";
			mes "^0000FF透明な布を10個^000000!!";
			mes "では、楽しみにお待ちしております！";
			close;
		}
		mes "[パンプキング]";
		mes "素晴らしい！";
		mes "見事に材料が揃っています！";
		mes "早速帽子を作りますから、";
		mes "少しだけ待っていてください。";
		next;
		mes "[パンプキング]";
		mes "これをこうやって〜";
		mes "あれをああやって〜";
		mes "手でモミモミして〜";
		next;
		mes "[パンプキング]";
		mes "さぁ、出来上がりました！";
		mes "どうですか？";
		mes "この形にこのつや……";
		mes "甘い香りまで……完璧でしょう？";
		mes "では、この帽子と一緒に";
		mes "ハロウィンを楽しんでください!!";
		delitem 1062,10;
		delitem 2226,1;
		delitem 1059,10;
		getitem 5356,1;
		close;
	case 3:
		mes "[パンプキング]";
		mes "イベントですか？";
		mes "うふふふ……";
		mes "お客さん……";
		mes "お客さんにも憎〜い人が";
		mes "一人や二人、いるでしょう？";
		next;
		mes "[パンプキング]";
		mes "まぁまぁ、そんな顔しないで！";
		mes "話を最後まで聞いてください。";
		mes "私が用意したイベントは、";
		mes "言うならば";
		mes "小さな復讐ってところでしょうか。";
		next;
		mes "[パンプキング]";
		mes "人間関係というものは、";
		mes "様々な思考が絡みあう事で、";
		mes "そんなつもりは無いのに";
		mes "相手を憎んだり、憎まれたり";
		mes "してしまいますよね？";
		next;
		mes "[パンプキング]";
		mes "そこで！";
		mes "人間関係からくるストレスを";
		mes "一気に解消してしまう！";
		mes "という愉快、爽快なイベントを";
		mes "ご用意したのです！";
		next;
		mes "[パンプキング]";
		mes "どうするかといいますと……";
		mes "ある物を集めてきてくだされば、";
		mes "街の内にモンスターを召喚します！";
		mes "素敵でしょう？";
		mes "皆、慌てるに違いありません！";
		next;
		mes "[パンプキング]";
		mes "この街に憎い人なんていない時？";
		mes "ふふふ……抜かりはありませんよ！";
		mes "憎い人がいる街に行ってみてください。";
		mes "私は色んな街を渡り歩いているので、";
		mes "きっと他の街でも会えます。";
		mes "なので、そこの街に召喚もできます！";
		next;
		mes "[パンプキング]";
		mes "やり方が気になりますか？";
		mes "デビルチは";
		mes "子悪魔の角か子悪魔の翼を";
		mes "2つ持ってきてくだされば、";
		mes "1匹を召喚できます。";
		next;
		mes "[パンプキング]";
		mes "そうそう、二種類とも持ってきた場合、";
		mes "子悪魔の角を優先して使いますね。";
		mes "また、子悪魔の角と子悪魔の翼を";
		mes "1つずつで計2つ。";
		mes "という事にはなりません。";
		next;
		mes "[パンプキング]";
		mes "ウィスパーは";
		mes "透明な布2つで1匹。";
		mes "ジャックは";
		mes "カボチャの頭を2つで1匹を";
		mes "召喚できます。";
		next;
		mes "[パンプキング]";
		mes "でも、広〜い街に一匹だけ";
		mes "ポツンと召喚されても、";
		mes "誰も驚きませんよね？";
		mes "なので、召喚は11匹ずつ。";
		mes "材料はサービスで10匹分";
		mes "で大丈夫です。";
		next;
		mes "[パンプキング]";
		mes "細々としているより";
		mes "一気にドカーンとしたほうが";
		mes "より効果的ですからね！";
		mes "では、材料さえ揃っていれば";
		mes "すぐに召喚しますよ！";
		next;
		if(select("召喚してください！","また後で……")==2) {
			mes "[パンプキング]";
			mes "おやおや……これは残念!!";
			mes "私はここに年中いるわけではないので、";
			mes "チャンスを逃さないでくださいね!!";
			close;
		}
		mes "[パンプキング]";
		mes "ふふふ……";
		mes "憎い人がいるのですか？";
		mes "人々を脅かしたいのですか？";
		mes "いえいえ、答えたくない気持ちは";
		mes "よ〜くわかりますとも。";
		mes "さぁ、どのモンスターにしますか？";
		next;
		switch(select("デビルチ","ウィスパー","ジャック")) {
		case 1:
			if(countitem(1038) < 20 && countitem(1039) < 20) {
				mes "[パンプキング]";
				mes "おやおや、せっかちさんですね〜";
				mes "気持ちはわかりますけど、";
				mes "材料がないと召喚できませんよ。";
				mes "デビルチは子悪魔の角20個。";
				mes "または、子悪魔の翼20個が";
				mes "必要になります。";
				close;
			}
			set '@mobid,1928;
			set '@mobname$,"デビルチ";
			if(countitem(1038) >= 20) set '@itemid,1038;
			else set '@itemid,1039;
			break;
		case 2:
			if(countitem(1059) < 20) {
				mes "[パンプキング]";
				mes "おやおや、せっかちさんですね〜";
				mes "気持ちはわかりますけど、";
				mes "材料がないと召喚できませんよ。";
				mes "ウィスパーは透明な布20個が";
				mes "必要になります。";
				close;
			}
			set '@mobid,1927;
			set '@mobname$,"ウィスパー";
			set '@itemid,1059;
			break;
		case 3:
			if(countitem(1062) < 20) {
				mes "[パンプキング]";
				mes "おやおや、せっかちさんですね〜";
				mes "気持ちはわかりますけど、";
				mes "材料がないと召喚できませんよ。";
				mes "ジャックはカボチャの頭20個が";
				mes "必要になります。";
				close;
			}
			set '@mobid,1926;
			set '@mobname$,"ジャック";
			set '@itemid,1062;
			break;
		}
		mes "[パンプキング]";
		mes "素晴らしい！";
		mes "見事に材料が揃っています！";
		mes "ではでは、早速" +'@mobname$+ "を";
		mes "召喚しますよ〜!!";
		next;
		mes "[パンプキング]";
		mes "ハロウィィイィイーーーン!!";
		next;
		misceffect 74;
		next;
		misceffect 138;
		next;
		misceffect 169;
		delitem '@itemid,20;
		set '@dummy,getmapxy('@mapname$,'@dummy,'@dummy,1);
		areamonster '@mapname$,0,0,500,500,"--ja--",'@mobid,11;
		getitem 7460,1;
		next;
		mes "[パンプキング]";
		mes "はい、" +'@mobname$+ "を召喚しました！";
		mes "おや？　見えないって？";
		mes "召喚したのは、お客さんを驚かす";
		mes "為ではありませんよね？";
		mes "街を歩き回ってみてください。";
		mes "今頃、人々が騒いでいるはずですよ。";
		next;
		mes "[パンプキング]";
		mes "あ、そのチケットはおまけです！";
		mes "よかったらまたどうぞ〜";
		close;
	}
}

prontera.gat,140,115,6	script	パンプキング	1130,{}
prontera.gat,140,115,6	duplicate(パンプキング#Haloween)	パンプキング	111
morocc.gat,149,90,6	script	パンプキング	1130,{}
morocc.gat,149,90,6	duplicate(パンプキング#Haloween)	パンプキング	111
geffen.gat,103,56,6	script	パンプキング	1130,{}
geffen.gat,103,56,6	duplicate(パンプキング#Haloween)	パンプキング	111
payon.gat,164,95,6	script	パンプキング	1130,{}
payon.gat,164,95,6	duplicate(パンプキング#Haloween)	パンプキング	111
alberta.gat,73,51,6	script	パンプキング	1130,{}
alberta.gat,73,51,6	duplicate(パンプキング#Haloween)	パンプキング	111
aldebaran.gat,138,92,6	script	パンプキング	1130,{}
aldebaran.gat,138,92,6	duplicate(パンプキング#Haloween)	パンプキング	111
yuno.gat,164,179,6	script	パンプキング	1130,{}
yuno.gat,164,179,6	duplicate(パンプキング#Haloween)	パンプキング	111
einbroch.gat,227,183,6	script	パンプキング	1130,{}
einbroch.gat,227,183,6	duplicate(パンプキング#Haloween)	パンプキング	111
lighthalzen.gat,171,107,6	script	パンプキング	1130,{}
lighthalzen.gat,171,107,6	duplicate(パンプキング#Haloween)	パンプキング	111
hugel.gat,93,134,6	script	パンプキング	1130,{}
hugel.gat,93,134,6	duplicate(パンプキング#Haloween)	パンプキング	111

//==========================================
// 姉弟のハロウィン
//------------------------------------------
cmd_fild07.gat,289,93,5	script	元気そうな兵士	105,{
	if(HALLOWEEN07_EVE) {
		mes "[イズレン]";
		mes "ファロス燈台島の地下には";
		mes "何があると思いますか？";
		mes "意味深な建物はありますが、";
		mes "入り口は閉鎖されています……";
		mes "凄く危ない何かが";
		mes "隠されているのでしょうか？";
		close;
	}
	mes "[イズレン]";
	mes "こんにちは。";
	mes "ファロス燈台島にようこそ。";
	next;
	mes "[イズレン]";
	mes "見ての通り、";
	mes "ここは静かな島です。";
	mes "娯楽の為の施設もありませんし、";
	mes "寂しさを感じてしまうぐらい";
	mes "静かな島なんです。";
	next;
	mes "[イズレン]";
	mes "騒がしい都会が嫌になり、";
	mes "ここへ療養しに来る方もいますが、";
	mes "ここに住んでいると……";
	mes "嫌だったはずの騒がしい人込みも、";
	mes "だんだん恋しくなってきたりします。";
	next;
	mes "[イズレン]";
	mes "そう言えば、";
	mes "そろそろハロウィンですね。";
	mes "私の弟はハロウィンを";
	mes "すごく楽しみにしているんです。";
	mes "……あっ！";
	mes "すみません、余計な話でしたね……";
	next;
	if(select("いえいえ、ではそろそろ失礼します","弟さんの事が好きなんですね")==1) {
		mes "[イズレン]";
		mes "時間を取ってしまって";
		mes "すみませんでした。";
		mes "では、良い一日を〜";
		close;
	}
	mes "[イズレン]";
	mes "ばれてしまいましたね。";
	mes "弟……イレンダルは、";
	mes "とても大切な家族です。";
	next;
	mes "[イズレン]";
	mes "^FF0000プロンテラ^000000で一緒に住んでいた時は、";
	mes "イレンダルの好きな事を";
	mes "何でも一緒にやっていましたが、";
	mes "私がここに配属されてからは";
	mes "あまり会えなくなって……";
	next;
	mes "[イズレン]";
	mes "まぁ、弟はしっかり者だから、";
	mes "元気にしてるとは思いますけど……";
	mes "少し心配です。";
	set HALLOWEEN07_EVE,1;
	close;
}

prontera.gat,128,117,0	script	リナ	717,{
	switch(HALLOWEEN07_EVE) {
	case 0:
		if(rand(5)) {
			mes "[リナ]";
			mes "キャンディ！　キャンディ！";
			mes "キャンディをくれないと";
			mes "どうなっても知らないんだから！";
			close;
		}
		mes "[リナ]";
		mes "アアッ！　に……肉っ！";
		mes "……おおっ……";
		mes "あ……お……お腹空いた……";
		next;
		mes "[リナ]";
		mes "ぇ？　何？";
		mes "私、何か言ってた？";
		close;
	case 1:
	case 2:
		mes "[リナ]";
		mes "ママに塔に連れてってもらうよう";
		mes "お願いしてみたけど……";
		mes "危ないからダメだって……";
		mes "私もイレンダルと一緒に";
		mes "塔で遊びたいのになぁ〜";
		if(HALLOWEEN07_EVE == 1)
			set HALLOWEEN07_EVE,2;
		close;
	default:
		mes "[リナ]";
		mes "キャンディ！　キャンディ！";
		mes "キャンディをくれないと";
		mes "どうなっても知らないんだから！";
		next;
		if(select("あげる","無視する")==2) {
			mes "[リナ]";
			mes "何よ、無視しないでよ！";
			misceffect 79,"";
			percentheal -10,0;
			close;
		}
		if(countitem(529) < 10) {
			mes "[リナ]";
			mes "キャンディ……";
			mes "10個は欲しいの！";
			misceffect 79,"";
			percentheal -10,0;
			close;
		}
		mes "[リナ]";
		mes "やったぁ〜";
		mes "キャンディだーい好き！";
		mes "はいこれ！";
		mes "お礼だよ。";
		delitem 529,10;
		getitem 505,1;
		close;
	}
}

prontera.gat,130,121,4	script	ケン	764,{
	switch(HALLOWEEN07_EVE) {
	case 0:
		if(rand(5)) {
			mes "[ケン]";
			mes "スティックキャンディを";
			mes "くれないとイタズラするよ！";
			mes "早く、早く！";
			mes "スティックキャンディ!!";
			close;
		}
		mes "[ケン]";
		mes "アアッ！　に……肉っ！";
		mes "……おおっ……";
		mes "あ……お……お腹空いた……";
		next;
		mes "[ケン]";
		mes "あれ？";
		mes "今変な事を言っていたような……？";
		close;
	case 1:
	case 2:
		mes "[ケン]";
		mes "イレンダル？";
		mes "うん、知ってるよ！";
		mes "何だっけ……";
		mes "^FF0000でっかい塔^000000に行くって言って、";
		mes "まだ帰ってきてないよ。";
		if(HALLOWEEN07_EVE == 1)
			set HALLOWEEN07_EVE,2;
		close;
	default:
		mes "[ケン]";
		mes "スティックキャンディ!!";
		mes "くれないとイタズラするよ!!";
		next;
		if(select("あげる","無視する")==2) {
			mes "[ケン]";
			mes "いじわるー!!";
			misceffect 16,"";
			percentheal -10,0;
			close;
		}
		if(countitem(530) < 5) {
			mes "[ケン]";
			mes "スティックキャンディ……";
			mes "5本は欲しいのー！";
			mes "うあぁぁぁん!!";
			misceffect 16,"";
			percentheal -10,0;
			close;
		}
		mes "[ケン]";
		mes "イヤッホー！";
		mes "スティックキャンディだっ!!";
		mes "やったぁ〜！";
		mes "じゃ、お礼にこれをあげる！";
		delitem 530,5;
		getitem 505,1;
		close;
	}
}

prontera.gat,127,122,5	script	ナナ	759,{
	switch(HALLOWEEN07_EVE) {
	case 0:
		if(rand(5)) {
			mes "[ナナ]";
			mes "よく焼いたクッキー……";
			mes "クッキーが食べたいなぁ……";
			mes "くれないと……泣いちゃうよ？";
			close;
		}
		mes "[ナナ]";
		mes "アアッ！　に……肉っ！";
		mes "……おおっ……";
		mes "あ……お……お腹空いた……";
		next;
		mes "[ナナ]";
		mes "？";
		mes "私、今へんな事を";
		mes "言ってなかった？";
		close;
	case 1:
	case 2:
		mes "[ナナ]";
		mes "イレンダル？";
		mes "^FF0000おっきい塔^000000に行くって。";
		mes "でも……おっきい塔って";
		mes "どこにあるのかな？";
		if(HALLOWEEN07_EVE == 1)
			set HALLOWEEN07_EVE,2;
		close;
	default:
		mes "[ナナ]";
		mes "よく焼いたクッキー……";
		mes "クッキーが食べたいなぁ……";
		mes "くれないと……泣いちゃうよ？";
		next;
		if(select("あげる","無視する")==2) {
			mes "[ナナ]";
			mes "ぅ……いじわる……";
			mes "しくしく……";
			misceffect 329,"";
			percentheal -10,0;
			close;
		}
		if(countitem(538) < 10) {
			mes "[ナナ]";
			mes "よく焼いたクッキー……";
			mes "できれば10個ぐらい欲しいな……";
			misceffect 329,"";
			percentheal -10,0;
			close;
		}
		mes "[ナナ]";
		mes "ありがとー！";
		mes "クッキー大好き！";
		mes "代わりにこれをどうぞ！";
		delitem 538,10;
		getitem 505,1;
		close;
	}
}

prontera.gat,125,120,6	script	ルーク	706,{
	switch(HALLOWEEN07_EVE) {
	case 0:
		if(rand(5)) {
			mes "[ルーク]";
			mes "ひとくちケーキを";
			mes "くれないとイタズラするよ！";
			mes "早く、早く！";
			mes "ひとくちケーキ!!";
			close;
		}
		mes "[ルーク]";
		mes "アアッ！　に……肉っ！";
		mes "……おおっ……";
		mes "あ……お……お腹空いた……";
		next;
		mes "[ルーク]";
		mes "ハッ！";
		mes "僕は今、何を言って？";
		close;
	case 1:
	case 2:
		mes "[ルーク]";
		mes "イレンダル？";
		mes "そいつなら旅に出るって";
		mes "言ってたよ。";
		mes "^FF0000遠い西にあるでっかい街^000000";
		mes "に行くんだってさ。";
		if(HALLOWEEN07_EVE == 1)
			set HALLOWEEN07_EVE,2;
		close;
	default:
		mes "[ルーク]";
		mes "ひとくちケーキを";
		mes "くれないとイタズラするよ！";
		mes "早く、早く！";
		mes "ひとくちケーキ!!";
		next;
		if(select("あげる","無視する")==2) {
			mes "[ルーク]";
			mes "こっちを見ろ！";
			misceffect 107,"";
			percentheal -10,0;
			close;
		}
		if(countitem(539) < 3) {
			mes "[ルーク]";
			mes "ひとくちケーキ、";
			mes "3個はくれよ！";
			misceffect 107,"";
			percentheal -10,0;
			close;
		}
		mes "[ルーク]";
		mes "やったぁー!!";
		mes "ケーキだ、ケーキ!!";
		mes "それじゃ代わりに";
		mes "俺はこれをあげる!!";
		delitem 539,3;
		getitem 505,1;
		close;
	}
}

geffen.gat,144,61,5	script	一人ぼっちの子供	896,{
	switch(HALLOWEEN07_EVE) {
	default:
		mes "[イレンダル]";
		mes "ハァ……ハァ……";
		mes "やっとついた……";
		mes "ハロウィンにも……";
		mes "ハァ……ハァ……";
		mes "間に合った……";
		close;
	case 2:
		mes "[イレンダル]";
		mes "ハロウィンのイベントも";
		mes "すごく久しぶりだなぁ……";
		mes "お姉ちゃんと一緒にいた頃は、";
		mes "毎年一緒にきてたのに……";
		next;
		if(select("……","イズレンとの話を伝える")==1) {
			mes "[イレンダル]";
			mes "よっし！";
			mes "お姉ちゃんの分まで";
			mes "めいっぱい遊ぶぞ！";
			close;
		}
		emotion 28;
		mes "[イレンダル]";
		mes "ぇ？　そっか……";
		mes "お姉ちゃんは元気にしてるんだ……";
		mes "良かった……";
		next;
		mes "[イレンダル]";
		mes "お姉ちゃんがファロス燈台島に";
		mes "赴任してから、";
		mes "僕も毎日頑張ってたんだよ。";
		next;
		mes "[イレンダル]";
		mes "最初はいきなり一人になったから、";
		mes "すごく寂しかったけど……";
		mes "僕が元気にしていないと、";
		mes "お姉ちゃんが心配すると思って";
		mes "頑張る事にしたんだ。";
		next;
		mes "[イレンダル]";
		mes "友達とも遊んだし、";
		mes "勉強も運動も頑張った！";
		mes "僕もお姉ちゃんのように";
		mes "立派な兵士になって、";
		mes "お姉ちゃんと一緒に働くんだ！";
		next;
		mes "[イレンダル]";
		mes "だから……";
		mes "お姉ちゃんのような兵士になれる";
		mes "方法を思いついたんだ！";
		mes "それは、自分を鍛える一人旅！";
		next;
		mes "[イレンダル]";
		mes "まずは、プロンテラから";
		mes "ゲフェンまで一人で行ってみよう！";
		mes "って思ったんだ。";
		next;
		mes "[イレンダル]";
		mes "最初のうちは予想より大変で、";
		mes "途中で諦めて家に戻ったりもしたけど、";
		mes "今回はちゃんとここまで";
		mes "一人でこれたんだ！";
		next;
		mes "[イレンダル]";
		mes "あの大きな塔を目にした瞬間、";
		mes "お姉ちゃんにやっと追いついた。";
		mes "そんな気がしたんだ……";
		next;
		mes "[イレンダル]";
		mes "そうそう！";
		mes "今年のハロウィンイベントだと、";
		mes "すぐそこにいるおじさんが";
		mes "ニブルヘイムにつれて行って";
		mes "くれるんだよ！";
		next;
		mes "[イレンダル]";
		mes "お姉ちゃんも多分行った事がない";
		mes "ニブルヘイムにまで行けて、";
		mes "今、すっごく嬉しいんだ……";
		mes "……けど……";
		mes "…………";
		next;
		mes "[イレンダル]";
		mes "けど……";
		mes "一つだけ足りないんだ……";
		mes "お姉ちゃんが毎年作ってくれてた";
		mes "パンプキンハット……";
		mes "それだけが……足りないんだ……";
		next;
		mes "[イレンダル]";
		mes "お姉ちゃんは毎年、";
		mes "ハロウィンには必ず";
		mes "パンプキンハットを作って";
		mes "くれたんだ……";
		next;
		emotion 9;
		mes "[イレンダル]";
		mes "……また、^FF0000パンプキンハット^000000を";
		mes "被ってみたいなぁ……";
		set HALLOWEEN07_EVE,3;
		close;
	case 3:
		if(countitem(5356) < 1) {
			mes "[イレンダル]";
			mes "お姉ちゃんが言ってた。";
			mes "自分を信じる、自分の力を信じる。";
			mes "だから人は強くなる。";
			mes "だから願いが叶えられるって……";
			next;
			mes "[イレンダル]";
			mes "お姉ちゃんの言う通りだと思う。";
			mes "だから、僕は自分の力で";
			mes "パンプキンハットを手に入れるんだ！";
			close;
		}
		mes "[イレンダル]";
		mes "わ！　パンプキンハットだ！";
		mes "すごい、すごい！";
		mes "凄く格好いい！";
		mes "…………いいなぁ……";
		mes "お姉ちゃんがいたら";
		mes "僕もパンプキンハットを……";
		next;
		mes "[イレンダル]";
		mes "…………違う！　違う！";
		mes "弱気になんてなってない！";
		mes "今年は自分で作るんだ！";
		next;
		if(select("頑張って！","パンプキンハットをあげる")==1) {
			mes "[イレンダル]";
			mes "うんっ！";
			mes "よーし、頑張るぞ!!";
			close;
		}
		mes "[イレンダル]";
		mes "えぇ!?　ほ、本当？";
		mes "あ〜ぅ〜……";
		mes "でも……うぅ〜……";
		mes "……うん、やっぱりいいや。";
		next;
		mes "[イレンダル]";
		mes "ごめんなさい。";
		mes "今年は、自分の力で";
		mes "パンプキンハットを作りたいんだ。";
		mes "気持ちだけで十分！";
		next;
		if(select("そう……","お姉ちゃんからだと思って……")==1) {
			mes "[イレンダル]";
			mes "ぅ……ごめんなさい。";
			mes "今年はどうしても";
			mes "自分で作ってみたいんだ。";
			close;
		}
		mes "[" +strcharinfo(0)+ "]";
		mes "私からじゃなくて、";
		mes "イズレンさんからだと思って";
		mes "受け取ってくれないかな？";
		mes "自分の力で作りたいなら、";
		mes "イズレンさんに作ってあげるといい。";
		next;
		mes "[イレンダル]";
		mes "う……ん…………";
		mes "そこまで言うなら……";
		mes "いいよ、もらってあげる！";
		next;
		mes "[イレンダル]";
		mes "これは……";
		mes "お" +(Sex? "兄": "姉")+ "さんがパンプキンハットを";
		mes "くれたからって、";
		mes "あげるわけじゃないよ。";
		mes "ただ……その……";
		mes "余ってるからあげるだけだよ！";
		set HALLOWEEN07_EVE,4;
		delitem 5356,1;
		getitem 7460,1;
		close;
	case 4:
		mes "[イレンダル]";
		mes "ぇ？　パンプキンハット!?";
		mes "いや、その……これは……";
		mes "嬉しくて被ってるんじゃなくて……";
		mes "……そう！";
		mes "大きくて持ち辛いから";
		mes "被っているだけだからね！";
		close;
	}
}

geffen.gat,148,64,4	script	暗い男	794,{
	if(HALLOWEEN07_EVE != 4) {
		mes "[デニス]";
		mes "……あの世を見たくはないか？";
		mes "…………";
		mes "いや……君はまだ";
		mes "準備が整っていないな……";
		close;
	}
	mes "[デニス]";
	mes "……あの世を見たいようだな。";
	mes "チケットは持っているか？";
	next;
	if(select("はい、お願いします","あの世には行きたくない……")==2) {
		mes "[デニス]";
		mes "……それは本心かね？";
		mes "人間は皆、";
		mes "いつかそこに行くというのに……";
		close;
	}
	if(countitem(7460) < 1) {
		mes "[デニス]";
		mes "チケットさえあれば";
		mes "あの世に送ってやろう……";
		mes "…………";
		mes "……ふふ……君は……";
		mes "あの世への移住を";
		mes "考えているようだな……";
		close;
	}
	mes "[デニス]";
	mes "良いだろう。";
	mes "しばらくの間、";
	mes "あの世を楽しむといい。";
	mes "ただし……間違って死んだりしても、";
	mes "我は知らぬがな……";
	close2;
	delitem 7460,1;
	warp "nif_in.gat",65,20;
	end;
}
