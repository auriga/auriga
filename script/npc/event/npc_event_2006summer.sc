//=====================================================================
// モンスター
//=====================================================================
amatsu.gat,0,0,0,0	monster	ガジョマート	1309,1,1800000,900000,0
amatsu.gat,0,0,0,0	monster	ホロン	1129,5,600000,300000,0

//=====================================================================
// 転送NPC
//=====================================================================
prontera.gat,104,65,5	script	アマツ観光協会員#summer	757,{
	emotion 3;
	mes "[アマツ観光協会員]";
	mes "お元気ですか？";
	mes "今、泉水の国アマツでは";
	mes "『^0000FFアマツ納涼泉水祭^000000』を開催中です！";
	mes "今なら^FF00CE無料^000000でアマツまでご案内";
	mes "していますよ♪";
	next;
	if(select("アマツに行く","行かない")==2) {
		emotion 28;
		mes "[アマツ観光協会員]";
		mes "そうですかー……";
		mes "行きたくなったらいつでも";
		mes "声をかけてくださいね！";
		close;
	}
	emotion 21;
	emotion 27,"";
	mes "[アマツ観光協会員]";
	mes "かしこまりました！";
	mes "それではアマツにご案内いたします!!";
	next;
	warp "amatsu.gat",197,83;
	end;
}
morocc.gat,163,56,4	duplicate(アマツ観光協会員#summer)	アマツ観光協会員	757
geffen.gat,104,56,6	duplicate(アマツ観光協会員#summer)	アマツ観光協会員	757
payon.gat,165,100,5	duplicate(アマツ観光協会員#summer)	アマツ観光協会員	757
alberta.gat,235,95,4	duplicate(アマツ観光協会員#summer)	アマツ観光協会員	757
izlude.gat,134,93,4	duplicate(アマツ観光協会員#summer)	アマツ観光協会員	757

amatsu.gat,200,79,4	script	特殊客船案内人	109,{
	mes "[特殊客船案内人]";
	mes "いらっしゃいませ。";
	mes "アマツ納涼泉水祭、";
	mes "楽しんでおられますか？";
	mes "私は皆様を格安のお値段で";
	mes "各地の船着場へお届けする";
	mes "特殊客船の案内人で御座います。";
	next;
	mes "[特殊客船案内人]";
	mes "祭事中のみの特別サービスと";
	mes "なります。";
	mes "是非、ご利用ください。";
	next;
	while(1) {
		switch(select("^9C31FF近海^000000航路","^0000CE遠海^000000航路","利用しない")) {
		case 1:
			switch(select("イズルード港 -> 250 z","バイラン島 -> 50 z","コモドファロス燈台 -> 300 z","沈没した海賊船 -> 125 z","幻想の島コモド -> 900 z","航路選択に戻る")) {
			case 1:
				set '@zeny,250;
				set '@mapname$,"izlude";
				setarray '@warp,176,182;
				break;
			case 2:
				set '@zeny,50;
				set '@mapname$,"izlu2dun";
				setarray '@warp,107,50;
				break;
			case 3:
				set '@zeny,300;
				set '@mapname$,"cmd_fild07";
				setarray '@warp,82,121;
				break;
			case 4:
				set '@zeny,125;
				set '@mapname$,"alb2trea";
				setarray '@warp,43,53;
				break;
			case 5:
				set '@zeny,900;
				set '@mapname$,"comodo";
				setarray '@warp,202,78;
				break;
			case 6:
				continue;
			}
			break;
		case 2:
			switch(select("タートルアイランド -> 5000 z","コンロン -> 5000 z","龍之城 -> 5000 z","アユタヤ -> 5000 z","ジュノーフィールド11 -> 5000 z","航路選択に戻る")) {
			case 1:
				set '@mapname$,"tur_dun01";
				setarray '@warp,159,35;
				break;
			case 2:
				set '@mapname$,"gon_fild01";
				setarray '@warp,262,87;
				break;
			case 3:
				set '@mapname$,"lou_fild01";
				setarray '@warp,197,112;
				break;
			case 4:
				set '@mapname$,"ayothaya";
				setarray '@warp,148,68;
				break;
			case 5:
				set '@mapname$,"yuno_fild11";
				setarray '@warp,300,124;
				break;
			case 6:
				continue;
			}
			set '@zeny,5000;
			break;
		case 3:
			mes "[特殊客船案内人]";
			mes "左様で御座いますか。";
			mes "機会がありましたら";
			mes "是非、ご利用ください。";
			close;
		}
		if(Zeny < '@zeny) {
			mes "[特殊客船案内人]";
			mes "申し訳御座いませんが";
			mes "お金が足りないようですね。";
			mes "またご利用ください。";
			close;
		}
		set Zeny,Zeny-'@zeny;
		mes "[特殊客船案内人]";
		mes "では、良い旅を……";
		next;
		warp '@mapname$,'@warp[0],'@warp[1];
		end;
	}
}

amatsu.gat,272,230,4	script	巫女	769,{
	emotion 3;
	mes "[桜子]";
	mes "ようこそ、アマツへ！";
	mes "私はアマツ納涼泉水祭";
	mes "実行委員の桜子といいます。";
	mes "よろしくお願いしますね。";
	next;
	mes "[桜子]";
	mes "様々な方がお祭に出し物を";
	mes "していらっしゃいますが、";
	mes "私の出し物はズバリ、";
	mes "空間転送サービスです！";
	next;
	mes "[桜子]";
	mes "私の巫術で色んな場所に";
	mes "ご案内しますよ！";
	mes "……でも、すぐには";
	mes "ご案内できませんよ〜";
	next;
	mes "[桜子]";
	mes "せっかくの納涼泉水祭ですから、";
	mes "^0000FF涼しげな収集品^000000を持ってきて";
	mes "みてください！";
	mes "持っていたら、ご案内可能な場所を";
	mes "お教えしますよ♪";
	mes "判定は私の独断と偏見です！";
	next;
	if(countitem(749))
		set '@itemname$,getitemname(749);
	else if(countitem(7036))
		set '@itemname$,getitemname(7036);
	else if(countitem(7066))
		set '@itemname$,getitemname(7066);
	else {
		mes "[桜子]";
		mes "まあ、やっぱり……";
		mes "冷たそうな物が妥当だと";
		mes "思いますよ。";
		close;
	}
	emotion 5;
	mes "[桜子]";
	mes "……あら？";
	mes "それは^0000FF" +'@itemname$+ "^000000ですね。";
	mes "実に涼しげな一品ですね！";
	mes "合格です！";
	next;
	mes "[桜子]";
	mes "では、ご案内可能な場所を";
	mes "お教えします！";
	mes "ちなみに私は場所を色のイメージで";
	mes "覚えているので、色の名前を";
	mes "どれか選んでくださいね。";
	next;
	mes "[桜子]";
	mes "私がそのイメージした色の場所に";
	mes "お送りします。";
	mes "結構力を使うので高めですけど……";
	mes "それでも良ければご利用ください！";
	next;
	while(1) {
		switch(select("^9C31FF1頁目^000000","^0000CE2頁目^000000","利用しない")) {
		case 1:
			switch(select("^008463常盤緑^000000 -> 1000 z","^214A7B青藍^000000 -> 2000 z","^9C3939蘇芳^000000 -> 5000 z","^291810鉄黒^000000 -> 5000 z","頁選択へ戻る")) {
			case 1:
				set '@zeny,1000;
				set '@mapname$,"prt_maze02";
				setarray '@warp,106,100;
				break;
			case 2:
				set '@zeny,2000;
				set '@mapname$,"iz_dun04";
				setarray '@warp,233,267;
				break;
			case 3:
				set '@zeny,5000;
				set '@mapname$,"mag_dun02";
				setarray '@warp,244,66;
				break;
			case 4:
				set '@zeny,5000;
				set '@mapname$,"ein_dun02";
				setarray '@warp,32,22;
				break;
			case 5:
				continue;
			}
			break;
		case 2:
			switch(select("^313131黒緑^000000 -> 5555 z","^EF3929紅緋^000000 -> 5555 z","^BD63A5若紫^000000 -> 8888 z","^5A4252減紫^000000 -> 9696 z","頁選択へ戻る")) {
			case 1:
				set '@zeny,5555;
				set '@mapname$,"lhz_dun01";
				setarray '@warp,149,286;
				break;
			case 2:
				set '@zeny,5555;
				set '@mapname$,"gl_dun02";
				setarray '@warp,26,32;
				break;
			case 3:
				set '@zeny,8888;
				set '@mapname$,"gl_sew03";
				setarray '@warp,310,50;
				break;
			case 4:
				if(Zeny < 9696) {
					mes "[桜子]";
					mes "申し訳御座いませんが";
					mes "お金が足りないようですね。";
					mes "またご利用ください。";
					close;
				}
				mes "[桜子]";
				mes "これは……実は行き先が";
				mes "二箇所あるんです。";
				mes "時計のような……洞窟のような……";
				mes "どちらに行ってしまうかわかりません。";
				mes "それでもよろしいですか？";
				next;
				if(select("OK！","嫌だ")==2) {
					mes "[桜子]";
					mes "ですよね……";
					mes "他のを選んでください！";
					next;
					continue;
				}
				set Zeny,Zeny-'@zeny;
				misceffect 8;
				mes "[桜子]";
				mes "じゃあ行きますよ！";
				next;
				if(rand(2))
					warp "c_tower4",117,112;
				else
					warp "alde_dun04",120,256;
				end;
			case 5:
				continue;
			}
			break;
		case 3:
			mes "[桜子]";
			mes "わかりました！";
			mes "どこかに行きたくなったら";
			mes "また来てくださいね！";
			close;
		}
		if(Zeny < '@zeny) {
			mes "[桜子]";
			mes "申し訳御座いませんが";
			mes "お金が足りないようですね。";
			mes "またご利用ください。";
			close;
		}
		set Zeny,Zeny-'@zeny;
		misceffect 8;
		mes "[桜子]";
		mes "じゃあ行きますよ！";
		next;
		warp '@mapname$,'@warp[0],'@warp[1];
		end;
	}
}

amatsu.gat,195,238,4	script	カプラ職員？	791,{
	misceffect 33;
	mes "[カプラ職員？]";
	mes "うらめしや〜〜……";
	next;
	if(countitem(719) < 1) {
		emotion 23,"";
		mes "[" +strcharinfo(0)+ "]";
		mes "ギャー!!";
		mes "出たーっ!!";
		next;
		warp "amatsu.gat",195,180;
		end;
	}
	emotion 9,"";
	mes "-^63319Cアメジスト^000000を持っているからか-";
	mes "-心が落ち着いていて-";
	mes "-ちっとも怖くない。-";
	mes "-ひょっとしてこれは幽霊ではない？-";
	next;
	emotion 9;
	mes "[カプラ職員？]";
	mes "……ちっとも驚かないんですね……";
	mes "他の人たちは皆びっくりして";
	mes "一目散に逃げてしまわれるのに！";
	next;
	emotion 18;
	mes "[カプラ職員]";
	mes "あはは、私はちゃんとした";
	mes "カプラサービスの人間ですよ！";
	mes "アマツ納涼泉水祭にあわせて";
	mes "幽霊の仮装をしているんです。";
	next;
	mes "[カプラ職員]";
	mes "『夏と言えば怪談！幽霊！』";
	mes "『幽霊といえば水のそば』";
	mes "って指示されてこの場所に居るんですが";
	mes "賑わっている場所から離れているので";
	mes "ちょっと寂しいですね……";
	next;
	mes "[カプラ職員]";
	mes "あ、私はここで";
	mes "特別な空間転送サービスをしています。";
	mes "納涼泉水祭にちなんで";
	mes "身も心も涼しくなってしまうような";
	mes "場所をご用意しております。";
	mes "良かったらご利用くださいね。";
	next;
	switch(select("雪で涼む -> 2222 z","古城で涼む -> 4444 z","死者と涼む -> 6666 z","洞窟で涼む -> 6666 z")) {
	case 1:
		set '@zeny,2222;
		set '@mapname$,"xmas";
		setarray '@warp,235,308;
		break;
	case 2:
		set '@zeny,4444;
		set '@mapname$,"glast_01";
		setarray '@warp,199,32;
		break;
	case 3:
		set '@zeny,6666;
		set '@mapname$,"niflheim";
		setarray '@warp,274,152;
		break;
	case 4:
		set '@zeny,6666;
		set '@mapname$,"jupe_cave";
		setarray '@warp,140,51;
		break;
	}
	if(Zeny < '@zeny) {
		mes "[カプラ職員]";
		mes "あらら、";
		mes "お金が足りないようですね。";
		mes "またご利用くださいね。";
		close;
	}
	set Zeny,Zeny-'@zeny;
	mes "[カプラ職員]";
	mes "では、いってらっしゃいませ！";
	next;
	warp '@mapname$,'@warp[0],'@warp[1];
	end;
}

//=====================================================================
// 行商
//=====================================================================
amatsu.gat,171,118,6	shop	行商	53,516,535,515,512,513,519,517,528,501,502,503,504,506,537
amatsu.gat,177,122,4	shop	行商	770,1750,1770,1752,1751,1065,1247,1248,1249,13000
amatsu.gat,182,121,4	shop	行商	77,611,601,602,717,645,656,657,610
amatsu.gat,172,112,6	shop	行商	886,581,580,577,7482,579,7457,7453,7455,7454,7456,7452

amatsu.gat,208,123,3	script	輸入洋酒屋	766,{
	mes "[輸入洋酒屋のおじいさん]";
	mes "いらっしゃい。";
	mes "祭はいいもんじゃのう。";
	mes "やはり祭は酒じゃろう〜！";
	mes "祭が来ると、若返った気分になるぞい！";
	mes "わしは輸入物の酒を扱っておるんじゃ。";
	mes "手数料込みで原価の^FF0000二割高^000000じゃがの。";
	next;
	switch(select("トロピカル・ソグラト","ヴァーミリオン・ザ・ビーチ","他にないの？","いらないです")) {
	case 1:
		set '@itemid,12112;
		break;
	case 2:
		set '@itemid,12113;
		break;
	case 3:
		mes "[輸入洋酒屋のおじいさん]";
		mes "他の酒かい。";
		mes "マステラ酒や赤いきのこワインは";
		mes "あったんじゃが……";
		mes "どうにも数が少なくてのう。";
		mes "売るほど物がないんじゃよ。";
		mes "自分で作ってみてはどうかの？";
		close;
	case 4:
		mes "[輸入洋酒屋のおじいさん]";
		mes "そうかい。";
		close;
	}
	if((MaxWeight - Weight) < 2000) {
		mes "[輸入洋酒屋のおじいさん]";
		mes "お客さん、荷物が溢れそうじゃぞ。";
		mes "酒はかさばるからのう……";
		mes "荷物を軽くしてからおいで。";
		close;
	}
	mes "[輸入洋酒屋のおじいさん]";
	mes "これじゃな。";
	if('@itemid == 12112) {	//トロピカル・ソグラト
		mes "甘い果実酒じゃが度数は高めじゃ。";
		mes "わしは辛口の酒が好きなんじゃが……";
	}
	else {			//ヴァーミリオン・ザ・ビーチ
		mes "明るい色合いが女性にも人気が高い。";
		mes "わしは濁り酒が好きなんじゃが……";
	}
	mes "……で、値段は^0000FF1200^000000zenyじゃ。";
	mes "いるかね？";
	next;
	if(select("買う","買わない")==2) {
		mes "[輸入洋酒屋のおじいさん]";
		mes "そうかい。";
		close;
	}
	if(Zeny < 1200) {
		emotion 7;
		mes "[輸入洋酒屋のおじいさん]";
		mes "……";
		mes "お金が足りんぞい。";
		close;
	}
	mes "[輸入洋酒屋のおじいさん]";
	mes "^0000FF10^000000本までまとめ買いできるぞい。";
	mes "買いたい本数を教えておくれ。";
	mes "ちなみに^0000FF10^000000本なら^0000FF11500^000000zenyに";
	mes "割引サービスじゃ。";
	while(1) {
		next;
		input '@num;
		if('@num < 1 || '@num > 10) {
			emotion 7;
			mes "[輸入洋酒屋のおじいさん]";
			mes "1〜10個までと";
			mes "言ったじゃろう？";
			mes "次は間違えないでおくれ。";
			continue;
		}
		if('@num == 10)
			set '@zeny,11500;
		else
			set '@zeny,'@num*1200;
		if(Zeny < '@zeny) {
			emotion 7;
			mes "[輸入洋酒屋のおじいさん]";
			mes "……";
			mes "お金が足りんぞい。";
			close;
		}
		set Zeny,Zeny-'@zeny;
		getitem '@itemid,'@num;
		mes "[輸入洋酒屋のおじいさん]";
		mes "毎度ありがとう。";
		mes "祭を楽しんでいきなされ。";
		mes "じゃが酒はほどほどにな。";
		close;
	}
}

//=====================================================================
// 一般人
//=====================================================================
amatsu.gat,189,90,6	script	腹ペコ少年	764,{
	mes "[寿司田部大洋]";
	mes "あー……";
	mes "お腹空いた……";
	mes "お寿司……お寿司が食べたいよう！";
	mes "僕のお爺ちゃん、たくさんお寿司を";
	mes "持ってるのに、ひとつもくれないんだ！";
	next;
	mes "[寿司田部大洋]";
	mes "お・寿・司!!　お・寿・司!!";
	mes "ねえねえカッコイイお兄ちゃん、";
	mes "お寿司頂戴よう〜！";
	mes "僕が持ってる他の食べ物を";
	mes "あげるからさ！";
	next;
	if(select("あげない","お寿司 ^0000FF100個^000000あげる")==1) {
		emotion 28;
		mes "[寿司田部大洋]";
		mes "けちんぼ!!";
		mes "うあーん！　お寿司が食べたい!!";
		mes "おもちは食べ飽きたよ〜!!";
		mes "魚のたんぱく質が欲しいよ〜!!";
		close;
	}
	if(countitem(551) < 100) {
		emotion 28;
		mes "[寿司田部大洋]";
		mes "うあーん!!";
		mes "そんなにお寿司持ってないじゃないか！";
		mes "騙されたー!!";
		mes "ひどいやひどいやー!!";
		close;
	}
	delitem 551,100;
	emotion 2;
	mes "[寿司田部大洋]";
	mes "わーい、やった!!";
	mes "こんなにお寿司くれるんだね!?";
	mes "ありがとう!!";
	next;
	emotion 9,"";
	mes "^0000FF−少年はお寿司100個を−";
	mes "−あっという間に平らげた……−^000000";
	next;
	mes "[寿司田部大洋]";
	mes "ごちそうさま!!";
	mes "すっごくおいしかったよ!!";
	mes "お礼にこれをあげるね。";
	mes "またお寿司ちょうだいね!!";
	close;
}

amatsu.gat,190,106,6	script	お婆さん	760,{
	mes "[お婆さん]";
	mes "ようこそいらっしゃいました。";
	mes "お祭りは楽しんでいますか？";
	mes "お祭りの時間は短いですが";
	mes "楽しんでいってくださいね。";
	next;
	if(select("楽しませていただきます","お化けがいるのですが……")==1) {
		mes "[お婆さん]";
		mes "でも、くれぐれも";
		mes "遊び過ぎないように";
		mes "注意してくださいね。";
		close;
	}
	mes "[お婆さん]";
	mes "確かに物の怪の類は";
	mes "おりますよ。";
	mes "お祭りの華やかな雰囲気に";
	mes "惹かれるモノもいますからね。";
	next;
	mes "[お婆さん]";
	mes "ただ、心が乱れていると";
	mes "物の怪でないものがそう見えてしまう";
	mes "こともあるでしょうね……";
	mes "そんな時は^63319Cアメジスト^000000を";
	mes "持っているといいですよ。";
	next;
	mes "[お婆さん]";
	mes "貴方から恐怖や不安を取り除いて";
	mes "正しく物事を見つめることが";
	mes "出来るかもしれません。";
	close;
}

amatsu.gat,212,106,3	script	怖そうなお姉さん	747,{
	mes "[怖そうなお姉さん]";
	mes "へぇー、なかなか";
	mes "賑わってるじゃない。";
	mes "アマツのお祭りには";
	mes "初めて来てみたんだけど";
	mes "いい雰囲気だね。";
	next;
	misceffect 18;
	mes "[怖そうなお姉さん]";
	mes "これだけ人がいるなら";
	mes "仕事もやりやすそうだね……";
	mes "……";
	mes "……あ、いやなんでもない、";
	mes "こっちの話だよ。";
	close;
}

amatsu.gat,204,176,4	script	暗がりの男	97,{
	mes "[暗がりの男]";
	mes "うう……うぷっ……";
	mes "寿司……お寿司が……";
	mes "お寿司1個が……100個くらいに";
	mes "増えたから一気に食べちゃったんだ……";
	next;
	emotion 26;
	mes "[暗がりの男]";
	mes "胃が痛い……！";
	mes "き、きみは僕を真似しちゃだめだぞ。";
	mes "一気に食べるのは良くない……！";
	mes "こ、これではせっかくの祭りが";
	mes "楽しめないじゃないか……！";
	close;
}

amatsu.gat,190,197,7	script	震える男	748,{
	emotion 23;
	mes "[震える男]";
	mes "うぁーっ!!";
	mes "ビックリした……!!";
	mes "急に話しかけないでくれよっ!!";
	next;
	mes "[震える男]";
	mes "それより聞いてくれよ……!!";
	mes "こ、こ、ここここ……";
	mes "この先にっ……出たっ……";
	mes "出たんだよっ……アレがっ……!!";
	next;
	mes "[震える男]";
	mes "お、俺は信じないぞ……!!";
	mes "ま、まったく非科学的だ……!!";
	mes "そ、そうだ！";
	mes "これは錯覚だ……！";
	mes "または電磁波かっ!!";
	mes "ブツブツ……";
	close;
}

amatsu.gat,208,214,4	script	旅する女性	726,{
	mes "[旅する女性]";
	mes "やっほー！";
	mes "貴方も遊びに来てるの？";
	mes "アマツのお祭りっていい雰囲気だね。";
	next;
	mes "[旅する女性]";
	mes "お寿司で遊べるってのも";
	mes "アマツらしくていいんじゃない？";
	mes "あたしはもーボロ負け！";
	mes "アハハハハ!!";
	next;
	mes "[旅する女性]";
	mes "せっかくだからお祭りの間くらい";
	mes "ここに滞在して露店を開くのも";
	mes "いいかもしれないなー。";
	mes "ふふっ、そんなこと考えてるだけでも";
	mes "楽しいんだよね、お祭りってさ。";
	close;
}

amatsu.gat,209,213,4	script	場違いお兄さん	715,{
	mes "[場違いお兄さん]";
	mes "オー……";
	mes "『海の向こうでカーニバルしてるよ！』";
	mes "って聞いたから、";
	mes "もうその場のノリと勢いで";
	mes "こんな格好で船に乗ってみたら……";
	next;
	emotion 16;
	mes "[場違いお兄さん]";
	mes "まさかアマツとは……！";
	mes "俺、完全にBA・CHI・GA・I!!";
	mes "場違いの罰GUYサー!!";
	mes "まさかこんな形で道化を演じるなんて";
	mes "トホホのホー!!!!";
	close;
}

amatsu.gat,238,208,4	script	疲れきった女性	101,{
	mes "[疲れきった女性]";
	mes "あー……";
	mes "ふー……";
	next;
	mes "[疲れきった女性]";
	mes "あら……ごめんなさい……";
	mes "見苦しかったわよね。";
	mes "お祭りなんて久しぶりだから";
	mes "ついついはしゃいじゃって……";
	next;
	mes "[疲れきった女性]";
	mes "そしたらすぐ疲れちゃって";
	mes "動悸が止まらないのよ……";
	mes "頭の中では自分の体は丈夫だと";
	mes "思ってたのに、";
	mes "いざ体を使うとこれだもの……";
	mes "もう年かしら……";
	close;
}

amatsu.gat,244,237,7	script	全然勝てない男	753,{
	mes "[全然勝てない男]";
	mes "ぬがーッッッ!!!!";
	mes "か、勝てない!!!!";
	next;
	mes "[全然勝てない男]";
	mes "見た目は単純に確率の問題だ……！";
	mes "後はただ俺の運を引きこむ力だけが";
	mes "武器のはずなんだが……";
	mes "何故勝てない!!";
	mes "あの協会員に騙されているのか!?";
	mes "この俺が!?";
	next;
	misceffect 1;
	mes "[全然勝てない男]";
	mes "俺の馬鹿！馬鹿馬鹿!!";
	next;
	mes "[全然勝てない男]";
	mes "勝負は時の運……";
	mes "いつか勝ってやる……!!";
	close;
}

amatsu.gat,259,221,7	script	怪しい爺さん	120,{
	mes "[怪しい爺さん]";
	mes "ほぉ……";
	mes "ふぅーむ……";
	mes "ふむ……ふむ……";
	next;
	menu "何をしているのです？",-;
	emotion 23;
	mes "[怪しい爺さん]";
	mes "どわぁーッ！";
	next;
	mes "[怪しい爺さん]";
	mes "お、驚かすでないわ";
	mes "このたわけがッッ!!";
	mes "わしは民俗学を研究している者じゃ！";
	next;
	mes "[怪しい爺さん]";
	mes "アマツの民族的衣装……";
	mes "とりわけあの巫女装束とやらが";
	mes "専らの研究対象じゃ!!";
	mes "滅多に見れぬのでいい機会なんじゃ。";
	next;
	emotion 43;
	mes "[怪しい爺さん]";
	mes "良いか。";
	mes "わしは研究をしているだけじゃ。";
	mes "何も後ろめたいことなどないぞ!!";
	mes "わかったらどこかへ行くんじゃ!!";
	mes "気が散るんじゃ!!";
	close;
}

amatsu.gat,188,153,5	script	わたあめ親父#summer	826,{
	emotion 18,"わたあめ親父#summer";
	mes "[わたあめ親父]";
	mes "らっしゃいらっしゃい！";
	mes "美味しいわたあめだよ！";
	mes "どうだ子供達よ！";
	mes "美味しそうな色してるだろ？";
	next;
	emotion 6,"子供#summer1";
	emotion 6,"子供#summer2";
	mes "[子供たち]";
	mes "嘘ばっかり！";
	mes "こんな目玉がついてて";
	mes "動いているわたあめなんて";
	mes "あるわけないよ！";
	mes "この妖怪おやじ!!";
	next;
	emotion 6,"わたあめ親父#summer";
	mes "[わたあめ親父]";
	mes "ぬわぁんだとぉ!!?";
	mes "人を妖怪呼ばわりとはいい度胸だ！";
	next;
	mes "[わたあめ親父]";
	mes "……ところでそこの" +((Sex)? "兄": "姉")+ "ちゃん、";
	mes "なんかようかい？";
	mes "なんつってな!!　ガハハ!!";
	next;
	emotion 9,"";
	misceffect 28,"";
	mes "[" +strcharinfo(0)+ "]";
	mes "………………";
	close;
}
amatsu.gat,190,155,3	duplicate(わたあめ親父#summer)	子供#summer1	706
amatsu.gat,186,151,7	duplicate(わたあめ親父#summer)	子供#summer2	96
amatsu.gat,187,153,4	script	わたあめ	1620,{}
amatsu.gat,189,153,4	script	わたあめ	1621,{}

amatsu.gat,241,106,6	script	子供	703,{
	emotion 28;
	mes "[子供]";
	mes "あう〜";
	mes "お祭りでみんなで";
	mes "かき氷を食べようと思って";
	mes "フリーズタートル型かき氷機を";
	mes "持ってきたのに";
	mes "壊れちゃったみたい……";
	next;
	mes "[子供]";
	mes "ハティーベベ型かき氷機も";
	mes "持ってきておけば良かった……";
	mes "悔しい〜!!";
	next;
	emotion 29;
	mes "[子供]";
	mes "こうなったらヤケよ！";
	mes "氷片をそのままかじっちゃうんだから！";
	mes "ひゃ〜涼しい〜!!";
	mes "納涼最高〜!!";
	close;
}

amatsu.gat,243,106,4	script	かき氷機#summer	1319,{}
amatsu.gat,243,105,0	script	かき氷機	111,{
	emotion 17,"かき氷機#summer";
	mes "<<^0000FFかき氷機^000000>>";
	mes "-LHZ−FR001型-";
	mes " ";
	mes "ただいま^FF0000故障中^000000です。";
	mes "氷の素材の投入はしないでください。";
	close;
}

//=====================================================================
// 丁半お爺さん
//=====================================================================
amatsu.gat,158,127,3	script	丁半お爺さん#summer	765,{
	mes "[寿司田部太郎]";
	mes "ほっほっほ。";
	mes "ワシの名は寿司田部太郎。";
	mes "人呼んで^0000FF『丁半の寿司おじじ』^000000じゃ！";
	mes "どうじゃ若人よ。";
	mes "ワシと丁半で一勝負せんかね？";
	next;
	switch(select("勝負する！","丁半？","嫌です")) {
	case 1:
		mes "[寿司田部太郎]";
		mes "ふふ、勝負するんじゃな？";
		mes "賭け金は^FF31FFお寿司^000000を使うのじゃ。";
		mes "お寿司を何個賭けるのじゃ？";
		mes "最大^0000FF10個^000000まで賭けられるぞい。";
		mes "もし止めるのなら、0を入れるのじゃ。";
		while(1) {
			next;
			input '@bet;
			if('@bet == 0) {
				mes "[寿司田部太郎]";
				mes "ふむ、やめるのじゃな。";
				mes "勝負は時の運じゃ。";
				mes "退く時は退くのも正しき選択じゃ。";
				mes "また来るのじゃぞ。";
				close;
			}
			if('@bet < 0 || '@bet > 10) {
				mes "[寿司田部太郎]";
				mes "最大は10個までと";
				mes "言ったじゃろう？";
				mes "次は間違えないでくれい。";
				continue;
			}
			mes "[寿司田部太郎]";
			mes "お寿司^0000FF" +'@bet+ "個^000000で";
			mes "間違いないのじゃな？";
			next;
			if(select("はい","いいえ")==2) {
				mes "[寿司田部太郎]";
				mes "次は間違えないでくれい。";
				continue;
			}
			if(countitem(551) < '@bet) {
				mes "[寿司田部太郎]";
				mes "お主……";
				mes "無いお寿司をどこから";
				mes "出すつもりじゃい？";
				continue;
			}
			if(checkweight(551,'@bet) == 0) {
				mes "[寿司田部太郎]";
				mes "ふむ、このまま仮にお主が";
				mes "勝っても、お寿司を全部";
				mes "引き取ることができんぞい。";
				mes "荷物を軽くしてからまた来なされ。";
				close;
			}
			delitem 551,'@bet;
			mes "[寿司田部太郎]";
			mes "よしよし……";
			mes "さて、では始めるとするかのう。";
			mes "負けても恨みっこ無しじゃぞ。";
			next;
			emotion 29;
			mes "^6331FF−2つのサイコロが−";
			mes "−壺ザルに入れられ−";
			mes "−カラカラと音を立て始めた−^000000";
			next;
			mes "[寿司田部太郎]";
			mes "ほれ、どっちじゃ!?";
			next;
			if(select("^3131FF丁^000000","^FF3131半^000000")==2)
				set '@odd,1;	//奇数選択時
			mes "[寿司田部太郎]";
			mes (('@odd)? "^FF3131半": "^3131FF丁")+ "^000000じゃな？";
			mes "よし、勝負っ!!";
			next;
			set '@dice1,rand(1,6);	//サイコロ1
			set '@dice2,rand(1,6);	//サイコロ2
			set '@result,('@dice1 + '@dice2) % 2;	//合計が奇数の時、1をセット
			if('list$ == "") {	// staticに保存しておく
				setarray 'list$[0][0],"ピンゾロ","イチニ","サンミチ","ヨイチ","グイチ","イチロク";
				setarray 'list$[1][0],"イチニ","ニゾロ","サニ","シニ","グニ","ニロク";
				setarray 'list$[2][0],"サンミチ","サニ","サンゾロ","シソウ","グサン","サブロク";
				setarray 'list$[3][0],"ヨイチ","シニ","シソウ","シゾロ","グシ","シロク";
				setarray 'list$[4][0],"グイチ","グニ","グサン","グシ","ゴゾロ","ゴロク";
				setarray 'list$[5][0],"イチロク","ニロク","サブロク","シロク","ゴロク","ロクゾロ";
			}
			set '@yaku$,'list$['@dice1-1]['@dice2-1];
			mes "<<^006300丁半勝負結果^000000>>";
			mes " ";
			mes (('@dice1 >= 4)? "■": "　 ")+ "　 " +(('@dice1 >= 2)? "■": "　 ")+ "　　" +(('@dice2 >= 4)? "■": "　 ")+ "　 " +(('@dice2 >= 2)? "■": "　 ")+ "";
			mes (('@dice1 == 6)? "■": "　 ")+(('@dice1 % 2)? (('@dice1 == 1)? "^FF3131■^000000" : "■"): "　 ")+(('@dice1 == 6)? "■": "　 ")+ "　　" +(('@dice2 == 6)? "■": "　 ")+(('@dice2 % 2)? (('@dice2 == 1)? "^FF3131■^000000" : "■"): "　 ")+(('@dice2 == 6)? "■": "　 ")+ "";
			mes (('@dice1 >= 2)? "■": "　 ")+ "　 " +(('@dice1 >= 4)? "■": "　 ")+ "　　" +(('@dice2 >= 2)? "■": "　 ")+ "　 " +(('@dice2 >= 4)? "■": "　 ")+ "";
			mes " ";
			mes '@yaku$+ "の" +(('@result)? "^FF3131半": "^3131FF丁")+ "^000000じゃ！";
			next;
			if('@odd == '@result) {
				emotion 23;
				emotion 2,"";
				getitem 551,'@bet * 2;
				mes "[寿司田部太郎]";
				mes "ぶふぉっ！";
				mes "ま、負けたぞい……っ!!";
				mes "ほれ……お寿司を持って";
				mes "行くが良い。";
				next;
				mes "[寿司田部太郎]";
				mes "次は負けんぞい！";
				mes "また来るのじゃ！";
				close;
			}
			emotion 2;
			emotion 23,"";
			mes "[寿司田部太郎]";
			mes "ふぉっふぉっふぉ！";
			mes "お主の負けじゃ。";
			mes "残念じゃったのう！";
			mes "また来るのじゃ！";
			close;
		}
	case 2:
		mes "[寿司田部太郎]";
		mes "そうじゃ。丁半じゃ。";
		mes "まあ本来のやり方とは違う物じゃが。";
		mes "まずお主には賭け金として";
		mes "^FF31FFお寿司^000000を払ってもらうぞい。";
		mes "その後、ワシがサイコロを2つ";
		mes "振るんじゃ。";
		next;
		mes "[寿司田部太郎]";
		mes "お主はそこで【丁】か【半】かを";
		mes "選ぶのじゃ。";
		mes "サイコロの出た目の合計が";
		mes "偶数なら【丁】、";
		mes "奇数なら【半】じゃ。";
		next;
		mes "[寿司田部太郎]";
		mes "お主が当たれば";
		mes "賭けた^FF31FFお寿司^000000は倍返しするぞい。";
		mes "簡単じゃろう？";
		mes "勝負したくなったら";
		mes "また来るのじゃぞ。";
		close;
	case 3:
		mes "[寿司田部太郎]";
		mes "なんじゃ、そうか。";
		mes "勝負したくなったら";
		mes "また来るのじゃぞ。";
		close;
	}
}

amatsu.gat,165,121,1	script	怪しい男	64,{
	mes "[怪しい男]";
	mes "フフ……";
	mes "お寿司……残り1個……";
	mes "この状況……明らかに不利っ……";
	mes "丁か、半か……";
	mes "そんな安い物を掴むんじゃない……";
	next;
	mes "[怪しい男]";
	mes "俺が掴むのは、命っ……";
	mes "この勝負っ……";
	mes "負けられないのだっ……";
	next;
	emotion 6,"丁半お爺さん#summer";
	mes "[寿司田部太郎]";
	mes "こら、そこの兄ちゃん！";
	mes "やるならこっちへ来んか！";
	mes "そんなところでブツブツ言われると";
	mes "気になって仕方ないぞい……";
	next;
	emotion 29;
	mes "[怪しい男]";
	mes "ククッ……";
	mes "あの親父……";
	mes "後悔することになるっ……";
	close;
}

//=====================================================================
// 鍵屋のお兄さん
//=====================================================================
amatsu.gat,213,254,5	script	鍵屋のお兄さん	763,{
	if((MaxWeight - Weight) < 2000) {
		mes "[鍵屋錠兵衛]";
		mes "……なんだなんだ？";
		mes "やたら大量のアイテムを持ってるな。";
		mes "重くてヒーヒー言ってるようじゃ";
		mes "俺の出し物じゃ遊べないぜ。";
		mes "所持アイテムを減らしてから";
		mes "来てくれよな。";
		close;
	}
	mes "[鍵屋錠兵衛]";
	mes "よう！";
	mes "俺の名は錠兵衛。";
	mes "鍵屋を営んでいる。";
	mes "今まで祭りなんかにゃ無縁かと";
	mes "思ってたんだが、";
	mes "ちっとおもしれえ事を思いついてな。";
	next;
	mes "[鍵屋錠兵衛]";
	mes "どうだい？";
	mes "俺の出し物に挑戦してみねえかい？";
	mes "何、簡単だ。";
	mes "俺が作った^0000FFからくり錠前^000000を";
	mes "いかに早く開けられるかってもんだ。";
	next;
	switch(select("からくり錠前？","挑戦する[^FF3100練習用^000000]","挑戦する[^6331FF競技用^000000]","嫌です")) {
	case 1:
		mes "[鍵屋錠兵衛]";
		mes "ああそうさ。";
		mes "俺はこいつを^0000FF数錠^000000と名づけた。";
		mes "こいつに仕掛けているからくりは";
		mes "数字を使ったものってわけよ。";
		next;
		mes "[鍵屋錠兵衛]";
		mes "数錠を開けようとすると、";
		mes "滅茶苦茶な数列が表示される。";
		mes "そいつを入力窓に";
		mes "そっくりそのまま書けば";
		mes "ひとつ解除だ。";
		next;
		mes "[鍵屋錠兵衛]";
		mes "それが何段階か出てくる。";
		mes "全て連続で解除に成功すれば";
		mes "数錠は開くってわけだ。";
		mes "鍵になる数字は見えてるわけだから";
		mes "簡単だろう？";
		next;
		mes "[鍵屋錠兵衛]";
		mes "まあ、途中で間違えたら";
		mes "そこで失敗だがね。";
		mes "ああそうそう、あとこれは";
		mes "制限時間が^0000FF90秒^000000だ。";
		mes "全部解除しても^0000FF90秒^000000を";
		mes "超えていたら開かねえ。";
		next;
		mes "[鍵屋錠兵衛]";
		mes "まあ、そんなところだ。";
		next;
		mes "[鍵屋錠兵衛]";
		mes "^FF3100練習用^000000の数錠と、";
		mes "^6331FF競技用^000000の数錠を用意してある。";
		mes "^FF3100練習用^000000は途中で間違えても";
		mes "大丈夫だぜ。";
		mes "ただ、開けても何もならん。";
		next;
		mes "[鍵屋錠兵衛]";
		mes "^6331FF競技用^000000は1回^0000FF500zeny^000000いただく。";
		mes "ちなみに^6331FF競技用^000000は失敗すると終了な上、";
		mes "痛い目を見るぜ。";
		mes "だが解除できれば";
		mes "粗品を贈らせてもらうぜ。";
		next;
		mes "[鍵屋錠兵衛]";
		mes "成績が良ければ粗品を2種類以上";
		mes "贈ることもあるからな。";
		mes "荷物に空きが無ければ";
		mes "渡せないから覚えておいてくれよ。";
		next;
		mes "[鍵屋錠兵衛]";
		mes "あと、^6331FF競技用^000000は入力を";
		mes "間違えた時は何で間違えたのか";
		mes "教えてやるが、あんまり大きい数を";
		mes "入力して間違えた時は";
		mes "俺が覚えきれないから適当な数を";
		mes "言うからあてにするなよ。";
		next;
		mes "[鍵屋錠兵衛]";
		mes "まあ暇ならやってみてくれよ。";
		close;
	case 2:
		mes "[鍵屋錠兵衛]";
		mes "んじゃ練習用の数錠を渡すぜ。";
		mes "準備が出来たら";
		mes "数錠解除開始ボタンを押しな。";
		next;
		menu "数錠解除開始",-;
		emotion 27;
		setarray '@name$,"子","牛","寅","卯","辰","巳","午","未","申","酉","戌","亥";	//鍵名セット
		set '@len,3;			//初期入力桁数
		set '@time,gettimetick(0);	//タイマーセット
		for(set '@c,0; '@c<12;) {
			mes "【^FF3100練習用^000000】<<数錠解除>>";
			switch('@c) {
			case 0: mes "始■^313131□□□^636363□□□^9C9C9C□□□^CECECE□□^000000終"; break;
			case 1: mes "始■■^313131□□^636363□□□^9C9C9C□□□^CECECE□□^000000終"; break;
			case 2: mes "始■■■^313131□^636363□□□^9C9C9C□□□^CECECE□□^000000終"; break;
			case 3: mes "始■■■■^636363□□□^9C9C9C□□□^CECECE□□^000000終"; break;
			case 4: mes "始■■■■■^636363□□^9C9C9C□□□^CECECE□□^000000終"; break;
			case 5: mes "始■■■■■■^636363□^9C9C9C□□□^CECECE□□^000000終"; break;
			case 6: mes "始■■■■■■■^9C9C9C□□□^CECECE□□^000000終"; break;
			case 7: mes "始■■■■■■■■^9C9C9C□□^CECECE□□^000000終"; break;
			case 8: mes "始■■■■■■■■■^9C9C9C□^CECECE□□^000000終"; break;
			case 9: mes "始■■■■■■■■■■^CECECE□□^000000終"; break;
			case 10: mes "始■■■■■■■■■■■^CECECE□^000000終"; break;
			case 11: mes "始■■■■■■■■■■■■終"; break;
			}
			mes " ";
			mes "[" +'@name$['@c]+ "之鍵]";
			mes " ";
			if('@flag == 0) {	//新規数列フラグ判定
				set '@wish,0;	//古い数列をリセット
				for(set '@i,0; '@i<'@len; set '@i,'@i+1) {
					set '@r,rand(1,9);	//数字格納
					set '@wish,'@wish*10;	//桁数繰上げ
					set '@wish,'@wish+'@r;	//要求数列を合体
				}
				set '@flag,1;	//失敗時、同じ数列表示フラグ
			}
			mes "^0000FF" +'@wish+ "^000000";
			next;
			input '@num;
			if('@num != '@wish) {	//失敗時
				emotion 25,"";
				mes "<<" +'@name$['@c]+ "之鍵解除^FF0000失敗^000000!!>>";
				next;
				continue;
			}
			emotion 24,"";
			if('@c % 2)
				set '@len,'@len+1;	//2回ごとに入力桁数+1
			set '@flag,0;			//新規数列フラグリセット
			set '@c,'@c+1;			//カウントアップ
		}
		set '@time,(gettimetick(0)-'@time)/1000;	//タイマー取得
		if('@time > 90) {
			emotion 9,"";
			mes "【^FF3100練習用^000000】<<数錠解除>>";
			mes "■■■■■■■■■■■■";
			mes "^9C9C9C子牛寅卯辰巳午未申酉戌亥^000000";
			mes "■■■■■■■■■■■■";
			mes " ";
			mes "　　^9C0031時　限　超　過^000000";
		}
		else {
			misceffect 253,"";
			mes "【^FF3100練習用^000000】<<数錠解除>>";
			mes "■■■■■■■■■■■■";
			mes "^0000FF子牛寅卯辰巳午未申酉戌亥^000000";
			mes "■■■■■■■■■■■■";
			mes " ";
			mes "　　^009C00解　除　成　功　!!^000000";
		}
		next;
		mes "【^FF3100練習用^000000】<<数錠解除>>";
		mes "<<解除時間>> ^CE00FF" +('@time / 60)+ "^000000分^CE00FF" +('@time % 60)+ "秒^000000";
		mes "参考データ / 約^FF3100" +((90 * 60) / '@time)+ "^000000wpm";
		next;
		if('@time > 90) {
			emotion 29;
			mes "[鍵屋錠兵衛]";
			mes "ああ、惜しかったな。";
			mes "全部鍵を解いたはいいが";
			mes "時間が間に合わなかったぜ。";
			mes "もっと素早く反応して入力する";
			mes "鍛錬をするんだな。";
			close;
		}
		emotion 21;
		mes "[鍵屋錠兵衛]";
		mes "ほう、やるじゃねえか。";
		mes "競技用も今みたいな感じだぜ。";
		mes "まあ失敗は許されないがな。";
		mes "感覚が掴めたんなら";
		mes "やってみてくれよ。";
		close;
	case 3:
		mes "[鍵屋錠兵衛]";
		mes "おっ、競技用に挑戦するんだな？";
		mes "競技用は1回^0000FF500zeny^000000だ。";
		mes "やるかい？";
		next;
		if(select("やる","やっぱりやめる")==2) {
			mes "[鍵屋錠兵衛]";
			mes "なんでえ。";
			mes "つまらねえ奴だぜ。";
			mes "まあ、練習用でもやってってくれや。";
			close;
		}
		if(Zeny < 500) {
			mes "[鍵屋錠兵衛]";
			mes "よしきた！";
			mes "……っておいおい、";
			mes "払えないんじゃねえか？";
			mes "練習用でもやってな。";
			close;
		}
		set Zeny,Zeny-500;
		mes "[鍵屋錠兵衛]";
		mes "よしきた！";
		mes "それじゃあ500zenyは頂くぜ。";
		next;
		mes "[鍵屋錠兵衛]";
		mes "んじゃ競技用の数錠を渡すぜ。";
		mes "準備が出来たら";
		mes "数錠解除開始ボタンを押しな。";
		next;
		menu "数錠解除開始",-;
		emotion 27;
		setarray '@name$,"子","牛","寅","卯","辰","巳","午","未","申","酉","戌","亥","青龍","白虎","朱雀","玄武","黄龍";	//鍵名セット
		set '@len,6;			//初期入力桁数
		set '@time,gettimetick(0);	//タイマーセット
		for(set '@c,0; '@c<17;) {
			mes "【^6331FF競技用^000000】<<数錠解除>>";
			switch('@c) {
			case 0: mes "始■^313131□□□^636363□□□^9C9C9C□□□^CECECE□□^000000終"; break;
			case 1: mes "始■■^313131□□^636363□□□^9C9C9C□□□^CECECE□□^000000終"; break;
			case 2: mes "始■■■^313131□^636363□□□^9C9C9C□□□^CECECE□□^000000終"; break;
			case 3: mes "始■■■■^636363□□□^9C9C9C□□□^CECECE□□^000000終"; break;
			case 4: mes "始■■■■■^636363□□^9C9C9C□□□^CECECE□□^000000終"; break;
			case 5: mes "始■■■■■■^636363□^9C9C9C□□□^CECECE□□^000000終"; break;
			case 6: mes "始■■■■■■■^9C9C9C□□□^CECECE□□^000000終"; break;
			case 7: mes "始■■■■■■■■^9C9C9C□□^CECECE□□^000000終"; break;
			case 8: mes "始■■■■■■■■■^9C9C9C□^CECECE□□^313131終^000000"; break;
			case 9: mes "始■■■■■■■■■■^CECECE□□^636363終^000000"; break;
			case 10: mes "始■■■■■■■■■■■^CECECE□^9C9C9C終^000000"; break;
			case 11: mes "始■■■■■■■■■■■■^CECECE終^000000"; break;
			case 12: mes "始■■■■■■■■■■■■^3131FF■^CECECE□□□終^000000"; break;
			case 13: mes "始■■■■■■■■■■■■^3131FF■^633163■^CECECE□□終^000000"; break;
			case 14: mes "始■■■■■■■■■■■■^3131FF■^633163■^FF3100■^CECECE□終^000000"; break;
			case 15: mes "始■■■■■■■■■■■■^3131FF■^633163■^FF3100■^006300■^CECECE終^000000"; break;
			case 16: mes "^FFAD00■■■■■■■■■■■■■■■■■■^000000"; break;
			}
			if('@c < 12)	//行数調整
				mes " ";
			mes "[" +'@name$['@c]+ "之鍵]";
			mes " ";
			set '@wish,0;	//古い数列をリセット
			for(set '@i,0; '@i<'@len; set '@i,'@i+1) {
				set '@r,rand(1,9);	//数字格納
				set '@wish,'@wish*10;	//桁数繰上げ
				set '@wish,'@wish+'@r;	//要求数列を合体
			}
			mes (('@c < 12)? "^0000FF": (('@c == 12)? "^3131FF": (('@c == 13)? "^633163": (('@c == 14)? "^FF3100": (('@c == 15)? "^006300": "^FFAD00")))))+'@wish+ "^000000";
			next;
			input '@num;
			if('@num != '@wish) {	//失敗時
				emotion 25,"";
				if('@c < 12) {
					percentheal -50,0;
					sc_start3 SC_CURSE,0,0,0,0,10000,10;
				}
				else {
					percentheal -90,0;
					sc_start3 SC_CONFUSION,0,0,0,0,10000,10;
				}
				mes "<<" +'@name$['@c]+ "之鍵解除^FF0000失敗^000000!!>>";
				mes "[正]";
				mes "^0000FF" +'@wish+ "^000000";
				mes "[誤]";
				mes "^FF0000" +'@num+ "^000000";
				next;
				if('@c < 8) {
					mes "[鍵屋錠兵衛]";
					mes "はやっ！";
					mes "もう失敗したのか!!";
					mes "落ち着いてやってみろよ、な。";
					mes "またの挑戦待ってるぜ！";
					close;
				}
				if('@c < 12) {
					mes "[鍵屋錠兵衛]";
					mes "あーあ！";
					mes "失敗か!!";
					mes "いい線いってたと思うんだがな。";
					mes "またの挑戦待ってるぜ！";
					close;
				}
				if('@c < 16) {
					mes "[鍵屋錠兵衛]";
					mes "あー、すまんすまん！";
					mes "競技用は練習用よりも";
					mes "からくりが多いんだよ！";
					mes "言うの忘れてたぜ！";
					mes "ま、失敗は失敗だわな。";
					mes "また挑戦してくれよ！";
					close;
				}
				mes "[鍵屋錠兵衛]";
				mes "あー!!　勿体ねえ!!";
				mes "最後の最後で失敗か！";
				mes "惜しかったぜ！";
				mes "また挑戦してくれよ！";
				close;
			}
			emotion 24,"";
			set '@flag,0;			//新規数列フラグリセット
			set '@c,'@c+1;			//カウントアップ
			if(('@c % 4) == 0)
				set '@len,'@len+1;	//4回ごとに入力桁数+1
		}
		set '@time,(gettimetick(0)-'@time)/1000;	//タイマー取得
		if('@time > 90) {
			emotion 9,"";
			mes "【^6331FF競技用^000000】<<数錠解除>>";
			mes "■■■■■■■■■■■■";
			mes "^9C9C9C子牛寅卯辰巳午未申酉戌亥^000000";
			mes "　^9C9C9C青龍白虎朱雀玄武黄龍^000000";
			mes "■■■■■■■■■■■■";
			mes " ";
			mes "　　^9C0031時　限　超　過^000000";
		}
		else {
			misceffect 253,"";
			percentheal 100,0;
			if('@time > 90)
				getitem 519,1;
			else
				getitem 551,10;
			if('@time <= 40)
				getitem 549,1;
			else if('@time <= 55)
				getitem 553,1;
			else if('@time <= 70)
				getitem 550,3;
			mes "【^6331FF競技用^000000】<<数錠解除>>";
			mes "■■■■■■■■■■■■";
			mes "^0000FF子牛寅卯辰巳午未申酉戌亥^000000";
			mes "　^3131FF青龍^633163白虎^FF3100朱雀^006300玄武^FFAD00黄龍^000000";
			mes "■■■■■■■■■■■■";
			mes " ";
			mes "　　^009C00解　除　成　功　!!^000000";
		}
		next;
		mes "【^6331FF競技用^000000】<<数錠解除>>";
		mes "<<解除時間>> ^CE00FF" +('@time / 60)+ "^000000分^CE00FF" +('@time % 60)+ "秒^000000";
		mes "参考データ / 約^6331FF" +((180 * 60) / '@time)+ "^000000wpm";
		next;
		if('@time > 90) {
			emotion 29;
			mes "[鍵屋錠兵衛]";
			mes "ああ、惜しかったな。";
			mes "全部鍵を解いたはいいが";
			mes "時間が間に合わなかったぜ。";
			next;
			mes "[鍵屋錠兵衛]";
			mes "まあ、これでもやるよ。";
			mes "栄養補給したら";
			mes "もっと素早く反応して入力する";
			mes "鍛錬をするんだな！";
			mes "また挑戦しに来いよ！";
			close;
		}
		if('@time > 70) {
			emotion 21;
			mes "[鍵屋錠兵衛]";
			mes "ほう、やるじゃねえか！";
			mes "見事俺の数錠を解けたじゃねえか！";
			mes "こいつを受け取ってくれ！";
			next;
			mes "[鍵屋錠兵衛]";
			mes "また挑戦しに来いよ！";
			mes "今度はもっと早く解いてみるのも";
			mes "いいかもな。";
			close;
		}
		emotion 21;
		mes "[鍵屋錠兵衛]";
		mes "ほう、やるじゃねえか！";
		mes "かなりの速度だぜ！";
		mes "俺の数錠をここまで早く解くとは……";
		mes "鍵屋の魂、揺さぶられたぜ！";
		mes "こいつを受け取ってくれ！";
		next;
		mes "[鍵屋錠兵衛]";
		mes "また挑戦しに来いよ！";
		close;
	case 4:
		mes "[鍵屋錠兵衛]";
		mes "なんでえ。";
		mes "つまらねえ奴だぜ。";
		close;
	}
}

//=====================================================================
// クジ引きお姉さん
//=====================================================================
amatsu.gat,208,134,3	script	クジ引きお姉さん	758,{
	if((MaxWeight - Weight) < 2000) {
		mes "[美香]";
		mes "あら？";
		mes "そんなにたくさんアイテムを持って";
		mes "どうしたんですか？";
		mes "危ないですから、";
		mes "所持アイテムを減らしてから";
		mes "また来てくださいね。";
		close;
	}
	mes "[美香]";
	mes "ようこそアマツへ！";
	mes "私はアマツ観光協会の美香といいます。";
	mes "アマツ納涼泉水祭の開催を記念して、";
	mes "クジ引きを提供していま〜す！";
	next;
	mes "[美香]";
	mes "クジは1回^0000FF500^000000zenyです！";
	mes "クジには^9C00FF1〜25^000000までの数字が";
	mes "並んでいます。";
	mes "当たりの数字はひとつだけ！";
	mes "当たりだと思う数字を";
	mes "入力してくださいね！";
	next;
	mes "[美香]";
	mes "当たれば新鮮な景品をプレゼント♪";
	mes "はずれてしまった場合は、";
	mes "^FF3163隣接した2〜4つの数字^000000の中に";
	mes "当たりがあれば『^FF3163惜しい^000000』と";
	mes "お知らせします！";
	mes "隣接には斜めは含みませんよ〜。";
	next;
	mes "[美香]";
	mes "チャンスは4回まで！";
	mes "早く当てるほど賞品が良くなります。";
	mes "クジを引いてみますか？";
	next;
	switch(select("クジを引く","詳しく教えて","やらない")) {
	case 1:
		if(Zeny < 500) {
			emotion 16;
			mes "[美香]";
			mes "あらら、お金が足りないです！";
			mes "また来てくださいね！";
			close;
		}
		set Zeny,Zeny-500;
		mes "[美香]";
		mes "わかりました〜！";
		mes "それでは^0000FF500^000000zeny頂戴いたしますね。";
		mes "クジをお渡ししますので、";
		mes "当たりだと思う番号を";
		mes "入力してくださいね！";
		mes "頑張ってください!!";
		set '@hit,rand(1,25);	//当たり番号セット
		for(set '@c,0; '@c<4;) {
			next;
			mes "【アマツ観光協会】　^9C00FF−弐拾伍籤札−^000000";
			mes "^CE0063　01　02　03　04　05";
			mes "　06　07　08　09　10";
			mes "　11　12　13　14　15";
			mes "　16　17　18　19　20";
			mes "　21　22　23　24　25^000000";
			mes "−当たり番号を予想して入力−";
			next;
			input '@num;
			if('@num < 1 || '@num > 25) {
				mes "[美香]";
				mes "もー！";
				mes "そんな番号無いですよ！";
				mes "やり直しですっ！";
				continue;
			}
			if('@num == '@hit) {	//当たり
				misceffect 253,"";
				emotion 2;
				mes "[美香]";
				mes "^0000FF当たり^000000です！";
				mes "おめでとうございます〜!!";
				next;
				mes "[美香]";
				switch('@c) {
				case 0:
					switch(rand(4)) {
					case 0:	getitem 514,6; break;	//ブドウ6個
					case 1:	getitem 568,6; break;	//レモン6個
					case 2:	getitem 582,6; break;	//オレンジ6個
					case 3: getitem 620,1; break;	//オレンジジュース1個
					}
					mes "一発成功すごいです!!";
					mes "^9C009C松^000000クラス賞品の中からこちらを";
					break;
				case 1:
					switch(rand(3)) {
					case 0:	getitem 514,3; break;	//ブドウ3個
					case 1:	getitem 568,3; break;	//レモン3個
					case 2:	getitem 582,3; break;	//オレンジ3個
					}
					mes "2回目で成功でしたので";
					mes "^006300竹^000000クラス賞品の中からこちらを";
					break;
				case 2:
				case 3:
					switch(rand(3)) {
					case 0:	getitem 514,1; break;	//ブドウ1個
					case 1:	getitem 568,1; break;	//レモン1個
					case 2:	getitem 582,1; break;	//オレンジ1個
					}
					mes "3回目以降で成功でしたので";
					mes "^CE0063梅^000000クラス賞品の中からこちらを";
					break;
				}
				mes "差し上げます！";
				mes "また遊んでくださいね♪";
				close;
			}
			if((('@num % 5) != 1 && ('@num - 1) == '@hit) || (('@num % 5) != 0 && ('@num + 1) == '@hit) || ('@num - 5) == '@hit || ('@num + 5) == '@hit) {	//惜しい
				emotion 33;
				mes "[美香]";
				mes "あっ、^FF3163惜しい^000000です！";
				mes "隣接したどこかに当たりが";
				mes "ありますよ〜！";
				set '@c,'@c+1;
				continue;
			}
			emotion 16;
			mes "[美香]";
			mes "うーん残念！";
			mes "かすってもいないです！";
			mes "頑張って下さい!!";
			set '@c,'@c+1;
			continue;
		}
		next;
		getitem 551,1;
		mes "[美香]";
		mes "……って、あー、^FF0000残念^000000！";
		mes "4回以内で当たらなかったですね……";
		mes "当たりは^9C00FF" +'@hit+ "^000000でした〜……";
		mes "残念賞進呈です〜。";
		mes "また遊んでくださいね！";
		close;
	case 2:
		mes "[美香]";
		mes "わかりました！";
		mes "ではご説明しますね♪";
		next;
		mes "　01　02　03　04　05";
		mes "　06　07　08　09　10";
		mes "　11　12　13　14　15";
		mes "　16　17　18　19　20";
		mes "　21　22　23　24　25";
		mes "^630000クジの札はこんな感じで";
		mes "5×5マスに数字が並んでます。^000000";
		next;
		mes "　01　02　03　04　05";
		mes "　06　07　08　09　10";
		mes "　11　^0000FF12^000000　13　14　15";
		mes "　16　17　18　19　20";
		mes "　21　22　23　24　25";
		mes "^630000仮に当たりが^0000FF12^630000だとします。";
		mes "そして貴方様が19を入力すると……^000000";
		next;
		mes "　01　02　03　04　05";
		mes "　06　07　08　09　10";
		mes "　11　^0000FF12^000000　13　^FF000014^000000　15";
		mes "　16　17　^FF000018　19　20^000000";
		mes "　21　22　23　^FF000024^000000　25";
		mes "^FF000019^630000の隣接数に^0000FF12^630000は無いので";
		mes "『^FF0000かすってもいない^630000』とお伝えします。^000000";
		next;
		mes "　01　02　03　04　05";
		mes "　06　07　08　09　10";
		mes "　11　^0000FF12^000000　13　^FF000014^000000　15";
		mes "　16　17　^FF000018　19　20^000000";
		mes "　21　22　23　^FF000024^000000　25";
		mes "^630000この5つの数はハズレ確定です。";
		mes "次に^00630013^630000を入力すると……^000000";
		next;
		mes "　01　02　03　04　05";
		mes "　06　07　08　09　10";
		mes "　11　^0000FF12^000000　^00630013^630000　^FF000014^000000　15";
		mes "　16　17　^FF000018　19　20^000000";
		mes "　21　22　23　^FF000024^000000　25";
		mes "^630000隣接した数字に当たりがありますね！";
		mes "『^006300惜しい^630000』とお伝えします。^000000";
		next;
		mes "　01　02　03　04　05";
		mes "　06　07　08　09　10";
		mes "　11　^0000FF12^000000　^00630013^630000　^FF000014^000000　15";
		mes "　16　17　^FF000018　19　20^000000";
		mes "　21　22　23　^FF000024^000000　25";
		mes "^63000014と18は既にハズレ確定ですから、";
		mes "当たりは^00630008^630000か^0000FF12^630000の二択になります。^000000";
		next;
		mes "[美香]";
		mes "運良くこのパターンになれば、";
		mes "4回目までに当たりますね♪";
		mes "まあ、そううまくはいきません";
		mes "けれど……";
		next;
		mes "[美香]";
		mes "ちなみに、本番は今の説明のように";
		mes "わかりやすく^FF0000色はつきません^000000！";
		mes "あと、01〜09は入力するときに";
		mes "0を省いても大丈夫です！";
		mes "ちゃんと覚えててくださいね。";
		mes "詳しい説明は以上ですっ♪";
		close;
	case 3:
		mes "[美香]";
		mes "そうですか……";
		mes "残念です。";
		mes "遊びたくなったら";
		mes "いつでもいらしてくださいね。";
		close;
	}
}

//=====================================================================
// 札博打お兄さん
//=====================================================================
amatsu.gat,187,131,5	script	札博打お兄さん	762,{
	mes "[九札銀次郎]";
	mes "いよう！";
	mes "おいらの名前は九札銀次郎。";
	mes "いやぁ、この祭の雰囲気ってえのは";
	mes "実にいいねえ！";
	mes "どうだい、ここらで一丁";
	mes "おいらと賭けでもしないかい？";
	next;
	switch(select("する！","どんな賭けです？","遠慮します")) {
	case 1:
		mes "[九札銀次郎]";
		mes "よっしゃ！";
		mes "賭け金は^0000FFお寿司^000000を使うぜ。";
		mes "お寿司を何個賭けるかね？";
		mes "最大^0000FF10人前^000000まで賭けられるぜ。";
		mes "もし止めるんだったら、0を入れな。";
		while(1) {
			next;
			input '@bet;
			if('@bet == 0) {
				mes "[九札銀次郎]";
				mes "ふむ、やめるのかい。";
				mes "まあ、勝負は時の運だしな、";
				mes "退く時は退くのが正解だぜ。";
				mes "また来なよ！";
				close;
			}
			if('@bet < 0 || '@bet > 10) {
				mes "[九札銀次郎]";
				mes "最大は10個までと";
				mes "言ったろう？";
				mes "次は間違えないでくれよ！";
				continue;
			}
			mes "[九札銀次郎]";
			mes "お寿司^0000FF" +'@bet+ "個^000000で";
			mes "間違いないな？";
			next;
			if(select("はい","いいえ")==2) {
				mes "[九札銀次郎]";
				mes "今度は間違えるなよ？";
				continue;
			}
			if(countitem(551) < '@bet) {
				mes "[九札銀次郎]";
				mes "おいおい、お寿司が足りないぜ？";
				mes "冗談はさておき、何個";
				mes "賭けるんだい？";
				continue;
			}
			if(checkweight(551,'@bet) == 0) {
				mes "[九札銀次郎]";
				mes "うーむ、このまま仮にお前さんが";
				mes "勝っても、お寿司を全部";
				mes "引き取ることができないぜ。";
				mes "荷物を軽くしてからまた来いよ。";
				close;
			}
			delitem 551,'@bet;
			break;
		}
		mes "[九札銀次郎]";
		mes "よっしゃ！";
		mes "さーて、んじゃあ始めるぜ。";
		mes "負けても恨みっこ無しだからな。";
		next;
		mes "[九札銀次郎]";
		mes "まずは、限界数を決めるぜ。";
		mes "限界数は15から30までの数の";
		mes "どれかだ。";
		mes "あらよっと！";
		next;
		set '@limit,rand(15,30);	//限界数セット
		mes "[限界数]";
		mes "-^FF0000" +'@limit+ "^000000-";
		next;
		mes "[九札銀次郎]";
		mes "限界数は" +'@limit+ "だな。";
		mes "あとはこの数に近づけるよう";
		mes "裏のお稲荷さんに祈るだけだぜ。";
		mes "んじゃまあ、最初の1枚を";
		mes "軽く引いてみようかね。";
		for(set '@c,1; '@c<=5; set '@c,'@c+1) {
			next;
			if('@estop == 0) {	//敵はストップしているか
				set '@enemy['@c],rand(1,9);
				mes "[九札銀次郎]";
				mes "-^6300FF" +'@c+ "枚目 / " +'@enemy['@c]+ "^000000-";
				next;
				mes "[九札銀次郎]";
				if('@c == 1) {
					mes "おいらの1枚目は^6300FF" +'@enemy['@c]+ "^000000だ。";
					mes "まあ、まだどうなるか";
					mes "分かるようなもんじゃないな。";
					mes "さて今度はお前さんの番だぜ。";
				}
				else
					mes "おいらの" +'@c+ "枚目は^6300FF" +'@enemy['@c]+ "^000000か。";
				set '@enemy[0],'@enemy[0] + '@enemy['@c];	//敵数合計
				next;
			}
			if('@mstop == 0) {	//自分はストップしているか
				menu "札を引く",-;
				set '@mine['@c],rand(1,9);
				mes "[" +strcharinfo(0)+ "]";
				mes "-^009C00" +'@c+ "枚目 / " +'@mine['@c]+ "^000000-";
				next;
				mes "[九札銀次郎]";
				mes "お前さんの" +'@c+ "枚目は^009C00" +'@mine['@c]+ "^000000だ" +((rand(2))? "な": "")+"。";
				set '@mine[0],'@mine[0] + '@mine['@c];		//自分数合計
				next;
			}
			mes "<<勝負状況>> −札" +(('@c == 1)? "一": (('@c == 2)? "二": (('@c == 3)? "三": (('@c == 4)? "四": "五"))))+ "枚−";
			mes "限界数 / ^FF0000" +'@limit+ "^000000";
			mes "[九札銀次郎]";
			mes "^6300FF" +'@enemy[1]+ "^000000" +(('@enemy[2])? " + ^6300FF" +'@enemy[2]+ "^000000": "")+(('@enemy[3])? " + ^6300FF" +'@enemy[3]+ "^000000": "")+(('@enemy[4])? " + ^6300FF" +'@enemy[4]+ "^000000": "")+(('@enemy[5])? " + ^6300FF" +'@enemy[5]+ "^000000": "")+(('@enemy[2])? "＝^FF0000" +'@enemy[0]+ "^000000": "")+"";
			mes "[" +strcharinfo(0)+ "]";
			mes "^009C00" +'@mine[1]+ "^000000" +(('@mine[2])? " + ^009C00" +'@mine[2]+ "^000000": "")+(('@mine[3])? " + ^009C00" +'@mine[3]+ "^000000": "")+(('@mine[4])? " + ^009C00" +'@mine[4]+ "^000000": "")+(('@mine[5])? " + ^009C00" +'@mine[5]+ "^000000": "")+(('@mine[2])? "＝^FF0000" +'@mine[0]+ "^000000": "")+"";
			next;
			if((('@enemy[0] == '@mine[0]) && '@c == 5) || (('@enemy[0] == '@mine[0]) && '@enemy[0] == '@limit)) {	//ワカレ（同点）
				emotion 7;
				getitem 551,'@bet;
				mes "[九札銀次郎]";
				mes "ありゃりゃっ!!";
				mes "ここまで来てみりゃワカレかい！";
				mes "なんだ勿体ねえなあ……！";
				mes "賭け金は返すぜ。";
				mes "また来いよ！";
				close;
			}
			if('@enemy[0] == '@limit) {	//敵ピタリ
				emotion 22;
				mes "[九札銀次郎]";
				mes "うほっ!!";
				mes "おいらが限界数ピタリだぜ！";
				mes "悪いなあ、お前さんの負けだ！";
				mes "また来いよ！";
				close;
			}
			if('@mine[0] == '@limit) {	//自分ピタリ
				emotion 23;
				getitem 551,'@bet*2;
				mes "[九札銀次郎]";
				mes "んがぁ〜!?";
				mes "お前さん、限界数ピタリかよ！";
				mes "くそっ……";
				mes "お前さんの勝ちだ……持っていきな。";
				mes "だが次はねえからな！";
				close;
			}
			if(('@enemy[0] > '@limit) && ('@mine[0] > '@limit)) {	//両方ドボン
				emotion 23;
				getitem 551,'@bet;
				mes "[九札銀次郎]";
				mes "んんんん〜!?";
				mes "お互いに限界数を越えちまった!!";
				mes "どっちも^FF0000ドボン^000000だ！";
				mes "ちっ……ワカレか。";
				mes "賭け金は返すぜ。";
				mes "また来いよ！";
				close;
			}
			if('@enemy[0] > '@limit) {	//敵ドボン
				emotion 23;
				getitem 551,'@bet*2;
				mes "[九札銀次郎]";
				mes "んがぁ〜!?";
				mes "おいらが限界数を越えちまった!?";
				mes "^FF0000ドボン^000000だ、くそっ！";
				mes "お前さんの勝ちだ……持っていきな。";
				mes "今度は負けねえぞ！";
				close;
			}
			if('@mine[0] > '@limit) {	//自分ドボン
				emotion 29;
				mes "[九札銀次郎]";
				mes "ひっひっひ!!";
				mes "お前さん、限界数を越えちまったな！";
				mes "^FF0000ドボン^000000だ！";
				mes "お前さんの負けだ。";
				mes "残念だったな、また来いよ！";
				close;
			}
			if('@c > 1 && '@mine[0] > '@enemy[0] && '@mstop == 0) {	//自分 > 敵の場合、引くか止めるか選択
				mes "[九札銀次郎]";
				mes "んぬぅ……";
				mes (('@c == 4)? "まずい": "微妙な")+ "流れになったな……";
				mes "お前さん、どうするね？";
				mes "お前さんが勝っているから";
				mes "ここで止めることができるぜ。";
				next;
				mes "[九札銀次郎]";
				mes "お前さんが止めた場合、";
				if('@c == 4) {	//次が最後の札の場合
					mes "おいらだけが最後の札を引く。";
					mes "お前さんの数を越せばおいらの勝ち。";
					mes "お前さんの数と同じだったら引き分け。";
					mes "ドボンしたらおいらの負けだ。";
				}
				else {
					mes "おいらだけが次以降札を引く。";
					mes "おいらが5枚目を引くまでに";
					mes "お前さんの数を越せば";
					mes "おいらの勝ち。";
					mes "もちろんドボンしたらおいらの負けだ。";
				}
				next;
				mes "[九札銀次郎]";
				mes "ここで止めておくかい？";
				mes "それとも続行するかい？";
				next;
				if(select("止める","続行する")==2) {
					mes "[九札銀次郎]";
					mes "よし、続行だな？";
					mes ('@c == 4)? "次で全てが決まるわけだ。": "まだまだ勝負はこれからだぜ。";
					mes "んじゃ、おいらから引くぜ！";
					continue;
				}
				mes "[九札銀次郎]";
				mes "止めるんだな。";
				mes "冒険はしないわけだ。";
				mes "そんなんじゃ、勝負には勝てねえぜ！";
				mes "んじゃ、おいらが引くぜ。";
				set '@mstop,1;	//次以降自分ストップ
				continue;
			}
			if(('@limit - '@enemy[0]) < 5 && '@estop == 0) {	//敵が限界数の4以内だったらストップ
				mes "[九札銀次郎]";
				mes "ふーん……";
				mes "こういう流れになったか……";
				mes "よし、おいらはここで止めるぜ。";
				mes "踏みとどまるのも勇気、ってね。";
				next;
				mes "[九札銀次郎]";
				mes "お前さんが" +(('@c == 4)? "最後の1枚": ('@c + 1)+"枚目以降")+ "を引いて";
				mes "おいらの数を越えればお前さんの勝ち。";
				if('@c == 4)
					mes "おいらと同じ数なら引き分け。";
				mes "限界数を越えたらお前さんの負けだ。";
				mes "さ、腹ぁくくって引きな！";
				set '@estop,1;	//次以降敵ストップ
				continue;
			}
			if('@mstop) {	//自分ストップ時
				if('@enemy[0] > '@mine[0]) {	//負け
					emotion 29;
					mes "[九札銀次郎]";
					mes "いよっしゃぁ!!";
					mes "おいらの勝ちだぜ！";
					mes (('@c == 3)? "いやあ残念だったなぁ、お前さん！": (('@c == 4)? "結構危なかったな……": "ギリギリだったぜ……"));
					mes "また来いよ、な！";
					close;
				}
				mes "[九札銀次郎]";
				if('@c == 3)
					mes "ちぃ……雲行きが怪しいな。";
				else {
					mes "くそっ！";
					mes "もう";
				}
				mes "次の札を引くぜ！";
				continue;
			}
			if('@estop) {	//敵ストップ時
				if('@mine[0] > '@enemy[0]) {	//勝ち
					emotion 23;
					getitem 551,'@bet*2;
					mes "[九札銀次郎]";
					mes "ぬあぁあぁぁ!!";
					mes "や、やられたぜ……";
					mes ('@c == 3)? "退くべきじゃなかったか……！": "まさかここで引っ繰り返されるとは！";
					mes "お前さんの勝ちだ……持っていきな。";
					mes "だが次はねえぞ！";
					close;
				}
				if('@c == 3) {
					mes "[九札銀次郎]";
					mes "ふむ……まだ終わらんぜ。";
					mes "さぁ、札に手を掛けな！";
				}
				else {
					mes "[九札銀次郎]";
					mes "これはこれで運がいいな。";
					mes "さぁ、次が大一番だぜ？";
					mes "泣いても笑っても";
					mes "これで仕舞いだ！";
					mes "引きな！";
				}
				continue;
			}
			switch('@c) {
			case 1:
				mes "[九札銀次郎]";
				mes "さぁて、ここからが本番だぜ！";
				mes "お互い2枚目を引くんだ。";
				mes "札を引く手に魂籠めな！";
				mes "いくぜ！";
				break;
			default:
				mes "[九札銀次郎]";
				mes ((rand(2))? "ふむ": "よし")+ "……";
				mes "まだまだ勝負はこれからだぜ。";
				mes "んじゃ、おいらから引くぜ。";
				break;
			case 4:
				mes "[九札銀次郎]";
				mes "よし……";
				mes "ついにここまで来たか。";
				mes "次で全てが決まるわけだ。";
				mes "んじゃ、おいらから引くぜ！";
				break;
			}
			continue;
		}
		//5枚全て引き終わった結果
		if('@enemy[0] > '@mine[0]) {	//負け
			emotion 29;
			mes "[九札銀次郎]";
			if('@estop) {		//敵ストップして最後まで越えられなかった時
				mes "残念だったな！";
				mes "最後の5枚目でもお前さんは";
				mes "おいらを越えられなかった！";
				mes "お前さんの負けだ。";
				mes "惜しかったな、また来いよ！";
			}
			else {
				mes "いよっしゃぁ!!";
				mes "おいらの勝ちだぜ！";
				mes "いやあ残念だったなぁ、お前さん！";
				mes "また来いよ、な！";
			}
			close;
		}
		if('@mine[0] > '@enemy[0]) {	//勝ち
			emotion 23;
			getitem 551,'@bet*2;
			mes "[九札銀次郎]";
			mes "ぬあぁあぁぁ!!";
			mes "や、やられたぜ……";
			mes "ここまで引っ張って負けるとは！";
			mes "お前さんの勝ちだ……持っていきな。";
			mes "だが次はねえぞ！";
			close;
		}
	case 2:
		mes "[九札銀次郎]";
		mes "よし、説明してやろう。";
		mes "こいつは1から9までの";
		mes "9種類の札をお互いに引き合い、";
		mes "合計数が限界数に近いほうが";
		mes "勝ちになる博打だ。";
		next;
		mes "[九札銀次郎]";
		mes "限界数ってのは目標数みたいなもんだ。";
		mes "15から30の中から決まる。";
		mes "そして、おいら、お前さんの順に";
		mes "札を引いていくんだ。";
		next;
		mes "[九札銀次郎]";
		mes "札の数字の合計が限界数を";
		mes "超えたら負けだ。";
		mes "ピタリなら勝ち。";
		mes "ちなみに札を引ける回数は";
		mes "5回までだ。";
		next;
		mes "[九札銀次郎]";
		mes "札をお互い引いて、";
		mes "お互いの合計数が出た時点で、";
		mes "大きい数字の方は";
		mes "そこで止めるか、続行するかを";
		mes "選択することができる。";
		mes "例えばこんな状況だとしようか。";
		next;
		mes "<<勝負状況>> −札三枚−";
		mes "限界数 / ^FF000020^000000";
		mes "[九札銀次郎]";
		mes "^6300FF5^000000 + ^6300FF9^000000 + ^6300FF1^000000＝^FF000015^000000";
		mes "[" +strcharinfo(0)+ "]";
		mes "^009C007^000000 + ^009C003^000000 + ^009C006^000000＝^FF000016^000000";
		next;
		mes "[九札銀次郎]";
		mes "今の場合、お前さんが勝っているから";
		mes "お前さんは4枚目以降を引かないか、";
		mes "続行するかの選択ができる。";
		mes "もうじき限界数を越えそうな時";
		mes "安全だな。";
		next;
		mes "[九札銀次郎]";
		mes "だが止めた場合、おいらが4枚目以降で";
		mes "お前さんを越えれば";
		mes "即座においらの勝ちになる。";
		mes "おいらが限界数を越えたら";
		mes "おいらの負けだがね。";
		next;
		mes "[九札銀次郎]";
		mes "止めない限り、必ず札は";
		mes "引かなきゃならない。";
		mes "見極めが大事だぜ。";
		mes "ちなみにお互い限界数を越えたり";
		mes "5枚目でお互い同じ合計数だったり";
		mes "したら引き分けだ。";
		next;
		mes "[九札銀次郎]";
		mes "お前さんが勝ったら";
		mes "賭けたお寿司を倍返しするぜ。";
		mes "まああとは実際にやってみるのが";
		mes "早いと思うぜ。";
		close;
	case 3:
		mes "[九札銀次郎]";
		mes "そうかい……";
		mes "遊びたくなったらまた来な！";
		mes "この祭りの熱気が続く限り";
		mes "いつでも待ってるぜ！";
		close;
	}
}

//=====================================================================
// 牌籠お姉さん
//=====================================================================
amatsu.gat,260,246,4	script	牌籠お姉さん	757,{
	emotion 14;
	mes "[紘子]";
	mes "やっほー！";
	mes "お祭り楽しんでる？";
	mes "やっぱりお祭りって楽しいよね〜！";
	mes "ねぇねぇ、あたしと遊んでいかない？";
	next;
	mes "[紘子]";
	mes "あたしの出し物は";
	mes "『牌籠』だよ！";
	mes "まずあたしが基準になる数を出して、";
	mes "次に出す数がその数よりも";
	mes "高いか低いか賭けるの！";
	next;
	mes "[紘子]";
	mes "賭けには^0000FFお寿司^000000を^FF000010個^000000使うよ！";
	mes "どうする？";
	mes "やってみる？";
	mes "ちなみに失敗するまで続けるからね。";
	mes "何倍返しになるかは成功回数次第！";
	next;
	switch(select("やってみる","やってみない","詳しく！")) {
	case 1:
		if(countitem(551) < 10) {
			mes "[紘子]";
			mes "ちょっとお……";
			mes "お寿司10個持ってないじゃない！";
			close;
		}
		if((MaxWeight - Weight) < 3500) {
			mes "[紘子]";
			mes "あっ……";
			mes "もしキミが勝っても、お寿司を全部";
			mes "渡すことが出来ないよ！";
			mes "荷物を軽くしてからまた来てね！";
			close;
		}
		delitem 551,10;
		mes "[紘子]";
		mes "オッケー！";
		mes "じゃあ始めようか！";
		mes "最初の数を決めるから、";
		mes "次に出る数が高いか低いか当ててね！";
		mes "ちなみに出る数字は1から100まで！";
		mes "同じ数字が出たらキミの負けだよ！";
		while(1) {
			next;
			set '@num1,rand(1,100);		//基準数
			mes "<<この数より高い？低い？>>";
			if('@success > 1)
				mes "^0000FF" +'@success+ "^000000 連勝中!!";
			mes " ";
			mes "　　　-^006300" +'@num1+ "^000000-";
			next;
			if(select("^0000FFHIGH!!^000000","^FF0000LOW!!^000000")==1)
				set '@high,1;		//HIGH選択時フラグ
			else
				set '@high,0;
			set '@num2,rand(1,100);		//実際数
			if(('@num1 < '@num2 && '@high == 1) || ('@num1 > '@num2 && '@high == 0)) {	//成功
				emotion 24,"";
				mes "<<^0000FF" +(('@high)? "HIGH":"LOW")+ "!!　成功!!^000000>>";
				mes " ";
				mes "　　　-^006300" +'@num1+ "^000000-";
				mes " ";
				mes "　　　-^9C009C" +'@num2+ "^000000-";
				set '@success,'@success+1;	//成功数+1
				continue;
			}
			emotion 25,"";
			mes "<<^FF0000" +(('@high)? "HIGH":"LOW")+ "!!　失敗!!^000000>>";
			mes " ";
			mes "　　　-^006300" +'@num1+ "^000000-";
			mes " ";
			mes "　　　-^9C009C" +'@num2+ "^000000-";
			next;
			mes "[紘子]";
			mes "お疲れ様！";
			if('@success < 5) {
				emotion 19;
				mes "あららー、";
				mes "5回連続成功にも満たなかったね……";
				mes "ちょっとダメダメかも〜！";
				next;
				mes "[紘子]";
				mes "今度は最低5回以上連続成功に";
				mes "挑戦してみてね!!";
				close;
			}
			else if('@success < 20) {
				getitem 551,('@success / 5)*20;
				emotion 0;
				mes "おー！";
				mes "連続成功^0000FF" +'@success+ "^000000だね！";
				mes "いい感じだよ!!";
				mes ('@success / 5)*2+"倍返ししまーす!!";
				next;
				mes "[紘子]";
				mes "今度は" +(('@success / 5)+1)*5+ "回以上連続成功に";
				mes "挑戦してみてね!!";
				close;
			}
			else {
				getitem 551,80;
				emotion 0;
				mes "……ええ!?";
				mes "連続成功^0000FF" +'@success+ "^000000!?";
				mes "すっごーい!!";
				mes "これはすごいから8倍返しだー!!";
				next;
				mes "[紘子]";
				mes "これより上はないけど";
				mes "また挑戦してみてね!!";
				close;
			}
		}
	case 2:
		mes "[紘子]";
		mes "えーっ……";
		mes "つまんないのー！";
		close;
	case 3:
		mes "[紘子]";
		mes "えっとね！";
		mes "まず『牌籠』っていうのは、";
		mes "その昔、アマツの兵士達が";
		mes "戦争の時の唯一の娯楽として";
		mes "始めた遊びなの！";
		next;
		mes "[紘子]";
		mes "いろいろな数字が書かれた牌を";
		mes "籠の中に入れて混ぜて、";
		mes "最初の牌を取り出すの。";
		mes "そしたら、次に取り出す牌が";
		mes "最初の牌の数字より高いか低いかを";
		mes "賭けたってわけ！";
		next;
		mes "[紘子]";
		mes "それでこの遊びが『牌籠』と";
		mes "名付けられたの！";
		mes "この名前と、遊びの内容から";
		mes "海の向こうでは『ハイ＆ロー』";
		mes "として伝えられたみたいね。";
		next;
		if(select("本当？","それ嘘でしょう？")==1) {
			mes "[紘子]";
			mes "本当だったら楽しいよね！";
		}
		else
			mes "[紘子]";
		mes "あっ……嘘ってバレちゃった？";
		mes "あははー！";
		mes "ま、まあとりあえずあたしの";
		mes "牌籠のルールを説明するよ！";
		next;
		mes "[紘子]";
		mes "簡単に、最初に出した";
		mes "1から100までの数のどれかより、";
		mes "次の数が大きいか小さいか当てるの！";
		mes "ただし1回だけ成功してもダメ！";
		mes "最低でも連続5回以上成功しないと";
		mes "キミの負けにしちゃうよ！";
		next;
		mes "[紘子]";
		mes "5回以上連続成功で2倍返しかなー。";
		mes "10回以上だったら4倍にしちゃおう！";
		mes "そんな感じだよ！";
		next;
		mes "[紘子]";
		mes "ちなみに出てる数と同じ数が出たら";
		mes "悪いけどキミの負けになるよ！";
		mes "まあでも確率は1％だし……ね！";
		close;
	}
}

//=====================================================================
// 一本釣りおじさん
//=====================================================================
amatsu.gat,252,87,3	script	一本釣りおじさん	709,{
	if((MaxWeight - Weight) < 2000) {
		mes "[一本釣りおじさん]";
		mes "ん？";
		mes "そんなにたくさんアイテムを持って";
		mes "何の用かね？";
		mes "バランスを崩して落っこちたら";
		mes "どうするんだい？";
		mes "持ち物を軽くしてからまた来なさい。";
		close;
	}
	mes "[一本釣りおじさん]";
	mes "やあ、よく来たね。";
	mes "何やらアマツが祭で賑わっていると";
	mes "聞いてね、いい機会だからと";
	mes "来てみたんだが……";
	next;
	mes "[一本釣りおじさん]";
	mes "私は大の釣り好きでね。";
	mes "このアマツの水面を見ていたら";
	mes "祭を楽しむよりもまず";
	mes "釣りをしたくなってしまったのさ。";
	mes "いや、もちろん後で祭も";
	mes "楽しむ予定だよ、はっはっは！";
	next;
	mes "[一本釣りおじさん]";
	mes "どうだい、君もやっていかないか？";
	mes "普通の魚もそこそこ釣れるんだが";
	mes "不思議なことに色々な物や";
	mes "変な生き物まで釣れるがね……";
	mes "ただしエサ代くらいは頂くよ。";
	next;
	if(select("釣りをする","やらない")==2) {
		mes "[一本釣りおじさん]";
		mes "そうか……";
		mes "暇になったらまた来てみてくれよ。";
		close;
	}
	mes "[一本釣りおじさん]";
	mes "よしよし、";
	mes "それじゃあ竿はこれを使いなさい。";
	mes "エサはいくつか持ってきているから";
	mes "好きな物を選んでごらん。";
	next;
	switch(select("^CE0063梅^000000エサ [^CE0063500^000000z]","^006300竹^000000エサ [^0063001000^000000z]","^9C009C松^000000エサ [^9C009C5000^000000z]","やっぱりやめる")) {
	case 1:
		set '@feed,1;	//梅エサセット
		set '@time1,15;	//タイム幅15
		set '@time2,20;	//　　　〜20
		set '@zeny,500;
		break;
	case 2:
		set '@feed,2;	//竹エサセット
		set '@time1,10;	//タイム幅10
		set '@time2,15;	//　　　〜15
		set '@zeny,1000;
		break;
	case 3:
		set '@feed,3;	//松エサセット
		set '@time1,5;	//タイム幅5
		set '@time2,10;	//　　　〜10
		set '@zeny,5000;
		break;
	case 4:
		mes "[一本釣りおじさん]";
		mes "そうか……";
		mes "暇になったらまた来てみてくれよ。";
		close;
	}
	if(Zeny < '@zeny) {
		emotion 16;
		mes "[一本釣りおじさん]";
		mes "おいおい……";
		mes "流石にタダというわけにはいかんよ。";
		close;
	}
	set Zeny,Zeny-'@zeny;
	mes "[一本釣りおじさん]";
	switch('@feed) {
	case 1:	//梅エサ
		mes "^CE0063梅^000000エサだね。";
		mes "こいつは結構丈夫だから";
		mes "ぬいさしにしよう。";
		break;
	case 2:	//竹エサ
		mes "^006300竹^000000エサだね。";
		mes "こいつはエビみたいなやつだから";
		mes "尾掛けにしよう。";
		break;
	case 3:	//松エサ
		mes "^9C009C松^000000エサだね。";
		mes "こいつは結構いい動きをするんだ。";
		mes "ほお掛けにしよう。";
		break;
	}
	mes "よっ……と。よし、付いた。";
	next;
	mes "[一本釣りおじさん]";
	mes "あとはこいつを";
	mes "あっちに勢いよく投げるんだ！";
	mes "いいかい？";
	mes "このエサなら、着水してから";
	mes "^0000FF" +'@time1+ "〜" +'@time2+ "^000000秒もすればアタリがくるはずだ。";
	next;
	mes "[一本釣りおじさん]";
	mes "投げてから、^0000FF" +'@time1+ "〜" +'@time2+ "^000000秒で";
	mes "竿を引くこと。いいね？";
	mes "早すぎたら食いつかないし、";
	mes "遅すぎたら逃げられる。";
	mes "準備はいいかい？";
	mes "準備が良かったら、投げるんだよ。";
	next;
	menu "投げる！",-;
	set '@time,gettimetick(0);	//タイマーセット
	mes "【一本釣り】 / " +(('@feed == 1)? "^CE0063梅": (('@feed == 2)? "^006300竹": "^9C009C松"))+ "^000000エサ挑戦中!!";
	mes " ";
	mes "−投げてから^0000FF" +'@time1+ "〜" +'@time2+ "^000000秒で引き上げろ!!−";
	menu "引き上げる！",-;
	set '@time,(gettimetick(0)-'@time)/1000;	//タイマー取得
	emotion 0,"";
	if('@time < '@time1 || '@time > '@time2) {	//失敗
		mes " ";
		mes "−　^FF0000失　敗　……^000000　−";
		mes "−単細胞が付いていた……−";
		getitem 1052,1;
		next;
		mes "[一本釣りおじさん]";
		mes "おや、残念……";
		mes "エサによって、アタリが来る時間が";
		mes "決まっているからね。";
		mes "今、" +'@time+ "秒くらいだったろう？";
		mes "焦っても、待ちすぎても駄目なんだ。";
		mes "暇になったらまた来るといい。";
		close;
	}
	mes " ";
	switch(rand(40)) {
	case 0:
	case 1:
	case 2:
	case 3:
	case 4:
	case 5:
	case 6:
	case 7:
	case 8:
	case 9:		//エサ別小物
		switch('@feed) {
		case 1:	//梅エサ
			switch(rand(12)) {
			case 0: set '@getid,507; break;		//赤ハーブ
			case 1: set '@getid,508; break;		//黄ハーブ
			case 2: set '@getid,509; break;		//白ハーブ
			case 3: set '@getid,511; break;		//緑ハーブ
			case 4: set '@getid,531; break;		//リンゴジュース
			case 5: set '@getid,532; break;		//バナナジュース
			case 6: set '@getid,533; break;		//ブドウジュース
			case 6: set '@getid,657; break;		//バーサークポーション
			case 7: set '@getid,715; break;		//イエロージェムストーン
			case 8: set '@getid,716; break;		//レッドジェムストーン
			case 9: set '@getid,717; break;		//ブルージェムストーン
			case 10: set '@getid,756; break;	//オリデオコン原石
			case 11: set '@getid,971; break;	//ディトリミン
			}
			break;
		case 2:	//竹エサ
			switch(rand(9)) {
			case 0: set '@getid,514; break;		//ブドウ
			case 1: set '@getid,522; break;		//マステラの実
			case 2: set '@getid,578; break;		//イチゴ
			case 3: set '@getid,610; break;		//イグドラシルの葉
			case 4: set '@getid,757; break;		//エルニウム原石
			case 5: set '@getid,984; break;		//オリデオコン
			case 6: set '@getid,990; break;		//レッドブラッド
			case 6: set '@getid,991; break;		//クリスタルブルー
			case 7: set '@getid,992; break;		//ウィンドオブヴェルデュール
			case 8: set '@getid,993; break;		//イエローライブ
			}
			break;
		case 3:	//松エサ
			switch(rand(14)) {
			case 0: set '@getid,526; break;		//ローヤルゼリー
			case 1: set '@getid,604; break;		//古木の枝
			case 2: set '@getid,608; break;		//イグドラシルの種
			case 3: set '@getid,732; break;		//ダイヤモンド３カラット
			case 4: set '@getid,985; break;		//エルニウム
			case 5: set '@getid,7321; break;	//水晶のかけら
			case 6: set '@getid,12027; break;	//クスクス笑う箱
			case 7: set '@getid,12028; break;	//雷の箱
			case 8: set '@getid,12029; break;	//憂鬱な箱
			case 9: set '@getid,12030; break;	//恨みの箱
			case 10: set '@getid,12031; break;	//眠い箱
			case 11: set '@getid,12032; break;	//豪雨の箱
			case 12: set '@getid,12033; break;	//陽光の箱
			case 13: set '@getid,12034; break;	//あえぐ箱
			}
		}
		break;
	case 10:
	case 11:
	case 12:
	case 13:
	case 14:	//モンスター
		mes "−　^FF3163成　功　!?^000000　−";
		if('@feed == 3 && rand(20) == 0) {
			set 'big,1;
			announce "アマツ観光協会放送局 : た、大変です！　港で大物が釣れて被害が出ている模様!!",9,0x7B6BEF;
			monster "amatsu.gat",248,85,"天津三岐大蛇",1720,1,"Fishing#Summer";
		}
		else if(rand(2))
			monster "amatsu.gat",248,86,"暴れヒトデ",1266,1,"Fishing#Summer";
		else
			monster "amatsu.gat",248,85,"暴れフェン",1158,1,"Fishing#Summer";
		initnpctimer;
		next;
		emotion 23;
		mes "[一本釣りおじさん]";
		mes "おいおいおい!!";
		mes "勢いよく釣り上げすぎた!!";
		mes "しかもモンスターじゃないか!!";
		mes "さっさと片付けてくれ!!";
		close;
	case 15:	//レア
		if('@feed == 3) {
			switch(rand(12)) {
			case 0: set '@getid,617; break;				//古い紫色の箱
			case 1: set '@getid,2101; break;			//ガード
			case 2: set '@getid,2102; set '@slot$,"[1]"; break;	//ガード[1]
			case 3: set '@getid,2103; break;			//バックラー
			case 4: set '@getid,2104; set '@slot$,"[1]"; break;	//バックラー[1]
			case 5: set '@getid,2403; break;			//シューズ
			case 6: set '@getid,2404; set '@slot$,"[1]"; break;	//シューズ[1]
			case 7: set '@getid,2405; break;			//ブーツ
			case 8: set '@getid,2406; set '@slot$,"[1]"; break;	//ブーツ[1]
			case 9: set '@getid,2503; break;			//マフラー
			case 10: set '@getid,2504; set '@slot$,"[1]"; break;	//マフラー[1]
			case 11: set '@getid,7444; break;			//宝箱
			}
			mes "−　^0000FF成　功　!!^000000　−";
			mes "−" +getitemname('@getid)+'@slot$+ "が釣れた！−";
			getitem '@getid,1;
			next;
			emotion 21;
			mes "[一本釣りおじさん]";
			mes "お疲れさん！";
			mes "なかなかの結果じゃないか？";
			mes "暇になったらまた来るといい。";
			close;
		}
	default:	//小物
		switch(rand(10)) {
		case 0: set '@getid,579; break;		//おいしい魚
		case 1: set '@getid,624; break;		//腐った魚
		case 2: set '@getid,711; break;		//若芽
		case 3: set '@getid,713; break;		//空きビン
		case 4: set '@getid,902; break;		//木の根
		case 5: set '@getid,908; break;		//かえるの卵
		case 6: set '@getid,962; break;		//触手
		case 7: set '@getid,966; break;		//貝のむきみ
		case 8: set '@getid,1023; break;	//魚の尻尾
		case 9: set '@getid,7323; break;	//へこんだやかん
		}
		break;
	}
	mes "−　^0000FF成　功　!!^000000　−";
	mes "−" +getitemname('@getid)+ "が釣れた！−";
	getitem '@getid,1;
	next;
	emotion 12;
	mes "[一本釣りおじさん]";
	mes "お疲れさん！";
	mes "まあ、なかなかいい結果は";
	mes "出ないだろうけど、";
	mes "釣りはまったりやるにつきるさ。";
	mes "暇になったらまた来るといい。";
	close;
OnTimer10000:
	if('big)
		end;
	killmonster "amatsu.gat","Fishing#Summer";
	stopnpctimer;
	end;
OnTimer20000:
	killmonster "amatsu.gat","Fishing#Summer";
	announce "アマツ観光協会放送局 : お騒がせいたしました！　大物は帰っていきました!!",9,0x7B6BEF;
	set 'big,0;
	end;
}
amatsu.gat,0,0,0	script	Fishing#Summer	-1,{}	//ダミーイベント

amatsu.gat,253,92,6	script	釣り人	767,{
	emotion 9;
	mes "[釣り人]";
	mes "……";
	mes "……なかなか釣れないなあ。";
	mes "流れの商人から買った、この";
	mes "伝説の釣竿『太公望』でも";
	mes "釣れない物は釣れないのか……";
	next;
	if(select("大変ですね","どう見ても槍です")==1) {
		emotion 33;
		mes "[釣り人]";
		mes "うん……";
		mes "……でもまあ釣りなんて根気だよ。";
		mes "気長にまったり楽しもう。";
		mes "君もそこにいる男に話しかけて";
		mes "釣りをしてみたらどうだい？";
		mes "結構楽しいもんだよ。";
		close;
	}
	emotion 57;
	mes "[釣り人]";
	mes "……やっぱり？";
	mes "そうだよね、これ。";
	mes "釣竿に刃が付いてるのおかしいよね。";
	mes "……だ、騙されたー!!";
	close;
}

//=====================================================================
// 3周年アイスクリーム商人
//=====================================================================
amatsu.gat,168,100,5	script	3周年アイスクリーム商人	85,{
	mes "[3周年アイスクリーム商人]";
	mes "夏ですね！";
	mes "夏といえば夏祭り！";
	mes "そして冷たいアイスクリーム！";
	mes "アマツ納涼泉水祭、";
	mes "楽しんでますかー！";
	next;
	mes "[3周年アイスクリーム商人]";
	mes "実は、我々、アイスクリーム商人組合は";
	mes "先月めでたく3周年を迎えました!!";
	mes "そこで、3周年記念キャンペーンです！";
	mes "私とジャンケンで勝負して";
	mes "勝てたらもう1個プレゼント！";
	mes "あいこはお客さんの負けです。";
	next;
	mes "[3周年アイスクリーム商人]";
	mes "1周年の時に同じキャンペーンを";
	mes "アルベルタで行ったのですが";
	mes "今回はアマツのお祭りに";
	mes "便乗したのですよ！";
	mes "さあさあ、いかがですか？";
	next;
	switch(select("アイスクリームをください！","アイスクリームについて","取引をやめる")) {
	case 1:
		mes "[3周年アイスクリーム商人]";
		mes "ありがとうございます!!";
		mes "1個^0000FF100zeny^000000で、";
		mes "一度に10個までまとめて";
		mes "お買い求めいただけますよ！";
		while(1) {
			next;
			input '@num;
			if('@num < 0 || '@num > 10) {
				mes "[3周年アイスクリーム商人]";
				mes "え？";
				mes "ちょっと多すぎです。";
				mes "一度に販売できるのは";
				mes "10個までです。";
				mes "あまり食べ過ぎると";
				mes "お腹を壊しますよ〜！";
				continue;
			}
			if('@num == 0) {
				mes "[3周年アイスクリーム商人]";
				mes "あら？";
				mes "お腹の調子が良くないのですか？";
				close;
			}
			if('@num*100 < Zeny) {
				mes "[3周年アイスクリーム商人]";
				mes "お客さん、お金が足りないです……";
				mes "1個^0000FF100zeny^000000ですよ！";
				close;
			}
			if(checkweight(536,'@num+1) == 0) {
				mes "[3周年アイスクリーム商人]";
				mes "お客さん、ちょっと荷物が";
				mes "多すぎますね。";
				mes "アイスクリームを落としてしまいます。";
				mes "持ち物を減らしてから来てください〜。";
				close;
			}
			set Zeny,Zeny-('@num*100);
			getitem 536,'@num;
			mes "[3周年アイスクリーム商人]";
			mes "はい、" +'@num+ "個ね。";
			mes "ありがとうございます！";
			mes "さて、では勝負しますか！";
			mes "行きますよ！";
			next;
			mes "[3周年アイスクリーム商人]";
			mes "ジャーンケーン……";
			mes "ポン!!";
			next;
			switch(select("アマツ石田流グー","アマツ泉水流チョキ","アマツ妖狐流パー")) {
			case 1: set '@mine,11; break;	//グー
			case 2: set '@mine,10; break;	//チョキ
			case 3: set '@mine,12; break;	//パー
			}
			emotion '@mine,"";
			set '@enemy,rand(10,12);
			emotion '@enemy;
			mes "[3周年アイスクリーム商人]";
			if(('@mine==10 && '@enemy==12) || (@mine==11 && '@enemy==10) || (@mine==12 && '@enemy==11)) {	//勝ち
				getitem 536,1;
				mes "^FF0000" +(('@enemy==10)? "チョキ": (('@enemy==11)? "グー": "パー"))+ "！^000000……ま、負けた……";
				mes "おめでとうございます！";
			}
			else if('@mine == '@enemy) {	//あいこ
				mes "^FF0000" +(('@enemy==10)? "チョキ": (('@enemy==11)? "グー": "パー"))+ "！^000000アイコですね！";
				mes "残念でした〜！";
			}
			else {	//負け
				mes "^FF0000" +(('@enemy==10)? "チョキ": (('@enemy==11)? "グー": "パー"))+ "！^000000フフフ、私の勝ちです！";
				mes "残念でした〜！";
			}
			mes "またアイスクリームを買って";
			mes "挑戦してくださいね！";
			mes "ただし食べすぎておなかを";
			mes "壊さないように！";
			close;
		}
	case 2:
		mes "[3周年アイスクリーム商人]";
		mes "「アイスクリーム」はですね…";
		mes "牛乳・乳製品の脂肪と";
		mes "無脂肪固形分を主原料に、";
		mes "砂糖・香料・安定剤・乳化剤";
		mes "色素などを加えて混合し、";
		next;
		mes "[3周年アイスクリーム商人]";
		mes "泡立てて凍結させた食べ物です。";
		mes "細かく言うと、";
		mes "乳固形分15%以上";
		mes "うち乳脂肪8%以上";
		mes "のものですね。";
		next;
		mes "[3周年アイスクリーム商人]";
		mes "エネルギーと栄養も豊富で";
		mes "とても美味しいんですが、";
		mes "だからと言って食べ過ぎると";
		mes "お腹を壊しますよ！";
		close;
	case 3:
		mes "[3周年アイスクリーム商人]";
		mes "ジャンケン勝負は";
		mes "今だけですよ〜！";
		mes "さぁ、さぁ、いかがですか〜？";
		close;
	}
}

amatsu.gat,169,99,3	script	犬	81,{
	if((MaxWeight - Weight) < 2000) {
		mes "[犬]";
		mes "ワン！";
		close;
	}
	mes "[犬]";
	mes "ワン！";
	mes "ワンワン!!";
	next;
	if(countitem(932) < 1) {
		mes "[犬]";
		mes "ワフー……";
		close;
	}
	mes "-^0000FFスケルボーン^000000が欲しいみたいだ-";
	next;
	if(select("あげる","あげない")==1) {
		emotion 2;
		mes "[犬]";
		mes "ワン!!";
		mes "ワン、ワンワン!!";
		next;
		delitem 932,1;
		getitem 536,1;
		mes "-こっそりアイスクリームを-";
		mes "-くれた……-";
		close;
	}
	emotion 6;
	misceffect 122,"";
	percentheal -25,0;
	mes "[犬]";
	mes "ガルルルル!!";
	mes "ガウ!!";
	next;
	mes "-噛みつかれた……-";
	close;
}

//=====================================================================
// 大絡繰屋敷
//=====================================================================
amatsu.gat,233,165,3	script	アトラクション係員	758,{
	mes "[係員 姫子]";
	mes "びっくりアトラクション";
	mes "「大絡繰屋敷」は、";
	mes "ただいま準備中となっております。";
	mes "オープンを楽しみにしていて";
	mes "くださいね！";
	close;
OnInit:
	waitingroom "大絡繰屋敷 準備中",0;
	end;
}
