//= Athena Script ==============================================================
// Ragnarok Online Niflheim Town Script
//==============================================================================

//============================================================
// Shop
//------------------------------------------------------------
nif_in.gat,37,93,1	shop	斧商人	801,1301,1351,1354,1357,1360
nif_in.gat,37,84,1	shop	防具商人	801,2501,2501,2503,2503,2505,2505
nif_in.gat,145,23,1	shop	道具商人	801,535,1062,902,7106,537,7154,1052,934

//============================================================
// ニブルヘイムNPC
//------------------------------------------------------------
niflheim.gat,52,147,3	script	キズ	796,{
	mes "[キズ]";
	mes "生きている者はこちらへ来ては";
	mes "いけない!!生きている者の通行は";
	mes "絶対に認めない!!";
	close;
}

niflheim.gat,195,211,6	script	ギガンティア	796,3,3,{
	if(getequipid(1)==2256 || getequipid(1)==2257 || getequipid(1)==2284) {
		mes "["+strcharinfo(0)+"]";
		mes "私に何か用でも？";
		next;
		mes "[ギガンティア]";
		mes "声をかけたのは俺だ。そして";
		mes "その相手は君！間違いない。";
		mes "ちょっとこっちに来てくれ。";
		next;
		mes "[ギガンティア]";
		mes "角を着けているんだな。角はいつでも";
		mes "俺ら死者の王様がちゃんと見て";
		mes "いらっしゃる。";
		close;
	}
	mes "[ギガンティア]";
	mes "王様はいつでも、全てのモノを";
	mes "見ていらっしゃる……";
	close;
OnTouch:
	if(getequipid(1)==2256 || getequipid(1)==2257 || getequipid(1)==2284) {
		mes "[ギガンティア]";
		mes "ちょっと待て！";
		close;
	}
	end;
}

//============================================================
// ニブルヘイム室内NPC
//------------------------------------------------------------
nif_in.gat,16,27,1	script	死んだニワトリ	800,{
	percentheal -5,0;
	mes "[モンチキン]";
	mes "生きていた時、おいらは人間に";
	mes "食われた。だから今度はおいらが";
	mes "食う番。コケコッコー!!";
	mes "でも死んだからこそ、こんな世界に";
	mes "いるんだよな……コケ、コケ、";
	mes "コケコッコー!!";
	close;
}

nif_in.gat,16,30,4	script	死んだコウモリ	799,{
	percentheal -5,0;
	mes "[バットミルリア]";
	mes "生きてても死んでても";
	mes "人間の血はトレビア〜ンです。";
	mes "何です？君も我に血を";
	mes "吸われたいのですか？";
	close;
}

//============================================================
// 呪われた魂NPC
//------------------------------------------------------------
nif_in.gat,156,93,5	script	少女	793,{
	mes "[鍵を握る少女]";
	mes "ここはどこ……？";
	mes "私、目が覚めたらここにいたの……";
	mes "ママ……ねぇ、ママを見なかった？";
	mes "……うわ〜ん。お家に帰りたいよ〜。";
	next;
	switch(select("魔女って何？","呪いって何？","会話をやめる")) {
	case 1:
		mes "[鍵を握る少女]";
		mes "魔女……？魔女なんて知らないよ……";
		mes "あ、でも絵本で見たことある。";
		mes "魔女か……もし本当にいたらとても";
		mes "怖いよね……";
		next;
		mes "[鍵を握る少女]";
		mes "ねぇ、どうしてこんなに寒いの……？";
		mes "お姉ちゃんは寒くないの？";
		mes "……変なのー。ふぅ〜。";
		mes "暖かいお家に帰りたいな。";
		mes "私も一緒に連れてってくれない……？";
		mes "ねぇ……？ねぇ……？";
		close;
	case 2:
		mes "[鍵を握る少女]";
		mes "呪い……お婆ちゃんの話を思い出すな。";
		mes "祝福でも解けない強い呪いを解く";
		mes "呪文があるんだって。";
		next;
		mes "[鍵を握る少女]";
		mes "クルラト……ベラタ……";
		mes "えっと……あれ？何だったけ……？";
		mes "うーん……あと一つが思い出せない。";
		next;
		mes "[鍵を握る少女]";
		mes "……とにかく、その3つの呪文を";
		mes "順番通りに唱えれば、呪われた";
		mes "運命から逃げられるらしいの。";
		mes "何のお話のことかは……うーん、";
		mes "思い出せないな。";
		close;
	case 3:
		mes "[鍵を握る少女]";
		mes "寒い……寒いよ〜……";
		mes "お家に帰りたいよぉ〜……";
		mes "ねぇ、私も連れてって……";
		mes "ここはとても怖い所なの。";
		mes "お願い……助けて……";
		close;
	}
}

niflheim.gat,350,258,1	script	呪われた魂::nif_soul	802,{
	killmonster "niflheim.gat","nif_soul::OnKilled";
	mes "[アッシュブルース]";
	mes "お主には強い呪いを感じる!!!!";
	mes "わしはお主の存在を拒否する!!!!";
	mes "もし退かぬのなら、死を与えよう!!";
	mes "わしの大事な本たちに";
	mes "触れるんじゃない!!!!";
	next;
	switch(select("1番目の本を取ってみる","2番目の本を取ってみる","3番目の本を取ってみる","おとなしく退く")) {
	case 1:
		mes "[アッシュブルース]";
		mes "ぐはははは!!";
		mes "むやみにわしの本を触れるからだ!!";
		mes "噛み切られ、引き裂かれるがよい!!";
		mes "永遠の呪いに囚われるのだ!!";
		monster "niflheim.gat",349,259,"--ja--",1478,1,"nif_soul::OnKilled";
		close;
	case 2:
		mes "[アッシュブルース]";
		mes "わしの本たちに触れるとは……";
		mes "早々に消えろ!!わしの休息の";
		mes "邪魔をするんじゃない!!";
		close2;
		warp "niflheim.gat",34,162;
		end;
	case 3:
		mes "[アッシュブルース]";
		mes "ぬうう……お主。";
		mes "結構、度胸があるようだな。";
		mes "…………!!!!";
		mes "ならば……一度呪文を";
		mes "唱えてみるがよい……!!";
		next;
		if(select("クローバ","クルラト","クレイトス")==2)
			set '@result,'@result+1;
		if(select("ベリト","ベリタス","ベラタ")==3)
			set '@result,'@result+1;
		if(select("ネクタイ","ネックレス","ネロ","^FFFFFFニックト^000000")==4)
			set '@result,'@result+1;
		if('@result==3) {
			mes "[アッシュブルース]";
			mes "呪文は確かに正しい……だが、";
			mes "お主の呪いは解けないのだ……";
			mes "ぐはははは……!!!!";
			//The Sign 3部クエストへ連結される模様
		}
		else {
			mes "[アッシュブルース]";
			mes "ぐはははは!!!!";
			mes "お主、呪文が間違っているぞ!!";
			mes "お主に必要なのは死のようだな。";
			mes "一生、呪われ続けるがよい……!!";
			monster "niflheim.gat",345,259,"--ja--",1462,1,"nif_soul::OnKilled";
			monster "niflheim.gat",347,261,"--ja--",1462,1,"nif_soul::OnKilled";
			monster "niflheim.gat",344,253,"--ja--",1462,1,"nif_soul::OnKilled";
			monster "niflheim.gat",346,251,"--ja--",1462,1,"nif_soul::OnKilled";
			monster "niflheim.gat",349,249,"--ja--",1462,1,"nif_soul::OnKilled";
			monster "niflheim.gat",350,260,"--ja--",1462,1,"nif_soul::OnKilled";
			monster "niflheim.gat",353,256,"--ja--",1462,1,"nif_soul::OnKilled";
		}
		close;
	case 4:
		mes "[アッシュブルース]";
		mes "ぶははは!!";
		mes "よく考えたようだな……";
		mes "お主が無事に生きて帰れることを";
		mes "わしも祈ってやろう。";
		close;
	}
OnKilled:	//ダミーイベント
	end;
}

//============================================================
// ピアノの鍵盤クエストNPC
//- Registry -------------------------------------------------
// NIF_1QUE -> 0〜&64
//------------------------------------------------------------
niflheim.gat,224,243,3	script	アルレグ	795,{
	mes "[アルレグ]";
	mes "煮込みやすくて……うん、";
	mes "お前……うまそうだ。";
	next;
	mes "[アルレグ]";
	mes "……";
	mes "お前を食べてやる!!";
	next;
	mes "-ガブッガブッ-";
	if(NIF_1QUE&1) {
		percentheal -30,0;
		close;
	}
	percentheal -80,0;
	next;
	mes "[アルレグ]";
	mes "久々にうまい肉だった……";
	mes "養殖の鳥並みにの良い肉だよ、お前は。";
	mes "……良い餌を与えて上質のソースを";
	mes "かければ、もっとうまくなりそうだ……";
	next;
	mes "[アルレグ]";
	mes "お礼にこれをあげるよ。";
	mes "この間、拾ったんだ。ほれっ！";
	getitem 7184,1;
	set NIF_1QUE,NIF_1QUE|1;
	close;
}

nif_in.gat,105,81,3	script	クレユ	794,{
	mes "[クレユ]";
	mes "生ある者よ……いつからか、";
	mes "誰からか……それすらも分からない程、";
	mes "古くから伝わる詩を聴かせましょう……";
	next;
	mes "[クレユ]";
	mes "^FF0000陽が傾いて西の山にかかりし時";
	mes "^FF0000空が紫の夕暮れに染まる頃";
	mes "^FF0000美しき旋律に身を委ねれば";
	mes "^FF0000主なる者を助ける鍵を得ようぞ^000000";
	next;
	mes "[クレユ]";
	mes "どんな意味があるのでしょうか……？";
	mes "私は奥深いものがあるような気が";
	mes "します。この詩はもしかしたら、";
	mes "何か秘密があるのかもしれませんね。";
	//if(!(NIF_1QUE&1) || NIF_1QUE&2)
	if(NIF_1QUE&3 != 1)
		close;
	next;
	switch(rand(4)) {
	case 0:
		set '@poem$,"陽が傾いて西の山にかかりし時";
		set '@colm$,"最初の行";
		break;
	case 1:
		set '@poem$,"空が紫の夕暮れに染まる頃";
		set '@colm$,"二行目";
		break;
	case 2:
		set '@poem$,"美しき旋律に身を委ねれば";
		set '@colm$,"三行目";
		break;
	case 3:
		set '@poem$,"主なる者を助ける鍵を得ようぞ";
		set '@colm$,"最後の行";
		break;
	}
	mes "[クレユ]";
	mes "それではここでテスト！";
	mes "今、聴かせた詩の" +'@colm$+ "を";
	mes "正確に再現してください！";
	next;
	input '@word$;
	if('@word$ != '@poem$) {
		mes "[クレユ]";
		mes "ううむ……。しっかり聴いていたら、";
		mes "こんなの簡単に歌えるでしょう！";
		mes "きちんと覚えてから再度、挑戦しに";
		mes "来なさい！";
		close;
	}
	mes "[クレユ]";
	mes "おお……よく覚えていましたね！";
	mes "私はクレユと申します。";
	mes "吟遊詩人にとってあなたは";
	mes "一番、好まれるお客です。";
	mes "ご褒美にこれをさしあげましょう。";
	next;
	set NIF_1QUE,NIF_1QUE|2;
	getitem 7184,1;
	mes "[クレユ]";
	mes "是非、他の吟遊詩人達も";
	mes "喜ばせてください。";
	close;
}

nif_in.gat,31,20,3	script	ガラクタ屋	794,{
	mes "[ガラクタ屋]";
	mes "商売にいっくら精を出してもよ！";
	mes "前世で出した店も今も、何故か商売が";
	mes "うまくいかないんだよな……";
	if(NIF_1QUE&5 != 1)
		close;
	next;
	mes "[ガラクタ屋]";
	mes "おい、あんた！";
	mes "どうせ外へ行くんなら";
	mes "これを捨ててきてくれ！";
	set NIF_1QUE,NIF_1QUE|4;
	getitem 7184,1;
	next;
	mes "[ガラクタ屋]";
	mes "全くどーして商売が";
	mes "軌道に乗らないんだよ。";
	mes "俺の何が悪いってんだよ。";
	mes "ぶつぶつぶつ……";
	close;
}

niflheim.gat,208,103,0	script	#墓1	111,1,1,{
	end;
OnTouch:
	if(NIF_1QUE&7 != 7 || NIF_1QUE&24 == 24)
		end;
	if(NIF_1QUE&8) {
		mes "- 先ほど掘りだした所に、更に何かが -";
		mes "- 埋まっているのを見つけた。また -";
		mes "- 同じ物のようだ。やはり何かの -";
		mes "- 部品かもしれない。 -";
	}
	else {
		mes "- 墓石の周りに何かが埋められて -";
		mes "- いるのを見つけた。細長い物だ。 -";
		mes "- 何かの部品だろうか？ -";
		mes "- ………… -";
	}
	next;
	mes "- 拾いますか？ -";
	next;
	if(select("はい","いいえ")==1) {
		getitem 7184,1;
		if(NIF_1QUE&8)
			set NIF_1QUE,NIF_1QUE|16;
		else
			set NIF_1QUE,NIF_1QUE|8;
	}
	close;
}

niflheim.gat,169,71,0	script	#墓2	111,1,1,{
	end;
OnTouch:
	if(NIF_1QUE&56 != 24)
		end;
	mes "- 墓石の周りに何かが埋められて -";
	mes "- いるのを見つけた。細長い物だ。 -";
	mes "- 何かの部品だろうか？ -";
	mes "- ………… -";
	next;
	mes "- 拾いますか？ -";
	next;
	if(select("はい","いいえ")==1) {
		getitem 7184,1;
		set NIF_1QUE,NIF_1QUE|32;
	}
	close;
}

nif_in.gat,115,181,0	script	#ピアノ	111,1,1,{
	end;
OnTouch:
	//if((NIF_1QUE&64) || !(NIF_1QUE&32)) close;
	if(NIF_1QUE&96 == 32) {
		mes "- ピアノの欠けた部分に鍵盤を -";
		mes "- 一つずつはめてみた。だが、 -";
		mes "- 一番左側の鍵盤が一つだけ  -";
		mes "- 足りなかった。-";
		set NIF_1QUE,NIF_1QUE|64;
		close;
	}
	end;
}

nif_in.gat,118,151,1	script	#ピアノ3	111,1,1,{
	end;
OnTouch:
	if(NIF_1QUE&64) {
		mes "- 自分の体から伸びた影が、 -";
		mes "- 一つだけ欠けた鍵盤の上に -";
		mes "- ちょうど重なった瞬間…… -";
		next;
		soundeffect "complete.wav",0;
		mes "- 体が軽くなるような感じがして…… -";
		mes "- 周囲が曇り始めた…… -";
		close2;
		warp "nif_in.gat",179,163;
	}
	end;
}

nif_in.gat,188,168,3	script	魔女	792,{
	mes "[キルケラ]";
	mes "うーん……？";
	mes "あなた、生きてるじゃない!?";
	mes "ここまで来るのは";
	mes "大変だったでしょう……";
	mes "理由はどうあれ、ここは";
	mes "生きている者が来る所じゃないよ。";
	next;
	mes "[キルケラ]";
	mes "私の力で外へ出してあげるから、";
	mes "もう二度と来ちゃ駄目だよ。";
	close2;
	warp "umbala.gat",138,208;
	end;
}

//============================================================
// 死者の伝言クエストNPC（未完成）
//- Registry -------------------------------------------------
// NIF_2QUE -> 0〜6
//------------------------------------------------------------
prt_in.gat,161,20,2	script	エリュス	92,{
	switch(NIF_2QUE) {
	case 0:
		emotion 28;
		mes "[エリュス]";
		mes "シクシクシク……";
		mes "うううぅぅ〜……";
		mes "エリー、どうして僕を残して";
		mes "先にいなくなるんだ……？";
		next;
		if(select("慰める","放っておく")==2) {
			mes "[エリュス]";
			mes "シクシク、うぅ……うわーん!!";
			mes "喧嘩してしまったあの時が、";
			mes "エリーとの最後だったなんて……";
			mes "ううううううう……";
			close;
		}
		mes "[エリュス]";
		mes "あ、ありがとうございます。";
		mes "シクシク……実は、旅から帰ってきたら";
		mes "私の妻であるエリーが……な、";
		mes "亡くなっていたんです!!うぅぅ〜。";
		mes "最後の挨拶をできなかったのが";
		mes "とても悔やまれて……胸が痛みます。";
		next;
		mes "[エリュス]";
		mes "彼女に、どうしても伝えたい";
		mes "言葉があるんです!!僕はどうすれば";
		mes "良いのでしょうか?!";
		next;
		mes "[エリュス]";
		mes "旅の途中で聞いた話によると……";
		mes "どこかに死者の街というものがある";
		mes "らしいんです。そこへ行けば、";
		mes "もしかしたら彼女に会えるかも";
		mes "しれません。ですが、僕は子供達を";
		mes "置いて行くわけにはいかないので……";
		next;
		if(select("私が伝えましょうか？","残念ですね")==2) {
			mes "[エリュス]";
			mes "ああ、何かいい方法がないでしょうか？";
			mes "もしエリーに言葉を伝えられるのなら、";
			mes "僕はどんなことでもするのに……";
			close;
		}
		mes "[エリュス]";
		mes "ほ、本当によろしいのですか?!";
		mes "はい。是非、お願いします!!";
		mes "とても感謝します。このご恩は";
		mes "一生、絶対に忘れません！";
		next;
		mes "[エリュス]";
		mes "ではエリーに会ったら……";
		mes "「一ヶ月前のことは誤解だ。";
		mes "本当にすまなかった。」";
		mes "と伝えてください。";
		mes "よろしくお願いします。";
		set NIF_2QUE,1;
		close;
	default:	//1〜4の場合
		mes "[エリュス]";
		mes "噂によると……死者の街では悲鳴が";
		mes "絶えないそうです。死後に行く世界が";
		mes "そんな感じだなんて……何とも言えない";
		mes "ですね。では、よろしくお願いします。";
		close;
	case 5:
		mes "[エリュス]";
		mes "はっ！本当に行ってくれたんですね！";
		mes "僕なんかの頼みの為に、わざわざあんな";
		mes "遠くまで……本当に感謝します。";
		mes "あの〜、エリーには会えましたか？";
		next;
		if(select("はい。許すと言っていました。","会えませんでした")==1) {
			mes "[エリュス]";
			mes "何か勘違いしたのではないでしょうか？";
			mes "このようなことで僕を欺こうとする";
			mes "なんて悪い人ですね。";
			close;
		}
		mes "[エリュス]";
		mes "やはり……会えませんでしたよね……";
		mes "僕がお願いしたことは、あまりにも";
		mes "難しい事でしたしね。";
		next;
		mes "[エリュス]";
		mes "それでも僕なんかの為に、わざわざ";
		mes "気を使ってくださったから……";
		mes "つまらない物ですが、お礼として";
		mes "これを受け取ってください。";
		next;
		mes "[エリュス]";
		mes "とにかくこんなに苦労してくださって";
		mes "本当に感謝します。あなたの旅に";
		mes "祝福がありますように……";
		getitem 610,10;
		set NIF_2QUE,6;
		close;
	case 6:
		mes "[エリュス]";
		mes "あなたの旅に祝福がありますように……";
		close;
	}
}

umbala.gat,215,181,3	script	ビルリック	733,{
	switch(NIF_2QUE) {
	case 0:
		mes "[ビルリック]";
		mes "何だ？特に用がないんなら、";
		mes "あっちに行きな！";
		close;
	case 1:
		mes "[ビルリック]";
		mes "死者の街……";
		mes "あそこはとても危ない所だから";
		mes "行かない方がいいのに。";
		mes "そこへどうして行こうと思うんだ？";
		next;
		if(select("会わなければならない人がいます","冒険の一部ですから")==2) {
			mes "[ビルリック]";
			mes "ははは。君も冒険好きか。俺もだよ。";
			mes "だがあそこは、熟練者にとっても危ない";
			mes "所だから、気をつけて行きな。";
			close;
		}
		mes "[ビルリック]";
		mes "会わなければならない人……";
		mes "あそこで会うってことは死んだ人か……";
		mes "大変だな。";
		next;
		mes "[ビルリック]";
		mes "じゃ、あそこへ行ったらペイリンという";
		mes "人に会ってみな。俺が紹介してくれたと";
		mes "言って、話を聞いてみるといい。";
		mes "ちなみにあいつはバラが好きだから、";
		mes "バラの花を1輪、持っていくといいよ。";
		next;
		mes "[ビルリック]";
		mes "死者の街はここから北の方へ行くと";
		mes "あるよ。気をつけて行きな。";
		mes "熟練者にとっても危ない所だから";
		mes "用心した方がいい。";
		set NIF_2QUE,2;
		close;
	default:
		emotion 21;
		mes "[ビルリック]";
		mes "おお、久しぶりだな。機会があれば、";
		mes "今度は死者の街で会おう。ははは。";
		close;
	}
}

niflheim.gat,153,215,3	script	ペイリン	794,{
	switch(NIF_2QUE) {
	default:	//0.1.5.6の場合
		mes "[ペイリン]";
		mes "こんなに危ない街にどうして";
		mes "いらっしゃったのですか？";
		mes "早く帰りなさい。";
		close;
	case 2:
		mes "[ペイリン]";
		mes "こんなに若くして死ぬなんて……";
		mes "あなたも本当に気の毒な方ですね。";
		next;
		mes "[ペイリン]";
		mes "…………？";
		mes "あら、まだ死んではいないのですね？";
		mes "生きている身で、ここまで来るなんて";
		mes "本当にすごいですね。";
		next;
		mes "[ペイリン]";
		mes "誰かに会いに来たのですか？でなければ";
		mes "他の目的で来たのですか？この前、";
		mes "死んだ人に会いたいから来たという方が";
		mes "いたんで……すみませんが、";
		mes "ニブルヘイムの住民でそんな能力を";
		mes "持った者はいません。";
		next;
		mes "[ペイリン]";
		mes "ここまで来た努力は察します。でも、";
		mes "こんな所まで来ても、誰にも会うことは";
		mes "できません。危険な目に遭う前に";
		mes "お帰りなさい。";
		set NIF_2QUE,3;
		close;
	case 3:
		emotion 6;
		mes "[ペイリン]";
		mes "帰りなさいと言ったのに、しつこい方";
		mes "ですね。私に尋ねても、何も知りません";
		mes "と言ったでしょ?!";
		next;
		if(select("そうですが……","申し訳ありません")==2) {
			mes "[ペイリン]";
			mes "気をつけて帰りなさい。";
			mes "こんな所へ来ることは、";
			mes "良いことではありませんから。";
			close;
		}
		mes "[ペイリン]";
		mes "こんなにしつこいってことは、";
		mes "誰かが余計なことを言いましたね？";
		mes "誰が何を言ったかは知りませんが、";
		mes "それはしてはいけない事です。";
		mes "お教えすることはできません。";
		set NIF_2QUE,4;
		close;
	case 4:
		if(countitem(748)<1) {
			mes "[ペイリン]";
			mes "何度も尋ねられても、何もありません。";
			mes "諦めて帰りなさい。";
			close;
		}
		mes "[ペイリン]";
		mes "うーん……ビルリックに聞いた";
		mes "のですか。その方には以前、お世話に";
		mes "なったんですよね……";
		next;
		mes "[ペイリン]";
		mes "それではお教えする前に注意事項を";
		mes "お話します。ご想像以上の代価を";
		mes "要求することがあります。その代価を";
		mes "受け入れる覚悟をなさったら";
		mes "お知らせください。";
		next;
		if(select("覚悟はできています","そんなことまではできません")==2) {
			mes "[ペイリン]";
			mes "よく考えたのですね。";
			mes "あまりにも危険な魔術なので、";
			mes "私も勧めたくはありません。";
			close;
		}
		mes "[ペイリン]";
		mes "そのようにまでおっしゃるのなら、";
		mes "お教えしますが……禁止されている";
		mes "魔術なので絶対、他言無用で";
		mes "お願いします。";
		next;
		mes "[ペイリン]";
		mes "魔術には大きな犠牲を要します。";
		mes "等価交換の法則により、死者に会う";
		mes "試みの為には、それに相応する材料と";
		mes "犠牲を要します。";
		next;
		mes "[ペイリン]";
		mes "時には失敗することもあります。";
		mes "材料としてイグドラシルの葉を10枚";
		mes "必要です。魔術を施す用意が";
		mes "できましたか？";
		next;
		if(select("はい","いいえ")==2) {
			mes "[ペイリン]";
			mes "それでは、材料を集めてから";
			mes "いらしてください。しかし、材料を";
			mes "集めてきても成功するかどうかは、";
			mes "判りませんが……";
			close;
		}
		if(countitem(610) < 10) {
			mes "[ペイリン]";
			mes "材料が足りないのなら仕方ないですね。";
			mes "材料が揃いましたら、声をかけて";
			mes "ください。";
			close;
		}
		if(BaseLevel < 81) {
			mes "[ペイリン]";
			mes "申し訳ありませんが、まだあなたには";
			mes "これに耐えられるだけの力がないみたい";
			mes "ですね。今、これを行うとあなたは";
			mes "死にます。だからできません。";
			close;
		}
		mes "[ペイリン]";
		mes "それでは始めます。";
		mes "…………";
		mes "ウムバソカサバハ";
		mes "ウムバウムバフムババ！";
		mes "イェエエイッ！";
		next;
		delitem 748,1;
		delitem 610,10;
		//確率で成否判定、成功ルート未実装
		emotion 23;
		mes "[ペイリン]";
		mes "あらら！申し訳ございません。";
		mes "召喚に失敗しました。こんなに";
		mes "ご苦労なさったのに……本当に";
		mes "申し訳ございません。";
		heal -(Hp-1),0;
		set NIF_2QUE,5;
		close;
	}
}
