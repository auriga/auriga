//====================================================================
//Ragnarok Online EP4.0(J) Super Novice Jobchange Script
//
//　■ CHANGE_SNV -> 0〜1
//====================================================================

//==========================================
// 試験申請および転職
//------------------------------------------

aldeba_in.gat,223,167,3	script	トゼーレ	709,{
	if(Upper == UPPER_HIGH) {
		mes "[トゼーレ]";
		mes "君はもう立派なノービスクラブの";
		mes "一員で……";
		next;
		mes "[トゼーレ]";
		mes "ええええ!!";
		mes "あ、あなたは!?　あなたは〜!!";
		mes "- ジリリリ〜ン!! -";
		next;
		mes "- ガチャ!! -";
		next;
		mes "[トゼーレ]";
		mes "え？　はい、はい、はい。";
		mes "分かりました。はい……";
		mes "あっ！　い、いえ！";
		mes "まだ何も言ってないです。";
		mes "はい、了解しました。";
		mes "- 受話器を置いた。 -";
		next;
		mes "[トゼーレ]";
		mes "…………";
		mes "では御機嫌よう〜。";
		close;
	}
	if(Job == Job_SuperNovice || Job == Job_ExpandedSuperNovice) {
		mes "[トゼーレ]";
		mes "君はもう立派なノービスクラブの一員";
		mes "です。特に秀でた何かがある訳じゃなし";
		mes "でも、特に悪いって面も無し。";
		mes "そんな普通で立派なノービスなのです。";
		next;
		mes "[トゼーレ]";
		mes "今後もずっと、普通で平凡な人生を歩み";
		mes "楽しみ、勤しみ、ノービスの存在を";
		mes "広々と広めてください。";
		close;
	}
	if(Job != Job_Novice) {
		mes "[トゼーレ]";
		mes "むむむ！あなたは普通で平凡な人では";
		mes "ありませんね！私はノービスクラブの";
		mes "非公式ボス、トゼーレと申します。";
		next;
		mes "[トゼーレ]";
		mes "ここは、普通で平凡じゃないあなたの";
		mes "様な人には一生縁がない場所です。";
		mes "いいんです、あなたに我等の信念を";
		mes "理解してもらおうなんて思ってませんし";
		mes "我等も、あなた達を理解しようと思って";
		mes "いませんから。";
		next;
		mes "[トゼーレ]";
		mes "……世の中はあんな人もいれば、こんな";
		mes "人もいる。ただそれだけの話ですよ。";
		close;
	}
	if(CHANGE_SNV == 0) {
		mes "[トゼーレ]";
		mes "そこのあなた！あなたです!!";
		mes "全身で普通で平凡だと言ってます！";
		mes "我等のために生まれたようなお方だ！";
		next;
		mes "[トゼーレ]";
		mes "どうです？";
		mes "我等がノービスクラブに加入し";
		mes "我等と共に普通で平凡に考え行動し";
		mes "普通で平凡な道を歩み、普通で";
		mes "平凡な生を思いっきり堪能して";
		mes "みませんか？";
		next;
		mes "[トゼーレ]";
		mes "その代わりと言っては何ですが";
		mes "会員になったら、ノービスの中でも";
		mes "最高位のスーパーノービスに";
		mes "転職させてあげます。";
		next;
		switch (select("加入します","加入しません","もうちょっと説明を聞く")) {
		case 1:
			if(getskilllv(1) < 9 || SkillPoint) {
				mes "[トゼーレ]";
				mes "おやおや……残念ですが、まだ転職に";
				mes "耐えられる実力が、備わっていないよう";
				mes "です。";
				mes "JobLvを10にまで成長させ、";
				mes "^0000FF「基本スキル」のレベルを9　^000000に";
				mes "してください。";
				next;
				mes "[トゼーレ]";
				mes "^0000FF「基本スキル」^000000のレベルは";
				mes "^0000FF「スキルリスト」^000000ウィンドウで";
				mes "上げることができます。";
				mes "^0000FF「スキルリスト」^000000ウィンドウは";
				mes "^0000FF「基本情報」^000000ウィンドウ内の";
				mes "^0000FF「Skill」^000000ボタンで表示されます。";
				next;
				mes "[トゼーレ]";
				mes "スキルのレベルを上げるには、";
				mes "スキルポイントを割り振った後、";
				mes "^FF0000「確定」^000000ボタンが必要となります。";
				mes "ご注意ください。";
				close2;
				cutin "start_020_jp.bmp",4;
				end;
			}
			if(BaseLevel < 45) {
				mes "[トゼーレ]";
				mes "おやおや……残念ですが、まだ転職に";
				mes "耐えられる実力が、備わっていないよう";
				mes "です。ベースレベルが^0000FF45以上^000000になって";
				mes "から、またお越しください。";
				close;
			}
			mes "[トゼーレ]";
			mes "おおーっ。このご時世、普通に平凡に";
			mes "生きるのは荊の道！その道をあえて";
			mes "選んでくれるとは……";
			mes "よく決心してくれました！";
			next;
			mes "[トゼーレ]";
			mes "普通……平凡……良い言葉だ。";
			mes "そう思いませんか。";
			next;
			mes "[トゼーレ]";
			mes "しかし、ただ単に申し込みをすれば";
			mes "全員ノービスクラブの会員になれる";
			mes "わけではありません。普通で平凡な";
			mes "資格テストがあるのです。";
			next;
			mes "[トゼーレ]";
			mes "普通で平凡な生活にふさわしく";
			mes "普通なマップに行って、平凡な";
			mes "アイテムを持って来てください。";
			mes "うーん……やっぱりここは、";
			mes "^FF0000べとべとした液体^000000と^FF0000古木の露^000000が";
			mes "いいでしょう。";
			next;
			mes "[トゼーレ]";
			mes "二つのアイテムを^FF000030個^000000ずつ集めて来て";
			mes "ください。ね、ね、この数字も実に";
			mes "普通で平凡でしょ〜。";
			next;
			mes "[トゼーレ]";
			mes "では、お帰りをお待ちしておりま〜す。";
			set CHANGE_SNV,1;
			setquest 6010;
			close;
		case 2:
			mes "[トゼーレ]";
			mes "おやおや……";
			mes "あなたは、普通で平凡な道の楽しさが";
			mes "まるで分かっていないようですね。";
			mes "……まぁいいでしょう。人生いろいろ。";
			mes "自分の道は自分で決めるものですから。";
			next;
			mes "[トゼーレ]";
			mes "考えが変わることがあれば";
			mes "いつでもいらっしゃい。";
			close;
		case 3:
			mes "[トゼーレ]";
			mes "えっへん。我等がクラブについて";
			mes "もっと知りたいと言うのですね。";
			mes "よろしい！私が全部包み隠さず";
			mes "教えて差し上げましょう!!";
			next;
			mes "[トゼーレ]";
			mes "まずは、ノービスクラブ創始者である";
			mes "キム=ションニムの話から、始めなけ";
			mes "ればなりますまい。キム=ションニムは";
			mes "人々が、普通で平凡な日常の大切さを";
			mes "忘れ去ろうとしているのを、それはそれ";
			mes "は切なく思い……";
			next;
			mes "- あなたはトゼーレの -";
			mes "- 演説を10分間、聞き続けなければ -";
			mes "- ならなかった…… -";
			next;
			mes "[トゼーレ]";
			mes "……そして、ノービスクラブは今日に";
			mes "至り、我等は普通で平凡をモットーに";
			mes "普通で平凡であることを誇りに、ノー";
			mes "ビスクラブの活動を続けているのです！";
			mes "えっへんっ!!";
			close;
		}
	}
	if(countitem(938) < 30 || countitem(907) < 30) {
		mes "[トゼーレ]";
		mes "おや、必要な品物を忘れてしまいました";
		mes "か。また説明します。いいですか？";
		next;
		mes "[トゼーレ]";
		mes "^FF0000べとべとする液体^000000と ^FF0000古木の露^000000";
		mes "です。今度は忘れないでくださいねー。";
		close;
	}
	mes "[トゼーレ]";
	mes "うん、やはり私の予想通り、あなたは";
	mes "普通で平凡な人生を歩むために生まれて";
	mes "きた方のようです！我等がノービスクラ";
	mes "ブに加入する資格充分と見ました。";
	next;
	mes "[トゼーレ]";
	mes "それでは約束どおりスーパーノービス";
	mes "に……おやや？";
	mes "ああーーー、あなたの背中に!!!!!!";
	next;
	if(select("えっ!?","な、何っ?!")==1) {
		mes "- あなたは振り返った -";
		mes "- しかし何もない…… -";
		mes "- おや？っと思った刹那！ -";
	}
	else {
		mes "- あなたはトゼーレの言葉に -";
		mes "- びっくりし、声を出してしまった -";
		mes "- その瞬間！ -";
	}
	next;
	delitem 938,30;
	delitem 907,30;
	getitem 2339,1;
	unequip;
	jobchange Job_SuperNovice;
	set CHANGE_SNV,0;
	chgquest 6010,50275;
	mes "[トゼーレ]";
	mes "あはははは！驚きました？";
	mes "普通で平凡な日常に、ちょっとした";
	mes "悪戯で一味違った日常を産み出す……";
	mes "それが、このトゼーレの信条です。";
	next;
	mes "[トゼーレ]";
	mes "ま、その話は置いておいて。";
	mes "結果、あなたもこれで立派な";
	mes "スーパーノービスです。";
	next;
	mes "[トゼーレ]";
	mes "差し上げたものは、伝説のキム=ション";
	mes "ニムも着ていた伝説の防具です！";
	mes "是非使ってください。";
	next;
	mes "[トゼーレ]";
	mes "では御機嫌よう〜。今後も普通に平凡に";
	mes "暮し、私たちノービスクラブの存在を";
	mes "非平凡な人達に知らしめてください〜。";
	close;
OnInit:
	waitingroom "転職",0;
	end;
}

//==============================================================
aldeba_in.gat,216,169,5	script	セレイ	86,{
	if(Job == Job_SuperNovice || Job == Job_ExpandedSuperNovice) {
		mes "[セレイ]";
		mes "おおおおおおおおおおおおお!!!!";
		mes "あなたは我等がノービスクラブの";
		mes "一員ではないですか！";
		next;
		mes "[セレイ]";
		mes "今後ともよろしく！";
		mes "これからも普通に平凡に暮らして";
		mes "みようじゃありませんか！";
		next;
		mes "[セレイ]";
		mes "あ、ちなみに私はトゼーレ様直属の";
		mes "ナンバーワン会員です！えっへん！";
		close;
	}
	if(Job != Job_Novice) {
		mes "[セレイ]";
		mes "ツン！プイッ!!";
		mes "普通で平凡な道を脱した奴なんて";
		mes "知らないもんね！";
		mes "せいぜい、思いっきり危険で非平凡に";
		mes "生きてるといいさ！";
		mes "プイプイッ!!";
		close;
	}
	mes "[セレイ]";
	mes "あなたはまだ本当に普通で平凡な道の";
	mes "楽しさが分かっていなーーーーーい！";
	mes "あなたもノービスクラブの一員に";
	mes "なりましょうよ、ねっねっ。";
	next;
	mes "[セレイ]";
	mes "やはりノービスは最高だ！";
	mes "ワッハッハッハッハッハッ!!";
	close;
}


//==========================================
// 専用カートサービス
//------------------------------------------

aldebaran.gat,54,238,5	script	カプラ職員	117,{
	if(Job != Job_SuperNovice && Job != Job_ExpandedSuperNovice) {
		mes "[カプラ職員]";
		mes "こんにちは〜。申し訳ございませんが";
		mes "私がお客様にして差し上げられることは";
		mes "何もありません。他のカプラ職員を";
		mes "あたってみてください。";
		close;
	}
	mes "[カプラ職員]";
	mes "おや、スーパーノービスですね。";
	mes "え、他のカプラ職員が、カートを";
	mes "貸してくれないのですか？";
	mes "ご安心を！";
	next;
	mes "[カプラ職員]";
	mes "この私が、スーパーノービスの皆さんに";
	mes "専用カートをお貸しして差し上げます。";
	mes "カプラ本社の命令と相反するので、";
	mes "こっそり内緒に貸しているのですけど。";
	mes "テヘ。";
	next;
	mes "[カプラ職員]";
	mes "前書きはこれくらいにして！";
	mes "カート利用料金は^FF00001900zeny^000000です。";
	next;
	mes "[カプラ職員]";
	mes "カートを利用するには";
	mes "「プッシュカート」スキルが必要です。";
	mes "そのスキルがないとカートを利用";
	mes "できずにお金だけ消えてしまいます。";
	mes "準備はよろしいですか？";
	next;
	if(select("カートを借りる","カートを借りない")==1) {
		mes "[カプラ職員]";
		mes "ここのサービスは内緒のサービスなので";
		mes "私は皆さんが「プッシュカート」を";
		mes "取得しているか、確認できないのです。";
		mes "大丈夫ですか？「プッシュカート」が";
		mes "ないと、カートを利用できずに";
		mes "お金だけ消えてしまいますよ。";
		next;
		if(select("大丈夫！","ちょっと待って")==1) {
			if(Zeny < 1900) {
				mes "[カプラ職員]";
				mes "申し訳ありません。";
				mes "お金が足りないようです。";
				close;
			}
			set Zeny,Zeny-1900;
			set KAFRA_PIT,KAFRA_PIT+190;
			setcart;
			mes "[カプラ職員]";
			mes "ご利用ありがとうございます。";
			mes "合法的サービスではないですが";
			mes "(株)カプラサービスをご利用いただき";
			mes "ありがとうございました。";
			close;
		}
	}
	mes "[カプラ職員]";
	mes "またのお越しをお待ちしております。";
	mes "ありがとうございました。";
	close;
}
