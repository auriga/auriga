//===============================================================================================
//Ragnarok Online Bard jobchange script
//
//　■ CHANGE_BA -> 0〜4
//　　 LUTIE_1QUE -> スノウノウクエストフラグ
//===============================================================================================

comodo.gat,211,155,5 script さすらいの吟遊詩人 741,{
	cutin "job_bard_aiolo01",2;
	if(Upper == UPPER_HIGH) {
		mes "[アイオルデ]";
		mes "英雄たる素質を持つ人間は";
		mes "その時代に数えきれず生まれ";
		mes "しかし可能性や運命を忘れ";
		mes "平凡な暮らしをしてしまう……";
		next;
		mes "[アイオルデ]";
		mes "風よ、雲よ、皆に伝えてごらん";
		mes "食べて飲んで歌う日々を送る皆に";
		mes "あなたの人生はそうではないのだと";
		mes "大事なことを忘れているよと……";
		close2;
		cutin "job_bard_aiolo01",255;
		end;
	}
	if(Job == Job_Novice) {
		mes "[アイオルデ]";
		mes "胸の中に一つ一つ刻まれる痛みを";
		mes "全て数える事はできないさ。";
		mes "アイテムは簡単に出るけれど";
		mes "それが旅の足しになるかって言うとね…";
		mes "私もまだレアなんて見たことないさ。";
		next;
		mes "[アイオルデ]";
		mes "私の歌が聞きたいかい？";
		mes "ごめんよ。";
		mes "新しい歌を作っているところなんだ。";
		next;
		mes "[アイオルデ]";
		mes "ハハ……他の人に歌ってくれるよう";
		mes "頼んでくれないかい？";
		mes "私は今ちょっと手が離せなくてね。";
		close2;
		cutin "job_bard_aiolo01",255;
		end;
	}
	if(Job == Job_Bard) {
		mes "[アイオルデ]";
		mes "やぁ、今日も元気に歌ってるかい？";
		mes "たくさん歌えるようになったかい？";
		next;
		mes "[アイオルデ]";
		mes "歌は人々に楽しさと感動を";
		mes "伝えるのだから、常に新しい歌を";
		mes "覚えることを忘れてはいけないよ。";
		next;
		mes "[アイオルデ]";
		mes "楽しい心も忘れないように。";
		mes "訪れる先々で歌ってあげることもね。";
		close2;
		cutin "job_bard_aiolo01",255;
		end;
	}
	if(Job != Job_Archer) {
		mes "[アイオルデ]";
		mes "ラララ〜、ラララ〜、美しいコモド〜";
		mes "いつも楽しいことで一杯なの〜";
		next;
		mes "[アイオルデ]";
		mes "君も今日はあらゆることを忘れて〜";
		mes "思いっきり楽しんでおくれ〜";
		mes "今日と言う日は二度は来ないから〜";
		close2;
		cutin "job_bard_aiolo01",255;
		end;
	}
	switch(CHANGE_BA) {
	case 0:
		mes "[アイオルデ]";
		if(Sex)
			mes "こんにちは！愉快なアーチャーの友よ";
		else {
			mes "こんにちは！";
			mes "素敵なアーチャーのお嬢さん";
		}
		mes "私のような流れ者に何の用だい？";
		next;
		switch(select("良い声をしていますね。","歌を一曲聞かせてください。","特に用はない。")) {
			case 1:
				mes "[アイオルデ]";
				mes "ハハハ! 勿論だとも！";
				mes "いつも楽しい気持ちで歌えば";
				mes "自然とそうなるものさ！";
				next;
				mes "[アイオルデ]";
				mes "しかし、バードという職業は声が命だ。";
				mes "たまには声がかれる時もあるから";
				mes "気を付けなければならないよ。";
				break;
			case 2:
				mes "[アイオルデ]";
				mes "歌かい? いいよ。";
				mes "それでは、一曲歌ってみようか……";
				next;
				mes "[アイオルデ]";
				mes "戦場の歌を歌ってみようかな。";
				mes "フムフム、ホッホッ";
				mes "1、2、3、4〜";
				next;
				mes "地平線から鳴り響く蹄の音";
				mes "夕陽を覆うフェイヨンの土埃";
				mes "夜空に幾千の目が開かれる瞬間";
				mes "城郭の松明をしっかと燃やせ";
				next;
				mes "聞こえる、心臓の鼓動";
				mes "押し寄せる、血の脈動";
				mes "伝わる、鎧の重さ";
				mes "現わる、敵方の姿";
				next;
				mes "もっと大きく、もっと大きく、";
				mes "もっと大きく鳴らすがいい！";
				mes "進む兵等よ、力を込めよ！";
				mes "もっと高く、もっと高く、";
				mes "もっと高く叫ぶがいい！";
				mes "今日と言う日は二度と来なかろう！";
				next;
				mes "空を仰ぎ、大地を踏み締めろ！";
				mes "裂けそうな心が駆け出すように！";
				mes "ほら笛を城壁に轟かせよ！";
				mes "今日と言う日は二度と来なかろう！";
				next;
				mes "[アイオルデ]";
				mes "フムフム……いつ歌っても良い歌だ";
				mes "君もそう思わないかい？";
				next;
				if(select("えぇ、良い歌ですね。","いいえ、別に……")==2) {
					cutin "job_bard_aiolo02",2;
					mes "[アイオルデ]";
					mes "おや……私の腕が落ちたか。";
					mes "もっと努力しなくてはね。";
					mes "とにかく、聞いてくれてありがとう。";
					break;
				}
				mes "[アイオルデ]";
				mes "ありがとう! 私の歌を";
				mes "気に入ってくれるとは嬉しいよ。";
				next;
				if(Sex == 1 && JobLevel >=40) {
					mes "[アイオルデ]";
					mes "もっとたくさんの人が";
					mes "歌ってくれたら良いのに……";
					mes "今のままでも、まぁ";
					mes "いいんだがね……ハッハー。";
					set CHANGE_BA,1;
				}
				else {
					mes "[アイオルデ]";
					mes "聞きたくなったら、またおいで。";
				}
				break;
			case 3:
				cutin "job_bard_aiolo02",2;
				mes "[アイオルデ]";
				mes "バードに歌を頼まないと言うかい？";
				mes "……うーん、忙しいのかい？";
				next;
				mes "[アイオルデ]";
				mes "狩りも良いが……";
				mes "少しは心に余裕を持ってはどうだい。";
				mes "今日という日は帰ってはこないものさ。";
				break;
		}
		close2;
		cutin "job_bard_aiolo01",255;
		end;
	case 1:
		mes "[アイオルデ]";
		mes "こんにちは！愉快なアーチャーの友よ";
		mes "私のような流れ者に何の用だい？";
		next;
		switch(select("良い声をしていますね。","歌を一曲聞かせてください。","特に用はない。")) {
			case 1:
				mes "[アイオルデ]";
				mes "ハッハー、君も";
				mes "良い声をしてるじゃないか。";
				mes "君も歌って見るつもりはないかい？";
				next;
				if(select("勿論です！","私が歌うなんて……")==1) {
					mes "[アイオルデ]";
					mes "ハッハー、いいね。うんうん。";
					mes "それでは、私がバードになる";
					mes "手伝いをしてあげよう。";
					next;
					mes "[アイオルデ]";
					mes "でもその前に……";
					mes "花を一輪持って来てくれないかい？";
					mes "花の香りを嗅げれば、";
					mes "良い歌が教えられそうだ。";
					next;
					mes "[アイオルデ]";
					mes "何の花でもいいんだが、";
					mes "私が好きな花を持って来てくれないかい";
					mes "出来れば売ってないような花がいいな。";
					set CHANGE_BA,2;
				}
				else {
					mes "[アイオルデ]";
					mes "ハッハー、怖がることはないよ。";
					mes "自分を過小評価しちゃいけないな。";
					next;
					mes "[アイオルデ]";
					mes "君には十分才能があるんだから";
					mes "気が変わったら、また尋ねておいで。";
				}
				break;
			case 2:
				mes "[アイオルデ]";
				mes "歌の素質がありそうなのに残念だ。";
				mes "歌は聞いてばかりじゃなくて、";
				mes "他の人に聞かせることも楽しいよ。";
				next;
				mes "[アイオルデ]";
				mes "村から村へ、歌って回るのも格別さ。";
				mes "君もバードとして生きてみないかい？";
				break;
			case 3:
				mes "[アイオルデ]";
				mes "うん……そうか。";
				mes "それはちょっと残念だ。";
				next;
				mes "[アイオルデ]";
				mes "それでは、またどこかで〜。";
				break;
		}
		close2;
		cutin "job_bard_aiolo01",255;
		end;
	case 2:
		mes "[アイオルデ]";
		mes "早かったね、友よ！";
		mes "花を持って来たかい？";
		mes "見せてくれないかい？";
		next;
		if(countitem(629)) { //歌う花
			mes "[アイオルデ]";
			mes "あ〜、これは歌う花じゃないか！";
			mes "私の思い出の花だ……";
			next;
			mes "[アイオルデ]";
			mes "私の友達だったジャイコフスキが";
			mes "好きだった花さ。";
			mes "今はどこで何をしているのだろう……";
			delitem 629,1;
		}
		else if(countitem(703)) { //ヒナレ
			mes "[アイオルデ]";
			mes "ああ……可愛いヒナレ…";
			mes "香りはないが素朴で可愛い花だね。";
			next;
			mes "[アイオルデ]";
			mes "ヒナレの葉は私が倒れた時に力をくれた";
			mes "本当に役立つ花だ。ありがとう。";
			delitem 703,1;
		}
		else if(countitem(704)) { //アロエ
			mes "[アイオルデ]";
			mes "アロエか……これは珍しいね。";
			mes "よく手に入れられたね！";
			next;
			mes "[アイオルデ]";
			mes "アロエの葉もおいしいが……";
			mes "やはり花が一番いいね。";
			delitem 704,1;
		}
		else if(countitem(708)) { //メント
			mes "[アイオルデ]";
			mes "メント……";
			mes "何か大切な事を思い出せる気がするよ…";
			mes "久しぶりに見れて、本当によかった。";
			next;
			mes "[アイオルデ]";
			mes "アンティペイメントを作れるらしいが…";
			mes "そうするには惜しいよね。ありがとう。";
			delitem 708,1;
		}
		else if(countitem(709)) { //イジドル
			mes "[アイオルデ]";
			mes "あー、これはイジドルだね。";
			mes "危ないけれど、とても美しい花だ……";
			next;
			mes "[アイオルデ]";
			mes "濃い紫色に、人を恍惚とさせる香り……";
			mes "気に入ったよ。ありがとう。";
			delitem 709,1;
		}
		else if(countitem(710)) { //幻想の花
			mes "[アイオルデ]";
			mes "やや、これは幻想の花！";
			mes "こんな貴重なものを持ってくるなんて！";
			next;
			mes "[アイオルデ]";
			mes "ああ……天国が目の前に見えるようだ。";
			mes "素晴らしい！本当に嬉しいよ！";
			delitem 710,1;
		}
		else if(countitem(748)) { //しおれないバラ
			mes "[アイオルデ]";
			mes "しおれないバラ…萎むことのないバラ。";
			mes "私ではなく恋人にあげればいいのに。";
			next;
			mes "[アイオルデ]";
			mes "私のような者が受け取っていいのかな。";
			mes "ハッハー、ありがとう。";
			delitem 748,1;
		}
		else if(countitem(749)) { //氷バラ
			mes "[アイオルデ]";
			mes "氷バラ……透明なバラよ。";
			mes "花とは言えないかもしれないけど、";
			mes "それでも美しいね……";
			next;
			mes "[アイオルデ]";
			mes "そうだね、香りが漂わなくても";
			mes "花なのかもしれないね。";
			mes "それでは受けとらせて頂くよ。";
			delitem 749,1;
		}
		else if(countitem(712)) { //花
			cutin "job_bard_aiolo02",2;
			mes "[アイオルデ]";
			mes "何だい、この平凡な花は。";
			mes "花は花だが…これでは駄目だね。";
			next;
			mes "[アイオルデ]";
			mes "プロンテラで少女が売ってるよね。";
			mes "他の花を持ってきてくれないかい。";
			close2;
			cutin "job_bard_aiolo02",255;
			end;
		}
		else if(countitem(744)) { //花たば
			mes "[アイオルデ]";
			mes "ほうほう、花束を持って来たね。";
			mes "これは私に持ってくる物ではないだろう";
			next;
			mes "[アイオルデ]";
			mes "お祝い物だからね、";
			mes "転職するノービス君にでも";
			mes "あげたらどうだい？";
			mes "僕には他の花を頼むよ。";
			close2;
			cutin "job_bard_aiolo01",255;
			end;
		}
		else if(countitem(745)) { //ブーケ
			cutin "job_bard_aiolo02",2;
			mes "[アイオルデ]";
			mes "おいおい……";
			mes "結婚式でもあげるつもりかい？";
			mes "男にブーケをあげてどうする。";
			next;
			mes "[アイオルデ]";
			mes "お似合いのお嬢さんにでも";
			mes "プレゼントしなよ。";
			mes "僕には他の花を頼むよ。";
			close2;
			cutin "job_bard_aiolo02",255;
			end;
		}
		else if(countitem(1032)) { //食人植物の花
			cutin "job_bard_aiolo02",2;
			mes "[アイオルデ]";
			mes "……どうしてこんな醜いものを";
			mes "持って来たんだい？";
			mes "もう少し考えてくれなくちゃ……";
			next;
			mes "[アイオルデ]";
			mes "笑わせようとでもしたのかい？";
			mes "もう少しまともな花を準備してくれよ。";
			close2;
			cutin "job_bard_aiolo02",255;
			end;
		}
		else if(countitem(2207)) { //装飾用花
			mes "[アイオルデ]";
			mes "うーん……これは装飾用花だね。";
			mes "花は花だが…これでは駄目だね。";
			next;
			mes "[アイオルデ]";
			mes "私は美しくて香しい花が好きなんだ。";
			mes "飾るためだけの花は嫌いだよ。";
			close2;
			cutin "job_bard_aiolo01",255;
			end;
		}
		else {
			cutin "job_bard_aiolo02",2;
			mes "[アイオルデ]";
			mes "うん？何も持ってないじゃないか。";
			mes "花を持って来てと言わなかったかな。";
			next;
			mes "[アイオルデ]";
			mes "一人で学びたいなら、それでもいいさ。";
			mes "歌うことは自由なんだから。";
			close2;
			cutin "job_bard_aiolo02",255;
			end;
		}
		next;
		mes "[アイオルデ]";
		mes "それじゃあ約束どおり、";
		mes "バードになる手伝いをしなくてはね。";
		mes "しかし簡単ではないぞ。ハッハー！";
		next;
		mes "[アイオルデ]";
		mes "歌を学ぶためには";
		mes "多くの人と触れ合うことが大切だ。";
		mes "あちこちの村のさまざまな事件も";
		mes "見聞きしなければならないし……";
		next;
		mes "[アイオルデ]";
		mes "そうだ、ルティエという村に";
		mes "喋る雪だるまがいるんだ。";
		mes "その村へ行って、";
		mes "贈り物をもらって来るように。いいね。";
		next;
		mes "[アイオルデ]";
		mes "スノウノウと友達になれば、";
		mes "何か貰うことができるだろう。";
		mes "村の人達とも、たくさん言葉を";
		mes "交わしてきてみてくれないかい？";
		set CHANGE_BA,3;
		set LUTIE_1QUE,1;
		close2;
		cutin "job_bard_aiolo01",255;
		end;
	case 3:
		if(LUTIE_1QUE < 11) { //npc_town_lutie.txtにて発行
			mes "[アイオルデ]";
			mes "そうか、彼と友達になることは";
			mes "出来なかったか。";
			mes "言葉だけでは駄目なこともあるんだよ。";
			next;
			mes "[アイオルデ]";
			mes "スノウノウと友達になれば、";
			mes "何か貰うことができるだろう。";
			mes "村の人達とも、たくさん言葉を";
			mes "交わしてきてみてくれないかい？";
			close2;
			cutin "job_bard_aiolo01",255;
			end;
		}
		mes "[アイオルデ]";
		mes "おかえり。";
		mes "たくさん人に会って来たかい？";
		mes "贈り物よりもっと大切なことが";
		mes "あるってわかったね。";
		next;
		mes "[アイオルデ]";
		mes "それでは、歌を歌って見るか……";
		mes "私がお手本を歌ってみるから……";
		mes "習って歌ってみてくれたまえ。";
		next;
		mes "[アイオルデ]";
		mes "始めようか";
		mes "フムフム、ホッホッ";
		mes "1、2、3、4〜";
		next;
		switch(rand(5)) {
		case 0: //ニヨルドの歌
			setarray '@list$,"お金がなかった商人さん","装備もなかった商人さん","なんとか元手を作ったよ","服と刀を売ったのさ","最初は赤ポーションを","プロンテラで芋も売ったと言うよ";
			break;
		case 1: //戦場の歌
			setarray '@list$,"もっと大きく鳴らすがいい！","進む兵等よ、力を込めよ！","空を仰ぎ、大地を踏み締めろ！","裂けそうな心が駆け出すように！","ほら笛を城壁に轟かせよ！","今日と言う日は二度と来なかろう！";
			break;
		case 2: //ジークフリードの歌
			setarray '@list$,"不死身と呼ばれた","男がいた","その名を「ジークフリード」","英雄「ジークムント」の息子","邪悪な巨人「ファフニール」","竜に変わって彼を阻んだ";
			break;
		case 3: //ブラギの歌
			setarray '@list$,"ブラギ、ブラギ","永遠の詩人の名を呼びなさい","我が歌は彼の息づかい","我が心は彼の意志","吟遊詩人は彼の化身","すべての歌は彼に帰るでしょう";
			break;
		case 4: //イドゥンの歌
			setarray '@list$,"美しき女神イドゥン","美しくて白い豊穣の女神よ","オーディンの息子ブラギの妻","彼女の籠いっぱい盛られた林檎","彼女の甘〜い林檎のおかげだろう","神々は永遠に老いることがない";
			break;
		}
		for(set '@i,0; '@i<6; set '@i,'@i+1) {
			for(set '@j,0; '@j<6; set '@j,'@j+1)
				mes (('@count<'@j)? "^000000": "^3173FF") +'@list$['@j]+ "^000000";
			next;
			input '@word$;
			if('@word$=='@list$['@i])
				set '@point,'@point+1;
		}
		if('@point<6) {
			cutin "job_bard_aiolo02",2;
			mes "[アイオルデ]";
			mes "おやおや……歌詞が違うじゃないか。";
			mes "きちんと歌ってくれなくてはね……";
			next;
			mes "[アイオルデ]";
			mes "発音がまだまだのようだね。";
			mes "次は、もっときちんと声を出すように。";
			close2;
			cutin "job_bard_aiolo02",255;
			end;
		}
		mes "……";
		next;
		mes "[アイオルデ]";
		mes "立派だ！凄いじゃないか！";
		mes "これなら、良いバードになれるだろう！";
		next;
		mes "[アイオルデ]";
		mes "それでは…";
		mes "これからバードになるといい。";
		mes "その前に記念になるものを上げたいな。";
		next;
		mes "[アイオルデ]";
		mes "このまま転職するかい？";
		mes "それとも何か欲しいかい？";
		next;
		if(select("このまま転職します。","贈り物が欲しいです。")==2) {
			mes "[アイオルデ]";
			mes "うん…そうだね。";
			mes "^3173FF木屑^000000を手に入れて来てくれないかい。";
			mes "なんでも構わないから、";
			mes "同じ種類で60個頼むよ。";
			next;
			mes "[アイオルデ]";
			mes "持って来てくれたら、";
			mes "記念品を一つあげるよ。";
			mes "じゃ、いってらっしゃい。";
			set CHANGE_BA,4;
			close2;
			cutin "job_bard_aiolo01",255;
			end;
		}
	case 4:
		if(SkillPoint) {
			mes "[アイオルデ]";
			mes "おや…まだスキルポイントが";
			mes "残っているようだね。";
			mes "全部使いきってから来てくれたまえ。";
			next;
			mes "[アイオルデ]";
			mes "では記念品を作りたいから、そうだね…";
			mes "^3173FF木屑^000000を集めてきて貰えないかな。";
			mes "なんでも構わないから、";
			mes "同じ種類で60個頼むよ。";
			set CHANGE_BA,4;
			close2;
			cutin "job_bard_aiolo01",255;
			end;
		}
		if(CHANGE_BA == 3) break;
		if(countitem(1066) >= 60) {
			set '@trunkid,1066;
			set '@itemid,(JobLevel >= 50)? 1910: 1905;
		}
		else if(countitem(1067) >= 60) {
			set '@trunkid,1067;
			set '@itemid,1903;
		}
		else if(countitem(1068) >= 60) {
			set '@trunkid,1068;
			set '@itemid,1903;
		}
		else if(countitem(1019) >= 60) {
			set '@trunkid,1019;
			set '@itemid,1901;
		}
		else {
			mes "[アイオルデ]";
			mes "うーん、^3173FF木屑^000000を集めるつもりだったのに";
			mes "このまま転職することにしたのかい？";
			next;
			if(select("このまま転職します。","いいえ、集めてきます。")==2) {
				mes "[アイオルデ]";
				mes "うん…そうだね。";
				mes "^3173FF木屑^000000を手に入れて来てくれないかい。";
				mes "なんでも構わないから、";
				mes "同じ種類で60個頼むよ。";
				next;
				mes "[アイオルデ]";
				mes "持って来てくれたら、";
				mes "記念品を一つあげるよ。";
				mes "じゃ、いってらっしゃい。";
				close2;
				cutin "job_bard_aiolo01",255;
				end;
			}
			break;
		}
		mes "[アイオルデ]";
		mes "やぁ、お疲れ様。";
		mes "早速、転職記念品を作ってあげるよ。";
		mes "少しの間だけ待っててくれないかい。";
		next;
		mes "^3173FFギーギー、トンタン、トンタン";
		mes "カンカン、キーコキーコ^000000";
		next;
		delitem '@trunkid,60;
		getitem '@itemid,1;
		unequip;
		jobchange Job_Bard;
		set CHANGE_BA,0;
		mes "[アイオルデ]";
		mes "さあ、記念品だ。";
		mes "歌う時に使ってくれないかい。";
		mes "これで楽しく歌って欲しい。";
		mes "じゃあ、また会おう！";
		close2;
		cutin "job_bard_aiolo01",255;
		end;
	}
	unequip;
	jobchange Job_Bard;
	set CHANGE_BA,0;
	mes "[アイオルデ]";
	mes "そう！楽しく歌っていこうか。";
	mes "風のように雲のようにね！";
	next;
	mes "[アイオルデ]";
	mes "じゃあ、また会おう！";
	close2;
	cutin "job_bard_aiolo01",255;
	end;
}
